
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаРаспределения = ПолучитьИзВременногоХранилища(Параметры.Адрес);
	ТаблицаЗаявок = ТаблицаРаспределения.Скопировать(,"Заявка");
	ТаблицаЗаявок.Свернуть("Заявка");
	
	Макет = Обработки.РаспределениеЗаявокПоСчетам.ПолучитьМакет("РезультатРаспределения");
	ОбластьЗаявка = Макет.ПолучитьОбласть("СтрокаЗаявки");
	ОбластьЭтап = Макет.ПолучитьОбласть("СтрокаЭтапа");
	
	РезультатРаспределения.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	РезультатРаспределения.НачатьАвтогруппировкуСтрок();
	Для Каждого Заявка из ТаблицаЗаявок Цикл
		ОбластьЗаявка.Параметры.Заполнить(Заявка);
		ВыведеннаяОбласть = РезультатРаспределения.Вывести(ОбластьЗаявка, 1);
		Если ТаблицаРаспределения.НайтиСтроки(Новый Структура("Заявка, ЭтоОшибка", Заявка.Заявка, Истина)).Количество() Тогда
			РезультатРаспределения.Область(ВыведеннаяОбласть.Верх, 2, ВыведеннаяОбласть.Верх, 6).ЦветТекста = WebЦвета.ТемноКрасный;
		Иначе
			РезультатРаспределения.Область(ВыведеннаяОбласть.Верх, 2, ВыведеннаяОбласть.Верх, 6).ЦветТекста = WebЦвета.ТемноЗеленый;
		КонецЕсли;
		НайденныеСтроки = ТаблицаРаспределения.НайтиСтроки(Новый Структура("Заявка", Заявка.Заявка));
		Для Каждого НайденнаяСтрока из НайденныеСтроки Цикл
			ОбластьЭтап.Параметры.Заполнить(НайденнаяСтрока);
			ВыведеннаяОбласть = РезультатРаспределения.Вывести(ОбластьЭтап, 2);
			Если НайденнаяСтрока.ЭтоОшибка Тогда
				РезультатРаспределения.Область(ВыведеннаяОбласть.Верх, 2, ВыведеннаяОбласть.Верх, 6).ЦветТекста = WebЦвета.ТемноКрасный;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	РезультатРаспределения.ЗакончитьАвтогруппировкуСтрок();
	
КонецПроцедуры
