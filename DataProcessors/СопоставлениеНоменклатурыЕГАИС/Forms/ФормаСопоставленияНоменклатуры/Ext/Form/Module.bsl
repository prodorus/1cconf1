
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "АлкогольнаяПродукция, Номенклатура, ИдентификаторУпаковки");
	
	ИспользуютсяХарактеристики = Номенклатура.ВестиУчетПоХарактеристикам;
	
	УстановитьВидимостьЭлементовФормы();
	
	ЗаполнитьСписокВыбораЕдиницИзмерений();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ИспользуютсяХарактеристики Тогда
		ПроверяемыеРеквизиты.Добавить("Характеристика");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьПредупреждения() Тогда
		РезультатВопроса = Вопрос(НСтр("ru = 'Между связываемыми элементами есть расхождения. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
		ЗаписатьСоответствие_Завершение(РезультатВопроса, Неопределено);
	Иначе
		ЗаписатьСоответствие_Завершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьСоответствиеНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоответствиеНаСервере()
	
	Запись = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ЭтаФорма);
	Запись.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЕдиницИзмерений()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АлкогольнаяПродукция", АлкогольнаяПродукция);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТТНВходящаяЕГАИСТовары.ИдентификаторУпаковки
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция = &АлкогольнаяПродукция
	|	И ТТНВходящаяЕГАИСТовары.ИдентификаторУпаковки <> """"
	|	И ТТНВходящаяЕГАИСТовары.Ссылка.СтатусОбработки В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.ПринятИзЕГАИС), ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.ПустаяСсылка))";
	
	Элементы.ИдентификаторУпаковки.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторУпаковки"));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕдиницыИзмерения.Ссылка
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Элементы.Упаковка.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.Характеристика.Видимость = ИспользуютсяХарактеристики;
	Элементы.ДекорацияХарактеристика.Видимость = ИспользуютсяХарактеристики;
	
	Элементы.ПредупреждениеОбъем.Видимость = Номенклатура.ОбъемДАЛ <> 0 И Номенклатура.ОбъемДАЛ <> АлкогольнаяПродукция.Объем / 10;
	Элементы.ПредупреждениеКрепость.Видимость = Номенклатура.Крепость <> 0 И Номенклатура.Крепость <> АлкогольнаяПродукция.Крепость;
	Элементы.ПредупреждениеВидПродукции.Видимость = Номенклатура.ВидАлкогольнойПродукцииЕГАИС <> АлкогольнаяПродукция.ВидПродукции;
	
	// Производителя/импортера номенклатуры ЕГАИС сопоставляем с реквизитами "Производитель"/"Импортер" нашей номенклатуры "один-в-один".
	
	// Если в номенклатуре ЕГАИС производитель/импортер не указан, значит им про него ничего не известно и мы у себя можем указывать любого.
	
	// Если в номенклатуре ЕГАИС производитель/импортер заполнен, то мы у себя должны использовать контрагента, сопоставленного с тем
	// который указан в номенклатуре ЕГАИС.
	
	Элементы.ПредупреждениеПроизводитель.Видимость = НЕ АлкогольнаяПродукция.Производитель.Пустая()
														И НЕ ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Производитель) = Номенклатура.Производитель;
	Элементы.ПредупреждениеИмпортер.Видимость = НЕ АлкогольнаяПродукция.Импортер.Пустая()
													И НЕ ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Импортер) = Номенклатура.Импортер;
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьПредупреждения()
	
	Возврат Элементы.ПредупреждениеОбъем.Видимость
		ИЛИ Элементы.ПредупреждениеКрепость.Видимость
		ИЛИ Элементы.ПредупреждениеВидПродукции.Видимость
		ИЛИ Элементы.ПредупреждениеПроизводитель.Видимость
		ИЛИ Элементы.ПредупреждениеИмпортер.Видимость;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтрагентаПоСоответствию(ОрганизацияЕГАИС)
	
	Если ОрганизацияЕГАИС.Пустая() Тогда
		Возврат Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеОрганизацийЕГАИС.Контрагент
	|ИЗ
	|	РегистрСведений.СоответствиеОрганизацийЕГАИС КАК СоответствиеОрганизацийЕГАИС
	|ГДЕ
	|	СоответствиеОрганизацийЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Контрагент;
	КонецЕсли;
	
КонецФункции