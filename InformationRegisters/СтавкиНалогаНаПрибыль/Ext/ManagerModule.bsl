// Формирование сообщения о необходимости проведения рег.операции "Закрытие года",
// в случае изменения ставки налога на прибыль
//
Процедура СообщитьОбИзмененииСтавки(ВходящиеДанные = Неопределено) Экспорт
	
	Если ВходящиеДанные = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
	ДатаРеформации = НачалоДня(ВходящиеДанные.Период) - 1;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаРеформации",    ДатаРеформации);
	Запрос.УстановитьПараметр("ГраницаРеформации", Новый Граница(ДатаРеформации, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",       ВходящиеДанные.Организация);
	
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеАктивы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеОбязательства);
	Запрос.УстановитьПараметр("Счета", Массив);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитикаОрганизаций
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаРеформации, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних
	|ГДЕ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.ПоддержкаПБУ18
	|	И УчетнаяПолитикаОрганизацийСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстатки.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ХозрасчетныйОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаРеформации,
	|			Счет В ИЕРАРХИИ (&Счета),
	|			,
	|			Организация = &Организация
	|				И Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ХозрасчетныйОстатки.Организация КАК Организация,
	|	ВТ_ХозрасчетныйОстатки.Организация.НаименованиеСокращенное КАК СокращенноеНаименованиеОрганизации
	|ИЗ
	|	ВТ_ХозрасчетныйОстатки КАК ВТ_ХозрасчетныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитикаОрганизаций КАК ВТ_УчетнаяПолитикаОрганизаций
	|		ПО ВТ_ХозрасчетныйОстатки.Организация = ВТ_УчетнаяПолитикаОрганизаций.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СокращенноеНаименованиеОрганизации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ШаблонСообщения = НСтр("ru = 'Изменена ставка налога на прибыль.
		|Рекомендуется выполнить регламентную операцию ""Закрытие года""
		|за декабрь %1 года для организации:
		|%2'");
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонСообщения, Формат(Год(ДатаРеформации), "ЧГ="),
		СокрЛП(Выборка.СокращенноеНаименованиеОрганизации));
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

// Заполняет ставки налога на прибыль, введенные на период с 2017 по 2020 годы (401-ФЗ)
// Если в каком-то году введены "нестандартные" ставки, то обновления настроек не происходит
Процедура ЗаполнитьСтавкиНалогаНаПрибыльС2017По2020Годы() Экспорт
			
	НачалоПериода = '2017-01-01';
	КонецПериода  = КонецДня('2020-12-31');
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	
	// Выбираем "стандартные" настройки ставок налога на прибыль (2% и 18%) с целью их дальнейшей перезаписи с новыми значениями
	// (1) Выбираем установленные "стандартные" настройки налога на прибыль за период с 2017 по 2020 годы
	// (2) Если на 2017 год настройка не устанавливалась, то проверяем действующие настройки на начало 2017 года
	
	// Вторая таблица запроса выбирает записи в срез последних только в том случае,
	// если последняя запись ранее 2017 года (см. условие "ГДЕ НастройкиСрезПоследних.Период < &НачалоПериода").
	// В этом случае первый запрос объединения заведомо не вернет запись за 2017 год (ее не будет в базе)
	
	// Все выбранные таким образом настройки подлежат изменению - см. ниже
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетнаяПолитикаНалоговыйУчет.Организация
	|ПОМЕСТИТЬ ПлательщикиНалогаНаПрибыль
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&НачалоПериода, ) КАК УчетнаяПолитикаНалоговыйУчет
	|ГДЕ
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаНалоговыйУчет.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	УчетнаяПолитикаНалоговыйУчет.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Настройки.Организация КАК Организация,
	|	Настройки.Период КАК Период
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаПрибыль КАК Настройки
	|ГДЕ
	|	Настройки.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Настройки.СтавкаФБ = 2
	|	И Настройки.СтавкаСубъектРФ = 18
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиСрезПоследних.Организация,
	|	&НачалоПериода
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаПрибыль.СрезПоследних(
	|			&НачалоПериода,
	|			Организация В
	|				(ВЫБРАТЬ
	|					ПлательщикиНалогаНаПрибыль.Организация
	|				ИЗ
	|					ПлательщикиНалогаНаПрибыль КАК ПлательщикиНалогаНаПрибыль)) КАК НастройкиСрезПоследних
	|ГДЕ
	|	НастройкиСрезПоследних.Период < &НачалоПериода
	|	И НастройкиСрезПоследних.СтавкаФБ = 2
	|	И НастройкиСрезПоследних.СтавкаСубъектРФ = 18";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаНастройки = РезультатЗапроса.Выбрать();
	
	Набор = РегистрыСведений.СтавкиНалогаНаПрибыль.СоздатьНаборЗаписей();
	
	Пока ВыборкаНастройки.Следующий() Цикл
		Набор.Очистить();
		Набор.Отбор.Период.Установить(ВыборкаНастройки.Период);
		Набор.Отбор.Организация.Установить(ВыборкаНастройки.Организация);
		
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаНастройки);
		
		Запись.СтавкаФБ        = 3;
		Запись.СтавкаСубъектРФ = 17;
		
		ОбщегоНазначения.ЗаписатьНабор(Набор)
	КонецЦикла;
		
КонецПроцедуры

