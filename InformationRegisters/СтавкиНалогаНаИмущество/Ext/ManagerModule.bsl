
Процедура ОбновитьЗаписиСтавкаНалогаДвижимогоИмуществаС2018() Экспорт
	
	НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмущество.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	ЗаписиКУдалению = Новый Массив;
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Не ЗначениеЗаполнено(Запись.Организация) Тогда
			ЗаписиКУдалению.Добавить(Запись);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяЗапись Из ЗаписиКУдалению Цикл
		НаборЗаписей.Удалить(УдаляемаяЗапись);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НаборЗаписей[0].НалоговаяСтавкаДвижимоеИмущество) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
	КоличествоЗаписей = НаборЗаписей.Количество();
	
	ИндексМаксимальнойЗаписиДо2018 = Новый Соответствие;
	Для Сч = 0 По КоличествоЗаписей - 1 Цикл
		
		Запись = НаборЗаписей[Сч];
		
		Если Не ЗначениеЗаполнено(Запись.НалоговаяСтавка) Тогда
			Запись.НалоговаяСтавка = 2.2;
		КонецЕсли;
		
		Если Запись.Период < '2018-01-01' И Запись.Активность Тогда
			ИндексМаксимальнойЗаписиДо2018.Вставить(Запись.Организация, Сч);
			Запись.НалоговаяСтавкаДвижимоеИмущество = Запись.НалоговаяСтавка;
			Запись.ОсвобождениеОтНалогообложенияДвижимогоИмущества = Истина;
			
		ИначеЕсли Запись.Период >= '2018-01-01' И Запись.Активность Тогда
			ИндексМаксимальнойЗаписиДо2018.Вставить(Запись.Организация, Неопределено);
			Запись.НалоговаяСтавкаДвижимоеИмущество = 1.1;
			
		КонецЕсли;
		
		Если Запись.СнижениеНалоговойСтавки Тогда
			Запись.НалоговаяСтавка = Запись.СниженнаяНалоговаяСтавка;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючЗначение Из ИндексМаксимальнойЗаписиДо2018 Цикл
		Если КлючЗначение.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, НаборЗаписей[КлючЗначение.Значение]);
		НоваяЗапись.Период = '2018-01-01';
		НоваяЗапись.НалоговаяСтавкаДвижимоеИмущество = 1.1;
		НоваяЗапись.ОсвобождениеОтНалогообложенияДвижимогоИмущества = Ложь;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

