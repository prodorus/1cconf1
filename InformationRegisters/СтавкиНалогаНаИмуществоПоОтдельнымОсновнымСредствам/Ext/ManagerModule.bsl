// Процедура заполняет новый реквизит "Вид имущества"
//
Процедура ЗаполнитьВидИмущества() Экспорт
	
	НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	ТекстСообщения = "Не удалось установить виды имущества для объектов";
	Для каждого СтрокаНабора Из НаборЗаписей Цикл
		Если СтрокаНабора.УдалитьВходитВСоставЕСГС Тогда
			СтрокаНабора.УдалитьВидИмущества = Перечисления.ВидыИмущества.ВходитВСоставЕСГС;
			ВидИмущества = "Входит в состав единой системы газоснабжения (ЕСГС)";
		Иначе
			СтрокаНабора.УдалитьВидИмущества = Перечисления.ВидыИмущества.ОсобенностиОтсутствуют;
			ВидИмущества = "Особенности отсутствуют";
		КонецЕсли;
		
		ТекстСообщения = ТекстСообщения + "
			|" + СтрокаНабора.ОсновноеСредство + " - " + ВидИмущества;
		
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

// Процедура заполняет новый реквизит "Налоговая база"
//
Процедура ЗаполнитьПорядокОпределенияНалоговойБазы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
	
		НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		
		Для каждого Запись Из НаборЗаписей Цикл
			
			Если НЕ ЗначениеЗаполнено(Запись.НалоговаяБаза) Тогда
				Запись.НалоговаяБаза = Перечисления.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Ставки налога на имущество по отдельным основным средствам""'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
		
КонецПроцедуры

// Процедура заполняет новый реквизит "Код вида имущества" регистра сведений СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
//
Процедура ЗаполнитьКодВидаИмущества() Экспорт

	НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	ТребуетсяЗапись = Ложь;
	
	Для каждого СтрокаНабора Из НаборЗаписей Цикл
		
		Если ЗначениеЗаполнено(СтрокаНабора.КодВидаИмущества) Тогда
			Продолжить;
		КонецЕсли;
		
		ТребуетсяЗапись = Истина;
		
		Если СтрокаНабора.НалоговаяБаза = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость Тогда
			СтрокаНабора.КодВидаИмущества = "11";
		ИначеЕсли СтрокаНабора.ДоляСтоимостиЧислитель <> 0 И СтрокаНабора.ДоляСтоимостиЗнаменатель <> 0 Тогда
			СтрокаНабора.КодВидаИмущества = "02";
		ИначеЕсли СтрокаНабора.УдалитьВидИмущества = Перечисления.ВидыИмущества.ВходитВСоставЕСГС Тогда
			СтрокаНабора.КодВидаИмущества = "01";
		ИначеЕсли СтрокаНабора.УдалитьВидИмущества = Перечисления.ВидыИмущества.НаходитсяНаТерриторииДругогоГосударства Тогда
			СтрокаНабора.КодВидаИмущества = "04";
		ИначеЕсли СтрокаНабора.УдалитьВидИмущества = Перечисления.ВидыИмущества.ИспользуетсяВОЭЗКалининградскойОбласти Тогда
			СтрокаНабора.КодВидаИмущества = "05";
		Иначе
			СтрокаНабора.КодВидаИмущества = "03";
		КонецЕсли;
		
		СтрокаНабора.УдалитьВидИмущества = Неопределено;

	КонецЦикла;
	
	Если Не ТребуетсяЗапись Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ТекстСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Ставки налога на имущество по отдельным основным средствам""'");
		ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

КонецПроцедуры

// Процедура переносит реквизиты КадастровыйНомер, КадастровыйНомерПомещения, УсловныйНомер из справочника ОсновныеСредства
// в регистр сведений СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
//
Процедура ЗаполнитьКадастровыйУсловныйНомер() Экспорт

	НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	ТребуетсяЗапись = Ложь;
	ОсновныеСредства = Новый Массив;
	
	Для каждого СтрокаНабора Из НаборЗаписей Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаНабора.ОсновноеСредство) Тогда
			Продолжить;
		КонецЕсли;
		
		КадастровыйНомер = СокрЛП(СтрокаНабора.ОсновноеСредство.УдалитьКадастровыйНомер);
		УсловныйНомер = СокрЛП(СтрокаНабора.ОсновноеСредство.УдалитьУсловныйНомер);
		
		Если ПустаяСтрока(КадастровыйНомер) И ПустаяСтрока(УсловныйНомер) Тогда
			Продолжить;
		КонецЕсли;
		
		ТребуетсяЗапись = Истина;
		
		Если СтрокаНабора.ОсновноеСредство.УдалитьПомещение Тогда
			СтрокаНабора.КадастровыйНомерПомещения = КадастровыйНомер;
		Иначе
			СтрокаНабора.КадастровыйНомер = КадастровыйНомер;
		КонецЕсли;
		СтрокаНабора.УсловныйНомер = УсловныйНомер;
		
		ОсновныеСредства.Добавить(СтрокаНабора.ОсновноеСредство);
		
	КонецЦикла;
	
	Если ТребуетсяЗапись Тогда
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Ставки налога на имущество по отдельным основным средствам""'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	Для Каждого ОсновноеСредство Из ОсновныеСредства Цикл
		
		ОсновноеСредствоОбъект = ОсновноеСредство.ПолучитьОбъект();
		
		ОсновноеСредствоОбъект.УдалитьКадастровыйНомер    = Неопределено;
		ОсновноеСредствоОбъект.УдалитьНазначениеПомещения = Неопределено;
		ОсновноеСредствоОбъект.УдалитьПомещение           = Неопределено;
		ОсновноеСредствоОбъект.УдалитьУсловныйНомер       = Неопределено;
		
		Попытка
			ОсновноеСредствоОбъект.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не выполнено обновление справочника ""Основные средства""'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Недвижимость = Новый Массив;
	Недвижимость.Добавить(Перечисления.ГруппыОС.Здания);
	Недвижимость.Добавить(Перечисления.ГруппыОС.Сооружения);
	
	Запрос.УстановитьПараметр("Недвижимость", Недвижимость);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияОС.ПринятоКУчету);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияОСОрганизаций.ОсновноеСредство,
	|	СостоянияОСОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА НЕ СостоянияОСОрганизаций.ОсновноеСредство.УдалитьПомещение
	|			ТОГДА СостоянияОСОрганизаций.ОсновноеСредство.УдалитьКадастровыйНомер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КадастровыйНомер,
	|	ВЫБОР
	|		КОГДА СостоянияОСОрганизаций.ОсновноеСредство.УдалитьПомещение
	|			ТОГДА СостоянияОСОрганизаций.ОсновноеСредство.УдалитьКадастровыйНомер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КадастровыйНомерПомещения,
	|	СостоянияОСОрганизаций.ОсновноеСредство.УдалитьУсловныйНомер КАК УсловныйНомер
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|ГДЕ
	|	СостоянияОСОрганизаций.ОсновноеСредство.ГруппаОС В(&Недвижимость)
	|	И СостоянияОСОрганизаций.Состояние = &Состояние";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	ТребуетсяЗапись = Ложь;
	ОсновныеСредства.Очистить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ПустаяСтрока(Выборка.КадастровыйНомер)
		   И ПустаяСтрока(Выборка.КадастровыйНомерПомещения)
		   И ПустаяСтрока(Выборка.УсловныйНомер) Тогда
			Продолжить;
		КонецЕсли;
		
		ТребуетсяЗапись = Истина;
		ОсновныеСредства.Добавить(Выборка.ОсновноеСредство);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		
		НоваяЗапись.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации;
		НоваяЗапись.НалоговаяБаза = Перечисления.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость;
		НоваяЗапись.КодВидаИмущества = "03";
		НоваяЗапись.ПорядокНалогообложения = Перечисления.ПорядокНалогообложенияИмущества.ОсобыеЛьготыНеУстановлены;
		
	КонецЦикла;
	
	Если ТребуетсяЗапись Тогда
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Ставки налога на имущество по отдельным основным средствам""'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	Для Каждого ОсновноеСредство Из ОсновныеСредства Цикл
		
		ОсновноеСредствоОбъект = ОсновноеСредство.ПолучитьОбъект();
		
		ОсновноеСредствоОбъект.УдалитьКадастровыйНомер    = Неопределено;
		ОсновноеСредствоОбъект.УдалитьНазначениеПомещения = Неопределено;
		ОсновноеСредствоОбъект.УдалитьПомещение           = Неопределено;
		ОсновноеСредствоОбъект.УдалитьУсловныйНомер       = Неопределено;
		
		Попытка
			ОсновноеСредствоОбъект.Записать();
		Исключение
			ТекстСообщения	= НСтр("ru = 'Не выполнено обновление справочника ""Основные средства""'");
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
