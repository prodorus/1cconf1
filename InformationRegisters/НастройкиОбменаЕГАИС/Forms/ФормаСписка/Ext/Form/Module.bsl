
//#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьТекущееРабочееМесто();
	
	Элементы.ОбменНаСервере.Видимость = НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
	                                  И НЕ ОбщегоНазначения.РазделениеВключено();
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(РабочееМесто) Тогда
		МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента();
		УстановитьТекущееРабочееМесто();
	КонецЕсли;
	
	УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПараметрыПодключенияЕГАИС" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_КлассификаторОрганизацийЕГАИС" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

//#КонецОбласти

//#Область ОбработчикиСобытийЭлементовФормы

//#КонецОбласти

//#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьВсеРабочиеМеста(Команда)
	
	ПоказатьВсеРабочиеМеста = НЕ ПоказатьВсеРабочиеМеста;
	
	Элементы.ФормаПоказатьВсеРабочиеМеста.Пометка = ПоказатьВсеРабочиеМеста;
	
	Если ПоказатьВсеРабочиеМеста Тогда
		УдалитьОтборПоРабочемуМесту();
	Иначе
		УстановитьОтборПоРабочемуМесту();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Форма)
	
	Если Форма.ПоказатьВсеРабочиеМеста Тогда
		Форма.АвтоЗаголовок = Истина;
		Форма.Заголовок = "";
	Иначе
		Форма.АвтоЗаголовок = Ложь;
		Форма.Заголовок = НСтр("ru = 'Настройки обмена с ЕГАИС:'") + " " + Строка(Форма.РабочееМесто);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОтборПоРабочемуМесту()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "РабочееМесто");
	
	УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоРабочемуМесту()
	
	РабочиеМеста = Новый Массив;
	РабочиеМеста.Добавить(РабочееМесто);
	РабочиеМеста.Добавить(Справочники.РабочиеМеста.ПустаяСсылка());
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"РабочееМесто",
		РабочиеМеста,
		ВидСравненияКомпоновкиДанных.ВСписке,
		НСтр("ru = 'Доступная настройка обмена'"),
		Истина);
	
	УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущееРабочееМесто()
	
	РабочееМесто = ПараметрыСеанса.РабочееМестоКлиента;
	
	УстановитьОтборПоРабочемуМесту();
	
КонецПроцедуры

//#КонецОбласти