#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//#Область Серии

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС);
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект	 - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
// 
// Возвращаемое значение:
//  Структура - состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС, Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ТекстЗапросаЗаполненияСтатусовУказанияСерий(Метаданные.РегистрыСведений.СоответствиеНоменклатурыЕГАИС, ПараметрыУказанияСерий);

КонецФункции

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

//#Область ОбновлениеИнформационнойБазы

Функция ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Серия КАК Серия,
	|	СоответствиеНоменклатурыЕГАИС.Справка2 КАК Справка2,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.УдалитьАлкогольнаяПродукция <> ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(ТаблицаИзменений) Экспорт
	
	НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
		
	Для каждого ЗаписьРегистра Из НаборЗаписей Цикл
		
		Если Не ЗначениеЗаполнено(ЗаписьРегистра.АлкогольнаяПродукция) Тогда
			ЗаписьРегистра.АлкогольнаяПродукция = ЗаписьРегистра.УдалитьАлкогольнаяПродукция;
			ЗаписьРегистра.УдалитьАлкогольнаяПродукция = Неопределено;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЗаписьРегистра.Номенклатура) Тогда
			ЗаписьРегистра.Номенклатура = ЗаписьРегистра.УдалитьНоменклатура;
			ЗаписьРегистра.УдалитьНоменклатура = Неопределено;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЗаписьРегистра.Характеристика) Тогда
			ЗаписьРегистра.Характеристика = ЗаписьРегистра.УдалитьХарактеристика;
			ЗаписьРегистра.УдалитьХарактеристика = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
		
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

Функция ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсиюЗаполнениеСправок2() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюЗаполнениеСправок2(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 КАК Справка2,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2.ДокументОснование КАК Справка2ДокументОснование
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.Остатки КАК ОстаткиАлкогольнойПродукцииЕГАИСОстатки
	|ГДЕ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.КоличествоОстаток > 0
	|	И ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2.ДокументОснование <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтОстатки.Справка2 КАК Справка2,
	|	ТИПЗНАЧЕНИЯ(ВтОстатки.Справка2ДокументОснование) КАК ТипДокумента,
	|	ВтОстатки.Справка2ДокументОснование КАК ДокументОснование
	|ИЗ
	|	ВтОстатки КАК ВтОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ВтОстатки.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|			И ВтОстатки.Справка2 = СоответствиеНоменклатурыЕГАИС.Справка2
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Справка2 ЕСТЬ NULL
	|ИТОГИ ПО
	|	ВтОстатки.Справка2ДокументОснование
	|";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокументы.Следующий() Цикл
		
		МассивСправок2 = Новый Массив;
		ВыборкаСправка2 = ВыборкаДокументы.Выбрать();
		Пока ВыборкаСправка2.Следующий() Цикл
			МассивСправок2.Добавить(ВыборкаСправка2.Справка2);
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ДокументЕГАИС.Номенклатура КАК Номенклатура,
		|	ДокументЕГАИС.Характеристика КАК Характеристика,
		|	ДокументЕГАИС.Серия КАК Серия,
		|	ДокументЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ДокументЕГАИС.Справка2 КАК Справка2,
		|	ДокументЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	Документ.ТТНВходящаяЕГАИС.Товары КАК ДокументЕГАИС
		|ГДЕ
		|	ДокументЕГАИС.Ссылка = &ДокументОснование
		|	И ДокументЕГАИС.Справка2 В(&МассивСправок2)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументЕГАИС.Номенклатура,
		|	ДокументЕГАИС.Характеристика,
		|	ДокументЕГАИС.Серия,
		|	ДокументЕГАИС.АлкогольнаяПродукция,
		|	ДокументЕГАИС.Справка2,
		|	""""
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ДокументЕГАИС
		|ГДЕ
		|	ДокументЕГАИС.Ссылка = &ДокументОснование
		|	И ДокументЕГАИС.Справка2 В(&МассивСправок2)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Серия КАК Серия,
		|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Таблица.Справка2 КАК Справка2,
		|	Таблица.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
		|	МАКСИМУМ(ЕСТЬNULL(СопоставленоПорядок.Порядок, 0)) КАК Порядок
		|ИЗ
		|	ВтТовары КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК Сопоставлено
		|		ПО (Сопоставлено.Номенклатура = Таблица.Номенклатура)
		|			И (Сопоставлено.Характеристика = Таблица.Характеристика)
		|			И (Сопоставлено.АлкогольнаяПродукция = Таблица.АлкогольнаяПродукция)
		|			И (Сопоставлено.ИдентификаторУпаковки = Таблица.ИдентификаторУпаковки)
		|			И (Сопоставлено.Справка2 = Таблица.Справка2)
		|			И (Сопоставлено.Серия = Таблица.Серия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СопоставленоПорядок
		|		ПО (СопоставленоПорядок.Номенклатура = Таблица.Номенклатура)
		|			И (СопоставленоПорядок.Характеристика = Таблица.Характеристика)
		|ГДЕ
		|	Сопоставлено.Справка2 ЕСТЬ NULL
		|	И Таблица.Номенклатура <> &ПустаяНоменклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.АлкогольнаяПродукция,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Серия,
		|	Таблица.ИдентификаторУпаковки,
		|	Таблица.Справка2";
		Запрос.УстановитьПараметр("МассивСправок2", МассивСправок2);
		Запрос.УстановитьПараметр("ДокументОснование", ВыборкаДокументы.ДокументОснование);
		Запрос.УстановитьПараметр("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура, Истина);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика, Истина);
			НаборЗаписей.Отбор.Серия.Установить(Выборка.Серия, Истина);
			НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.АлкогольнаяПродукция, Истина);
			НаборЗаписей.Отбор.ИдентификаторУпаковки.Установить(Выборка.ИдентификаторУпаковки, Истина);
			НаборЗаписей.Отбор.Справка2.Установить(Выборка.Справка2, Истина);
			
			Если ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
				
				Если Выборка.Номенклатура <> Справочники.Номенклатура.ПустаяСсылка() Тогда
					НоваяЗапись = НаборЗаписей.Добавить();
					НоваяЗапись.Номенклатура          = Выборка.Номенклатура;
					НоваяЗапись.Характеристика        = Выборка.Характеристика;
					НоваяЗапись.Серия                 = Выборка.Серия;
					НоваяЗапись.АлкогольнаяПродукция  = Выборка.АлкогольнаяПродукция;
					НоваяЗапись.ИдентификаторУпаковки = Выборка.ИдентификаторУпаковки;
					НоваяЗапись.Справка2              = Выборка.Справка2;
					НоваяЗапись.Порядок               = Выборка.Порядок + 1;
			
					НаборЗаписей.Записать();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

//#КонецОбласти

//#КонецОбласти

#КонецЕсли
