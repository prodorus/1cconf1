#Если Клиент Тогда

Процедура ВывестиТитульныйЛист(ТекНастройка, Результат, Макет)
	
	ОбластьОрганизация      = Макет.ПолучитьОбласть("ТитульныйЛист");
	
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекНастройка.ПредставлениеОрганизации;
	ОбластьОрганизация.Параметры.ПредставлениеПериода = ПолучитьПредставлениеПериода(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Склад) И НЕ ТекНастройка.НовыйФормат Тогда
		ОбластьОрганизация.Параметры.Подразделение = Склад;
	КонецЕсли;
	
	Если ТекНастройка.НовыйФормат Тогда  
		ОбластьОрганизация.Параметры.ИННКПП = ТекНастройка.СведенияОбОрганизации.ИНН + ?(Организация.ЮрФизЛицо=Перечисления.ЮрФизЛицо.ЮрЛицо, " / " + ТекНастройка.СведенияОбОрганизации.КПП, "");
	КонецЕсли;
	
	ОбластьОрганизация.Параметры.Организация = Организация;
	
	Результат.Вывести(ОбластьОрганизация);
	Результат.ВывестиГоризонтальныйРазделительСтраниц();
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "ТитульныйЛист";
	
КонецПроцедуры

Функция ТекстЗапроса153()
	Текст = 
		"ВЫБРАТЬ
		|	СведенияОбАлкогольнойПродукции.Номенклатура КАК Номенклатура,
		|	ВЫРАЗИТЬ(СведенияОбАлкогольнойПродукции.Номенклатура.НаименованиеПолное КАК СТРОКА(200)) КАК ТоварНаименование,
		|	СведенияОбАлкогольнойПродукции.КоэффПересчетаДал * 10 КАК Емкость,
		|	СведенияОбАлкогольнойПродукции.КоэффПересчетаДал КАК КоэффПересчетаДал,
		|	СведенияОбАлкогольнойПродукции.ВидПродукции.Наименование КАК ВидПродукции,
		|	СведенияОбАлкогольнойПродукции.ВидПродукции.КодВидаПродукции КАК КодВидаПродукции
		|ПОМЕСТИТЬ АлкогольнаяПродукция
		|ИЗ
		|	РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидПродукцииПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.КодВидаПродукции
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодВидаПоступление,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|					И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.НаименованиеПолное
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|					И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).НаименованиеПолное
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА &ОрганизацияНаименование
		|			ИНАЧЕ """"
		|		КОНЕЦ КАК СТРОКА(1000)) КАК Поставщик,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияИНН
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ИНН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента = """"
		|									ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Номер
		|								ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслугВНТТ).Номер
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Номер
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПеремещениеТоваров).Номер
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерТТН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента = """"
		|									ТОГДА ИСТИНА
		|								ИНАЧЕ ЛОЖЬ
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ИСТИНА
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ИСТИНА
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПреобразовыватьНомер,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|									ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Дата
		|								ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ДатаВходящегоДокумента
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслугВНТТ).Дата
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Дата
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПеремещениеТоваров).Дата
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДатаТТН,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ЕмкостьПоступление,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоПоступление,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА (ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт) * АлкогольнаяПродукция.КоэффПересчетаДал
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОбъемПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДокументПоступление,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДокументРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|			ТОГДА ""Проданная продукция""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщикуИзНТТ
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.СписаниеТоваров
		|			ТОГДА ""Недостачи продукции""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				И (ХозрасчетныйОбороты.КорСчет В (&Счет41)
		|					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет79))
		|			ТОГДА ""Продукция, переданная в другое подразделение""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ""Списанная продукция""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СодержаниеРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидПродукцииРасход,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ЕмкостьРасход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоРасход,
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				ТОГДА (ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт) * АлкогольнаяПродукция.КоэффПересчетаДал
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОбъемРасход
		|ИЗ
		|	АлкогольнаяПродукция КАК АлкогольнаяПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаНач, &ДатаКон, Регистратор, Счет В (&Счет41), &ВидыСубконто, &Условие, , ) КАК ХозрасчетныйОбороты
		|		ПО АлкогольнаяПродукция.Номенклатура = ХозрасчетныйОбороты.Субконто1
		|ГДЕ
		|	НЕ ХозрасчетныйОбороты.КорСчет ЕСТЬ NULL 
		|	И НЕ ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.КодВидаПродукции
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента = """"
		|									ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Номер
		|								ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслугВНТТ).Номер
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Номер
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПеремещениеТоваров).Номер
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).НомерВходящегоДокумента = """"
		|									ТОГДА ИСТИНА
		|								ИНАЧЕ ЛОЖЬ
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ИСТИНА
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ИСТИНА
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|						ТОГДА ВЫБОР
		|								КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|									ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Дата
		|								ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).ДатаВходящегоДокумента
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслугВНТТ).Дата
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Дата
		|					КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
		|						ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПеремещениеТоваров).Дата
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|			ТОГДА ""Проданная продукция""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщикуИзНТТ
		|			ТОГДА ""Продукция, возвращенная поставщику""
		|		КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.СписаниеТоваров
		|			ТОГДА ""Недостачи продукции""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|				И (ХозрасчетныйОбороты.КорСчет В (&Счет41)
		|					ИЛИ ХозрасчетныйОбороты.КорСчет В (&Счет79))
		|			ТОГДА ""Продукция, переданная в другое подразделение""
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ""Списанная продукция""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.ВидПродукции + "" ("" + АлкогольнаяПродукция.ТоварНаименование + "")""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА АлкогольнаяПродукция.Емкость
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотКт - ХозрасчетныйОбороты.КоличествоОборотДт > 0
		|			ТОГДА ХозрасчетныйОбороты.Регистратор
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.Период,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|					И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.НаименованиеПолное
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|					И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|				ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).НаименованиеПолное
		|			КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				ТОГДА &ОрганизацияНаименование
		|			ИНАЧЕ """"
		|		КОНЕЦ КАК СТРОКА(1000)),
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ВозвратТоваровОтПокупателя).Контрагент.ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|				И ХозрасчетныйОбороты.КорСубконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.Контрагенты).ИНН
		|		КОГДА ХозрасчетныйОбороты.КоличествоОборотДт - ХозрасчетныйОбороты.КоличествоОборотКт > 0
		|			ТОГДА &ОрганизацияИНН
		|		ИНАЧЕ """"
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ХозрасчетныйОбороты.Период";
	Возврат Текст;
КонецФункции

Функция ТекстЗапроса164()
	Текст = 
	"ВЫБРАТЬ
	|	ПродажиОбороты.Период КАК ДатаПродажи,
	|	ПродажиОбороты.Номенклатура,
	|	ПродажиОбороты.Номенклатура.НаименованиеПолное КАК НаименованиеПродукции,
	|	ПродажиОбороты.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.КодВидаПродукции КАК КодВидаПродукции,
	|	ПродажиОбороты.Номенклатура.ОбъемДАЛ * 10 КАК ОбъемРасход,
	|	ПродажиОбороты.КоличествоОборот КАК КоличествоРасход
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			День,
	|			Организация = &Организация
	|				И Номенклатура.АлкогольнаяПродукция
	|				И ДокументПродажи ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|				&Условие) КАК ПродажиОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПродажи";
	Возврат Текст;
КонецФункции

Функция ТекстЗапросаСчета()
	Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Родитель = &Счет41
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Родитель = &Счет79
	|;";
	Возврат Текст;
КонецФункции

Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	ТабличныйДокумент.Очистить();
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	МассивНастроек = Новый Массив;
	
	ДатаНачалаИспользованияНовойФормыЖурнала = Константы.ДатаНачалаПримененияПриказа164.Получить();
	Сведения1 = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, КонецПериода);
	Сведения2 = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ДатаНачалаИспользованияНовойФормыЖурнала-86400);
	СтрокаКлючей = "НовыйФормат, Макет, НачалоПериода, КонецПериода, СведенияОбОрганизации, ВыводитьИтогиПоДням";
	
	Если НачалоПериода >= ДатаНачалаИспользованияНовойФормыЖурнала Тогда
		
		МассивНастроек.Добавить(Новый Структура(СтрокаКлючей,
			Истина, ПолучитьМакет("ПФ_MXL_164"), НачалоПериода, КонецПериода, Сведения1, Истина));
		
	ИначеЕсли КонецПериода < ДатаНачалаИспользованияНовойФормыЖурнала Тогда
		
		МассивНастроек.Добавить(Новый Структура(СтрокаКлючей,
			Ложь, ПолучитьМакет("Макет"), НачалоПериода, КонецПериода, Сведения1, Ложь));
		
	Иначе
		
		МассивНастроек.Добавить(Новый Структура(СтрокаКлючей,
			Ложь, ПолучитьМакет("Макет"), НачалоПериода, ДатаНачалаИспользованияНовойФормыЖурнала-86400, Сведения2, Ложь));
												
		МассивНастроек.Добавить(Новый Структура(СтрокаКлючей,
			Истина, ПолучитьМакет("ПФ_MXL_164"), ДатаНачалаИспользованияНовойФормыЖурнала, КонецПериода, Сведения1, Истина));
		
	КонецЕсли;

	НомерСтроки = Макс(НомерНачальнойСтроки, 1);
	
	Для Каждого ТекНастройка Из МассивНастроек Цикл

		ТекНастройка.Вставить("ПредставлениеОрганизации",
			ФормированиеПечатныхФорм.ОписаниеОрганизации(ТекНастройка.СведенияОбОрганизации, "ПолноеНаименование,"));
		
		Если ТитульныйЛист Тогда
			ВывестиТитульныйЛист(ТекНастройка, ТабличныйДокумент, ТекНастройка.Макет);
		КонецЕсли;
	
		ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("Шапка");
		ТабличныйДокумент.Вывести(ОбластьМакета);
	
		Если Не ТекНастройка.НовыйФормат Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Счет79", ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);
			Запрос.УстановитьПараметр("Счет41", ПланыСчетов.Хозрасчетный.Товары);
			Запрос.Текст = ТекстЗапросаСчета();
	
			Результат = Запрос.ВыполнитьПакет();
			МассивСчетов41 = Результат[0].Выгрузить().ВыгрузитьКолонку("Счет");
			МассивСчетов79 = Результат[1].Выгрузить().ВыгрузитьКолонку("Счет");
	
			ВидыСубконто = Новый Массив;
			ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			Если ЗначениеЗаполнено(Склад) Тогда
				ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
			КонецЕсли;
	
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Счет41", 		МассивСчетов41);
			Запрос.УстановитьПараметр("Счет79", 		МассивСчетов79);
			Запрос.УстановитьПараметр("ВидыСубконто", 	ВидыСубконто);
			Запрос.УстановитьПараметр("ОрганизацияНаименование", ТекНастройка.ПредставлениеОрганизации);
			Запрос.УстановитьПараметр("ОрганизацияИНН",	ТекНастройка.СведенияОбОрганизации.Инн);
			Текст = ТекстЗапроса153();
			СтрокаУсловия = "Организация = &Организация";
	
			Если ЗначениеЗаполнено(Склад) Тогда
				СтрокаУсловия = СтрокаУсловия + " И Субконто2 = &Склад";
			КонецЕсли;
			
		Иначе
			
			Запрос = Новый Запрос;
			Текст = ТекстЗапроса164();
			Если ЗначениеЗаполнено(Склад) Тогда
				СтрокаУсловия = " И ДокументПродажи.Склад = &Склад";
			Иначе
				СтрокаУсловия = "";
			КонецЕсли;	  
			  
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Организация", 	Организация);
		Запрос.УстановитьПараметр("Склад", 			Склад);
		Запрос.УстановитьПараметр("ДатаНач", 		НачалоДня(ТекНастройка.НачалоПериода));
		Запрос.УстановитьПараметр("ДатаКон", 		Новый Граница(КонецДня(ТекНастройка.КонецПериода), ВидГраницы.Включая));
		
		Текст = СтрЗаменить(Текст, "&Условие", СтрокаУсловия);
		Запрос.Текст = Текст;
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Не ТекНастройка.ВыводитьИтогиПоДням Тогда
			
			ОбъемРасход 			= 0;
			КоличествоРасход		= 0;
			Если НЕ ТекНастройка.НовыйФормат Тогда 
				ОбъемПоступление		= 0;
				КоличествоПоступление	= 0; 
			КонецЕсли;
			
			ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("Строка");
			
			МассивПрефиксовНомеровДокументов = ОбщегоНазначения.СформироватьМассивПрефиксовДляРИБИОрганизации(Организация);
			
			Пока Выборка.Следующий() Цикл
				
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Выборка);
				ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
				
				Если НЕ ТекНастройка.НовыйФормат И Выборка.ПреобразовыватьНомер Тогда
					ОбластьМакета.Параметры.НомерТТН = ПреобразоватьНомерДокументаДляВыводаНаПечать(Выборка.НомерТТН, МассивПрефиксовНомеровДокументов);
				КонецЕсли;	
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ОбъемРасход 			= ОбъемРасход + Выборка.ОбъемРасход;
				КоличествоРасход 		= КоличествоРасход + Выборка.КоличествоРасход;
				Если НЕ ТекНастройка.НовыйФормат Тогда
					ОбъемПоступление 		= ОбъемПоступление + Выборка.ОбъемПоступление;
					КоличествоПоступление 	= КоличествоПоступление + Выборка.КоличествоПоступление;
				КонецЕсли;
				
				НомерСтроки 			= НомерСтроки + 1;
				
			КонецЦикла;
			
			ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("Итоги");
			
			Если НЕ ТекНастройка.НовыйФормат Тогда
				ОбластьМакета.Параметры.ОбъемПоступление 		= ОбъемПоступление;
				ОбластьМакета.Параметры.КоличествоПоступление 	= КоличествоПоступление;
				ОбластьМакета.Параметры.ОбъемРасход 			= ОбъемРасход;
				ОбластьМакета.Параметры.КоличествоРасход 		= КоличествоРасход;
			Иначе
				ОбластьМакета.Параметры.ОбъемРасход 			= "";
				ОбластьМакета.Параметры.КоличествоРасход 		= КоличествоРасход;
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
		Иначе
			Выгрузка = Результат.Выгрузить();
			
			Периоды = Выгрузка.Скопировать(,"ДатаПродажи");
			Периоды.Свернуть("ДатаПродажи");
			
			Для Каждого День Из Периоды Цикл
				
				ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("Строка");
				СтрокиДня = Выгрузка.Скопировать(Новый Структура("ДатаПродажи", День.ДатаПродажи));
				
				Для Каждого ТекСтрока Из СтрокиДня Цикл
					ТекСтрока.НаименованиеПродукции = СокрЛП(ТекСтрока.НаименованиеПродукции);
					ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ТекСтрока);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ТабличныйДокумент.Вывести(ОбластьМакета);
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла; // по строкам дня
				
				ИтогиПоКоду = СтрокиДня.Скопировать();
				ИтогиПоКоду.Свернуть("КодВидаПродукции", "КоличествоРасход");
				ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("ИтогиПоКоду");
				Для Каждого ТекСтрока Из ИтогиПоКоду Цикл
					ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ТекСтрока);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла; // по кодам

				ИтогиПоНаименованию = СтрокиДня.Скопировать();
				ИтогиПоНаименованию.Свернуть("НаименованиеПродукции", "КоличествоРасход");
				ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("ИтогиПоНаименованию");
				Для Каждого ТекСтрока Из ИтогиПоНаименованию Цикл
					ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ТекСтрока);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;

				ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("ИтогиПоКоличеству");
				ОбластьМакета.Параметры.КоличествоРасход = СтрокиДня.Итог("КоличествоРасход");
				ТабличныйДокумент.Вывести(ОбластьМакета);

			КонецЦикла; // по дням
			
		КонецЕсли;
		
		ОбластьМакета = ТекНастройка.Макет.ПолучитьОбласть("Подписи");
		ТабличныйДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область("СтрокиДляПовтора");
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(
		ТабличныйДокумент, , Строка(глЗначениеПеременной("глТекущийПользователь")));
	
КонецПроцедуры

Функция ПолучитьПредставлениеПериода(ОтчетОбъект = Неопределено, НачалоПериода = Неопределено, КонецПериода = Неопределено, ТолькоДаты  = Ложь)
	
	ТекстПериод = "";
	
	Если ОтчетОбъект <> Неопределено Тогда 
		НачалоПериода = ОтчетОбъект.НачалоПериода;
		КонецПериода  = ОтчетОбъект.КонецПериода;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда 
		Если КонецПериода >= НачалоПериода Тогда
			ТекстПериод = ?(ТолькоДаты, "", " за ") + ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");
		Иначе
			ТекстПериод = "";
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(НачалоПериода) И Не ЗначениеЗаполнено(КонецПериода) Тогда
		ТекстПериод = ?(ТолькоДаты, "", " за ") + ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(Дата(3999, 11, 11)), "ФП = Истина");
		ТекстПериод = СтрЗаменить(ТекстПериод, Сред(ТекстПериод, Найти(ТекстПериод, " - ")), " - ...");
	КонецЕсли;
	
	Возврат ТекстПериод;
	
КонецФункции

Функция ПреобразоватьНомерДокументаДляВыводаНаПечать(Знач Номер,МассивПрефиксов)
	
	Для Каждого ТекущийПрефикс ИЗ МассивПрефиксов Цикл
		
		// удаление префикса из номера документа
		Если Найти(Номер, ТекущийПрефикс)=1 Тогда 
			Номер = Сред(Номер, СтрДлина(ТекущийПрефикс)+1);
		КонецЕсли;
		
		// так же, может остаться "минус" впереди
		Если Лев(Номер, 1) = "-" Тогда
			Номер = Сред(Номер, 2);
		КонецЕсли;
		
		// удаление ведущих нулей
		Пока Лев(Номер, 1)="0" Цикл
			Номер = Сред(Номер, 2);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Номер;
	
КонецФункции	

#КонецЕсли
