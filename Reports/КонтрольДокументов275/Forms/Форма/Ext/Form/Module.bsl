
&НаСервере 
Перем КартинкаПроверкаПройдена;

&НаСервере 
Перем КартинкаПроверкаНеПройдена;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		Если Параметры.ПараметрКоманды.Ссылка.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Перед формированием отчета необходимо сохранить документ.'"), Отказ);
			Возврат;
		Иначе
			Отчет.КонтролируемыйДокумент = Параметры.ПараметрКоманды.Ссылка;
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Контекстный отчет не предназначен для непосредственного использования.'"), Отказ);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Отчет.КонтролируемыйДокумент) = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") Тогда
		ДокументОбъект = Отчет.КонтролируемыйДокумент.ПолучитьОбъект();
		РеквизитыДокумента = Новый Структура();
		Если ДокументОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Заявка = ДокументОбъект.РасшифровкаПлатежа[0].ДокументПланированияПлатежа;
			РеквизитыДокумента.Вставить("ЗаявкаУказана", Не Заявка.Пустая());
			Если РеквизитыДокумента.ЗаявкаУказана Тогда
				РеквизитыДокумента.Вставить("ПлатежПоФЗ275", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка, "ПлатежПоФЗ275"));
			Иначе
				РеквизитыДокумента.Вставить("ПлатежПоФЗ275", Ложь);
			КонецЕсли;
		Иначе
			РеквизитыДокумента.Вставить("ЗаявкаУказана", Ложь);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Отчет.КонтролируемыйДокумент) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеСредств") Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Отчет.КонтролируемыйДокумент, "ПлатежПоФЗ275");
		РеквизитыДокумента.Вставить("ЗаявкаУказана", Истина);
	КонецЕсли;
	РеквизитыДокумента.Вставить("ЕстьОтдельныеСчетаГОЗ");
	РеквизитыДокумента.Вставить("БанковскиеСчета");
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, СчетаДокумента(Отчет.КонтролируемыйДокумент));
	
	УправлениеЭлементамиФормы();
	
	Если РеквизитыДокумента.ПлатежПоФЗ275 И РеквизитыДокумента.ЗаявкаУказана И РеквизитыДокумента.ЕстьОтдельныеСчетаГОЗ Тогда
		ОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РежимВыводаПриИзменении(Элемент)
	РежимВыводаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	Результат.Очистить();
	
	МакетОтчета = Отчеты.КонтрольДокументов275.ПолучитьМакет("СоставКонтролей");
	
	ПорядокВыводаОбластей = ПорядокВыводаОбластей();
	СоответствиеРезультатовПроверкиИОбластей = СоответствиеРезультатовПроверкиИОбластей();
	
	РезультатКонтроляДокумента = Контроли275Сервер.КонтрольДокумента(Отчет.КонтролируемыйДокумент);
	
	Для Каждого ИмяОбласти Из ПорядокВыводаОбластей Цикл 
		Область = МакетОтчета.ПолучитьОбласть(ИмяОбласти);
		ИмяОбработчика = СоответствиеРезультатовПроверкиИОбластей[ИмяОбласти];
		
		Если ИмяОбработчика <> Неопределено Тогда
			ОписаниеРезультата = ОписаниеРезультатаПоИмениОбработчика(РезультатКонтроляДокумента, ИмяОбработчика);
			
			Если Область.Рисунки.Количество() > 0 Тогда
				Рисунок = Область.Рисунки[0];
				Рисунок.Картинка = ОписаниеРезультата.Картинка;
				Рисунок.ЦветФона = Новый Цвет();
			КонецЕсли;
			
			Область.Параметры[ИмяОбработчика] = ОписаниеРезультата.Описание;
		КонецЕсли;
		
		Если (Отчет.ТолькоСОшибками И ОбластьСОшибками(РезультатКонтроляДокумента, ИмяОбласти) 
				Или ОбязательнаяОбласть(ИмяОбласти, РезультатКонтроляДокумента))
			Или (Отчет.ТолькоКонтролируемыеПользователем И ОбластьКонтролируетсяПользователем(ИмяОбласти) 
				Или ОбязательнаяОбласть(ИмяОбласти, РезультатКонтроляДокумента)) 
			Или (НЕ Отчет.ТолькоСОшибками И НЕ Отчет.ТолькоКонтролируемыеПользователем) Тогда
				
			Результат.Вывести(Область);
		КонецЕсли;
	КонецЦикла;
	
	// Параметры печати
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	Результат.АвтоМасштаб = Истина;
	
	// Поля
	Результат.ПолеСверху = 5;
	Результат.ПолеСнизу = 10;
	Результат.ПолеСлева = 5;
	Результат.ПолеСправа = 5;
	
	Результат.РазмерКолонтитулаСверху = 5;
	Результат.РазмерКолонтитулаСнизу = 5;
	
	// Колонтитулы
	Результат.НижнийКолонтитул.Выводить = Истина;
	Результат.НижнийКолонтитул.ТекстСправа = НСтр("ru= 'Страница [&НомерСтраницы] из [&СтраницВсего]'");
	
	// Прочие параметры
	Результат.Защита = Истина;
	Результат.ОтображатьЗаголовки = Ложь;
	Результат.ОтображатьСетку = Ложь;
	Результат.КлючПараметровПечати = "ПечатнаяФормаКонтроляДокументов";
	Результат.ИспользуемоеИмяФайла = НСтр("ru= 'Контроль документов на соответствие 275-ФЗ'") + " " + Отчет.КонтролируемыйДокумент;
КонецПроцедуры

&НаСервере 
Функция ОбязательнаяОбласть(ИмяОбласти, РезультатыКонтроля)
	Области = Новый Массив;
	
	Области.Добавить("ОбщаяШапка");
	
	Если Отчет.ТолькоКонтролируемыеПользователем Тогда
		Области.Добавить("Шапка_Раздел1");
		Области.Добавить("Заголовок_Раздел1");
		Области.Добавить("Строка_Раздел1_275_83");
		Области.Добавить("Строка_Раздел1_275_83_1_2");
		Области.Добавить("Строка_Раздел1_275_83_1_2_а");
		Области.Добавить("Строка_Раздел1_275_83_1_2_д");
	КонецЕсли;
	
	Если Отчет.ТолькоСОшибками Тогда
		Отбор = Новый Структура("Раздел, КонтрольПройден", "Раздел1", Ложь);
		НайденныеСтроки = РезультатыКонтроля.ОписанияРезультатов.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Области.Добавить("Шапка_Раздел1");
			Области.Добавить("Заголовок_Раздел1");
			
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_83") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_83");
				КонецЕсли;
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_83_1_2") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_83_1_2");
				КонецЕсли;
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_84") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_84");
				КонецЕсли;
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_84_9") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_84_9");
				КонецЕсли;
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_85_1") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_85_1");
				КонецЕсли;
				Если Найти(НайденнаяСтрока.ИмяОбработчикаКонтроля, "Раздел1_275_85_2") > 0 Тогда
					Области.Добавить("Строка_Раздел1_275_85_2");
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Отбор = Новый Структура("Раздел, КонтрольПройден", "Раздел2", Ложь);
		НайденныеСтроки = РезультатыКонтроля.ОписанияРезультатов.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Области.Добавить("Шапка_Раздел2");
			Области.Добавить("Заголовок_Раздел2");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Области.Найти(ИмяОбласти) <> Неопределено;
КонецФункции

&НаСервере 
Функция ОбластьКонтролируетсяПользователем(ИмяОбласти)
	Области = Новый Массив;
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_а");
	Области.Добавить("Строка_Раздел1_275_83_1_2_д");
	
	Возврат Области.Найти(ИмяОбласти) <> Неопределено;
КонецФункции

&НаСервере 
Функция ОбластьСОшибками(РезультатыПроверки, ИмяОбласти)
	ОписанияРезультатов = РезультатыПроверки.ОписанияРезультатов;
	СоответствиеРезультатовПроверкиИОбластей = СоответствиеРезультатовПроверкиИОбластей();
	
	Отбор = Новый Структура("ИмяОбработчикаКонтроля, КонтрольПройден", СоответствиеРезультатовПроверкиИОбластей[ИмяОбласти], Ложь);
	НайденныеСтроки = ОписанияРезультатов.НайтиСтроки(Отбор);
	
	Возврат НайденныеСтроки.Количество() > 0;
КонецФункции

&НаСервере 
Функция СоответствиеРезультатовПроверкиИОбластей()
	СоответствиеРезультатовПроверкиИОбластей = Новый Соответствие;
	
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_1", "РезультатКонтроля_Раздел1_275_83_1_1");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_2_б", "РезультатКонтроля_Раздел1_275_83_1_2_б");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_2_в", "РезультатКонтроля_Раздел1_275_83_1_2_в");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_2_г", "РезультатКонтроля_Раздел1_275_83_1_2_г");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_2_е", "РезультатКонтроля_Раздел1_275_83_1_2_е");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_83_1_2_з", "РезультатКонтроля_Раздел1_275_83_1_2_з");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_84_2", "РезультатКонтроля_Раздел1_275_84_2");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_84_3", "РезультатКонтроля_Раздел1_275_84_3");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_84_9_б", "РезультатКонтроля_Раздел1_275_84_9_б");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_84_10", "РезультатКонтроля_Раздел1_275_84_10");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_85_1_1", "РезультатКонтроля_Раздел1_275_85_1_1");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_85_1_3", "РезультатКонтроля_Раздел1_275_85_1_3");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_85_2_1", "РезультатКонтроля_Раздел1_275_85_2_1");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел1_275_85_2_2", "РезультатКонтроля_Раздел1_275_85_2_2");
	
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел2_3729У_2_1", "РезультатКонтроля_Раздел2_3729У_2_1");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел2_3729У_2_2", "РезультатКонтроля_Раздел2_3729У_2_2");
	СоответствиеРезультатовПроверкиИОбластей.Вставить("Строка_Раздел2_3729У_2_3", "РезультатКонтроля_Раздел2_3729У_2_3");
	
	Возврат СоответствиеРезультатовПроверкиИОбластей;
КонецФункции

&НаСервере 
Функция ПорядокВыводаОбластей()
	Области = Новый Массив;
	
	Области.Добавить("ОбщаяШапка");
	
	// Раздел 1
	Области.Добавить("Шапка_Раздел1");
	
	Области.Добавить("Заголовок_Раздел1");
	
	Области.Добавить("Строка_Раздел1_275_83");
	
	Области.Добавить("Строка_Раздел1_275_83_1_1");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_а");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_б");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_в");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_г");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_д");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_е");
	
	Области.Добавить("Строка_Раздел1_275_83_1_2_з");
	
	Области.Добавить("Строка_Раздел1_275_84");
	
	Области.Добавить("Строка_Раздел1_275_84_2");
	
	Области.Добавить("Строка_Раздел1_275_84_3");
	
	Области.Добавить("Строка_Раздел1_275_84_9");
	
	Области.Добавить("Строка_Раздел1_275_84_9_б");
	
	Области.Добавить("Строка_Раздел1_275_84_10");
	
	Области.Добавить("Строка_Раздел1_275_85_1");
	
	Области.Добавить("Строка_Раздел1_275_85_1_1");
	
	Области.Добавить("Строка_Раздел1_275_85_1_3");
	
	Области.Добавить("Строка_Раздел1_275_85_2");
	
	Области.Добавить("Строка_Раздел1_275_85_2_1");
	
	Области.Добавить("Строка_Раздел1_275_85_2_2");
	
	// Раздел 2
	Области.Добавить("Шапка_Раздел2");
	
	Области.Добавить("Заголовок_Раздел2");
	
	Области.Добавить("Строка_Раздел2_3729У_2");
	
	Области.Добавить("Строка_Раздел2_3729У_2_1");
	
	Области.Добавить("Строка_Раздел2_3729У_2_2");
	
	Области.Добавить("Строка_Раздел2_3729У_2_3");
	
	Возврат Области;
КонецФункции

&НаСервере 
Функция ОписаниеРезультатаПоИмениОбработчика(РезультатКонтроляДокумента, ИмяОбработчика)
	ОписаниеРезультатаПоИмениОбработчика = Новый Структура("Картинка, Описание, Символ");
	Описание = "";
	КонтрольПройденПоВсемСтрокам = Истина;
	
	ОписанияРезультатов = РезультатКонтроляДокумента.ОписанияРезультатов;
	
	Отбор = Новый Структура("ИмяОбработчикаКонтроля", ИмяОбработчика);
	НайденныеСтроки = ОписанияРезультатов.НайтиСтроки(Отбор);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		Описание = Описание + ?(ЗначениеЗаполнено(Описание), Символы.ПС, "") + НайденнаяСтрока.Описание;
		Если КонтрольПройденПоВсемСтрокам Тогда
			КонтрольПройденПоВсемСтрокам = НайденнаяСтрока.КонтрольПройден;
		КонецЕсли;
	КонецЦикла;
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		ОписаниеРезультатаПоИмениОбработчика.Вставить("Описание", Описание);
		ОписаниеРезультатаПоИмениОбработчика.Вставить("Символ", ?(КонтрольПройденПоВсемСтрокам, "V", "Х"));
		ОписаниеРезультатаПоИмениОбработчика.Вставить("Картинка", ?(КонтрольПройденПоВсемСтрокам, КартинкаПроверкаПройдена, КартинкаПроверкаНеПройдена));
	Иначе
		ОписаниеРезультатаПоИмениОбработчика.Вставить("Картинка", Новый Картинка);
	КонецЕсли;
	
	Возврат ОписаниеРезультатаПоИмениОбработчика;
КонецФункции

&НаСервере
Процедура РежимВыводаПриИзмененииНаСервере()
	Отчет.ТолькоСОшибками = (РежимВывода = 1);
	Отчет.ТолькоКонтролируемыеПользователем = (РежимВывода = 2);
	
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере 
Процедура УправлениеЭлементамиФормы()
	КонтрольТребуется = РеквизитыДокумента.ПлатежПоФЗ275 И РеквизитыДокумента.ЗаявкаУказана И РеквизитыДокумента.ЕстьОтдельныеСчетаГОЗ;
	
	Элементы.ФормаОбновить.Видимость = КонтрольТребуется;
	Элементы.РежимВывода.Видимость = КонтрольТребуется;
	Элементы.СтраницыРезультатовКонтроля.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если КонтрольТребуется Тогда
		Элементы.СтраницыРезультатовКонтроля.ТекущаяСтраница = Элементы.СтраницаКонтрольПо275ФЗ;
	Иначе
		Элементы.СтраницыРезультатовКонтроля.ТекущаяСтраница = Элементы.СтраницаКонтрольНеТребуется;
	КонецЕсли;
	
	Если Не КонтрольТребуется Тогда
		МассивБанковскихСчетов = Новый Массив;
		Для Каждого БанковскийСчет Из РеквизитыДокумента.БанковскиеСчета Цикл 
			
			ПояснениеСсылкаНаСчет = """"+Строка(БанковскийСчет)+"""";
			
			МассивБанковскихСчетов.Добавить(ПояснениеСсылкаНаСчет);
			
		КонецЦикла;
		
		Если Не РеквизитыДокумента.ЕстьОтдельныеСчетаГОЗ Тогда
			
			КоличествоБанковскихСчетов = МассивБанковскихСчетов.Количество();
			
			Пояснение1 = НСтр("ru= 'В документе '") + 
				?(КоличествоБанковскихСчетов = 0, НСтр("ru= 'не указаны банковские счета'"),
					?(КоличествоБанковскихСчетов = 1, НСтр("ru= 'указан собственный счет '"), НСтр("ru= 'указаны собственные счета '"))) +
			СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивБанковскихСчетов) + ". ";
			
		КонецЕсли;
		
		Если Не РеквизитыДокумента.ПлатежПоФЗ275 Тогда
			Пояснение2 = НСтр("ru= 'В документе указан платеж без использования отдельных счетов.'");
		КонецЕсли;
		
		Если Не РеквизитыДокумента.ЗаявкаУказана Тогда
			Пояснение3 = НСтр("ru= 'В документе не указана заявка на расходование денежных средств.'");
		КонецЕсли;
		
		Пояснение4 = НСтр("ru= 'Контроль документов на соответствие 275-ФЗ проводится только для операций с отдельными счетами.'");
		
		Элементы.ПояснениеКонтрольНеТребуется.Заголовок = 
			?(Не РеквизитыДокумента.ЕстьОтдельныеСчетаГОЗ, Пояснение1 + Символы.ПС, "") + 
			?(Не РеквизитыДокумента.ПлатежПоФЗ275,         Пояснение2 + Символы.ПС, "") + 
			?(Не РеквизитыДокумента.ЗаявкаУказана,         Пояснение3 + Символы.ПС, "") + 
			Пояснение4;
			
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста 
Функция СчетаДокумента(Ссылка)
	СчетаДокумента = Новый Структура("ЕстьОтдельныеСчетаГОЗ, БанковскиеСчета");
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.МестоРазмещения КАК БанковскийСчет
	|ПОМЕСТИТЬ БанковскиеСчета
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеСредств.РазмещениеЗаявки КАК ЗаявкаНаРасходованиеДенежныхСредствФинансирование
	|ГДЕ
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.СчетОрганизации
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.БанковскийСчет
	|ИЗ
	|	БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	&ТекстЗапросаОтдельныйБанковскийСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.БанковскийСчет
	|ИЗ
	|	БанковскиеСчета КАК БанковскиеСчета");
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаОтдельныйБанковскийСчет", 
		Контроли275Сервер.ТекстЗапросаПроверкиОтдельногоСчета("БанковскиеСчета.БанковскийСчет"));
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	СчетаДокумента.Вставить("ЕстьОтдельныеСчетаГОЗ", Не Результат[1].Пустой());
	СчетаДокумента.Вставить("БанковскиеСчета", Результат[2].Выгрузить().ВыгрузитьКолонку(0));
	
	Возврат СчетаДокумента;
КонецФункции

КартинкаПроверкаПройдена = БиблиотекаКартинок.ВыполненоУспешно32;
КартинкаПроверкаНеПройдена = БиблиотекаКартинок.НекорректныйКонтрагент;
