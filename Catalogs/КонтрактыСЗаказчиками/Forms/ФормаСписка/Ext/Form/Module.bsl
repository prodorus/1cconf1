
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем КонтрактИсполнителя;
	
	Если Параметры.Свойство("КонтрактИсполнителя", КонтрактИсполнителя) Тогда
		СтруктураПараметров = Новый Структура("КонтрактИсполнителя", КонтрактИсполнителя);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Ссылка", ПолучитьДанныеВыбора(Тип("СправочникСсылка.КонтрактыСЗаказчиками"), СтруктураПараметров), ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
		
	УстановитьОтборСписка();
	
	Если Параметры.РежимВыбора Тогда
		Элементы.СписокВыполняются.РежимВыбора = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьОтборСписка();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка()
	
	Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.СтраницаВыполняются Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Состояние", Перечисления.СостоянияКонтрактов.Завершен, ВидСравненияКомпоновкиДанных.НеРавно);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Состояние", Перечисления.СостоянияКонтрактов.Завершен, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщегоНазначения275КлиентСервер.ПроверитьИспользованиеПодсистемы(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПаспортКонтракта(Команда)
	Контракт = ПолучитьСсылку();
	Если ЗначениеЗаполнено(Контракт) Тогда
		ПФ = Новый Структура("Ссылка", Контракт);
		ОткрытьФорму("Отчет.ПаспортКонтракта.Форма.Форма", ПФ, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСведенияОКооперацииПриложение1(Команда)
	Контракт = ПолучитьСсылку();
	Если ЗначениеЗаполнено(Контракт) Тогда
		ПФ = Новый Структура("Ссылка, Приложение1, Приложения2и3", Контракт, Истина, Ложь);
		ОткрытьФорму("Отчет.СведенияОКооперации.Форма.Форма", ПФ, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСведенияОКооперацииПриложения2и3(Команда)
	Контракт = ПолучитьСсылку();
	Если ЗначениеЗаполнено(Контракт) Тогда
		ПФ = Новый Структура("Ссылка, Приложение1, Приложения2и3", Контракт, Ложь, Истина);
		ОткрытьФорму("Отчет.СведенияОКооперации.Форма.Форма", ПФ, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСсылку()
	АктивныйСписок = Элементы.СписокВыполняются;
	Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.СтраницаЗавершены Тогда
		АктивныйСписок = Элементы.СписокЗавершены;
	КонецЕсли;
	Если АктивныйСписок.ТекущиеДанные <> Неопределено Тогда
		Результат = АктивныйСписок.ТекущиеДанные.Ссылка;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	Возврат Результат;
КонецФункции
