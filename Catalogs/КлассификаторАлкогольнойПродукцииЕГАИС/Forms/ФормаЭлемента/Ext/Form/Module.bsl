
//#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьИнформациюОСопоставлении();
	КонецЕсли;
	
	ПустойКонтрагент = Справочники.Контрагенты.ПустаяСсылка();
	
	Если ПустойКонтрагент = "" Или ПустойКонтрагент = Неопределено Тогда
		Элементы.ГруппаПроизводительИмпортерТолькоПросмотр.Видимость = Истина;
		Элементы.ГруппаПроизводительИмпортерРедактирование.Видимость = Ложь;
	Иначе
		Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ПустойКонтрагент))
			И Не ПравоДоступа("Чтение", Метаданные.Справочники.КлассификаторОрганизацийЕГАИС)
			Или Не ПравоДоступа("Чтение", ПустойКонтрагент.Метаданные()) Тогда
			Элементы.ГруппаПроизводительИмпортерТолькоПросмотр.Видимость = Истина;
			Элементы.ГруппаПроизводительИмпортерРедактирование.Видимость = Ложь;
		Иначе
			Элементы.ГруппаПроизводительИмпортерТолькоПросмотр.Видимость = Ложь;
			Элементы.ГруппаПроизводительИмпортерРедактирование.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ВидПродукции.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка." + ИнтеграцияЕГАИС.СправочникВидовАлкогольнойПродукции());
	
	СопоставитьИмпортераИПроизводителя();
	
	Элементы.ГруппаДанныеЕГАИС.ТолькоПросмотр = Не ОбщегоНазначенияКлиентСервер.РежимОтладки();
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = "ИзменениеСопоставленияАлкогольнойПродукцииЕГАИС" Тогда
		
		ОбновитьИнформациюОСопоставлении();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьИнформациюОСопоставлении();
	
	СобытияФормЕГАИСПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ПередЗаписью(ЭтаФорма, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОповеститьОЗаписи(ПараметрыЗаписи);
	
КонецПроцедуры

//#КонецОбласти

//#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИмпортерПриИзменении(Элемент)
	
	СопоставитьИмпортераИПроизводителя();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводительПриИзменении(Элемент)
	
	СопоставитьИмпортераИПроизводителя();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИнформациюОСопоставлении()
	
	Запрос = Новый Запрос();
	Запрос.Текст = Справочники.КлассификаторАлкогольнойПродукцииЕГАИС.ТекстЗапросаИнформацияОСопоставлении();
	Запрос.УстановитьПараметр("АлкогольнаяПродукция", Объект.Ссылка);
	
	МассивСтрок = Новый Массив();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество = 1 Тогда
			
			МассивСтрок.Добавить(ФорматированнаяСтрока(
				ИнтеграцияЕГАИСПереопределяемый.ПредставлениеНоменклатуры(
					Выборка.Номенклатура,
					Выборка.Характеристика, Неопределено),
				Новый Шрифт(,,,,Истина),
				ЦветаСтиля.ЦветГиперссылкиГИСМ,,
				"ОткрытьСоответствиеНоменклатурыЕГАИС"));
			
		ИначеЕсли Выборка.Количество > 1 Тогда
			
			МассивСтрок.Добавить(ФорматированнаяСтрока(
				СтрШаблон(НСтр("ru = '%1 ( + еще %2...)'"), Выборка.НоменклатураНаименование, Выборка.Количество - 1),
				Новый Шрифт(,,,,Истина),
				ЦветаСтиля.ЦветГиперссылкиГИСМ,,
				"ОткрытьСоответствиеНоменклатурыЕГАИС"));
			
		Иначе
			
			МассивСтрок.Добавить(ФорматированнаяСтрока(
				НСтр("ru = '<Не сопоставлено>'"),
				Новый Шрифт(,,,,Истина),
				ЦветаСтиля.ЕГАИССтатусОбработкиОшибкаПередачи,,
				"ОткрытьСоответствиеНоменклатурыЕГАИС"));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Сопоставлено = Новый ФиксированныйМассив(МассивСтрок);
	
	ИнтеграцияЕГАИСУТ.СформироватьФорматированнуюСтроку(ЭтаФорма, Сопоставлено, "ГруппаСопоставлено", "СопоставленоОбработкаНавигационнойСсылки");
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_КлассификаторАлкогольнойПродукцииЕГАИС", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура СопоставитьИмпортераИПроизводителя()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Объект.Импортер) Тогда
		КонтрагентИмпортер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Импортер, "Контрагент");
	Иначе
		КонтрагентИмпортер = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Производитель) Тогда
		КонтрагентПроизводитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Производитель, "Контрагент");
	Иначе
		КонтрагентПроизводитель = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	// Получение текстового представления
	КонтрагентИмпортерТекстовоеПредставление      = Строка(КонтрагентИмпортер);
	КонтрагентПроизводительТекстовоеПредставление = Строка(КонтрагентПроизводитель);
	ИмпортерТекстовоеПредставление                = Строка(Объект.Импортер);
	ПроизводительТекстовоеПредставление           = Строка(Объект.Производитель);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставленоОбработкаНавигационнойСсылки(Элемент, СтандартнаяОбработка)
	
	НавигационнаяСсылкаФорматированнойСтроки = ИнтеграцияЕГАИСУТКлиентСервер.НавигационнаяСсылкаСтроки(Элемент, Сопоставлено);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСоответствиеНоменклатурыЕГАИС" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИнтеграцияЕГАИСКлиент.ОткрытьФормуСопоставленияАлкогольнойПродукции(
			Объект.Ссылка,
			ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

//#КонецОбласти
