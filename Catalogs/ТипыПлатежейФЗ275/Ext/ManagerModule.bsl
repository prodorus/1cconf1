
Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт
	МакетПоставляемыеДанные = Справочники.ТипыПлатежейФЗ275.ПолучитьМакет("ПоставляемыеДанные").ПолучитьТекст();
	ТаблицаПоставляемыхДанных = ОбщегоНазначения.ПрочитатьXMLВТаблицу(МакетПоставляемыеДанные).Данные;
	
	Для Каждого СтрокаПоставляемыхДанных Из ТаблицаПоставляемыхДанных Цикл 
		ЭлементОбъект = Справочники.ТипыПлатежейФЗ275[СтрокаПоставляемыхДанных.Имя].ПолучитьОбъект();
		
		ЭлементОбъект.ПодтверждающиеДокументы.Очистить();
		
		ЭлементОбъект.ОписаниеПунктаЗакона = СтрокаПоставляемыхДанных.ОписаниеПунктаЗакона;
		ЭлементОбъект.НомерПунктаЗакона = СтрокаПоставляемыхДанных.НомерПунктаЗакона;
		ЭлементОбъект.НеТребуютсяДокументыПодтвержденияИсполненияКонтракта = Булево(СтрокаПоставляемыхДанных.НеТребуетсяДокументПодтверждающийИсполнениеРабот);
		
		// Добавим в табличную часть обязательные виды документов
		ДобавитьВидДокумента(ЭлементОбъект, Справочники.ВидыДокументов.Контракт, Булево(СтрокаПоставляемыхДанных.ТребуетсяКонтракт));
		ДобавитьВидДокумента(ЭлементОбъект, Справочники.ВидыДокументов.КредитныйДоговор, Булево(СтрокаПоставляемыхДанных.ТребуетсяКредитныйДоговор));
		ДобавитьВидДокумента(ЭлементОбъект, Справочники.ВидыДокументов.ДепозитныйДоговор, Булево(СтрокаПоставляемыхДанных.ТребуетсяДепозитныйДоговор));
		
		СортироватьПоКодуВидаДокумента(ЭлементОбъект);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ЭлементОбъект);
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьВидДокумента(Объект, ВидДокумента, ТребуетсяДобавление)
	Если ТребуетсяДобавление Тогда
		Если Объект.ПодтверждающиеДокументы.Найти(ВидДокумента , "ВидДокумента") = Неопределено Тогда
			НовыйОбязательныйДокумент = Объект.ПодтверждающиеДокументы.Добавить();
			НовыйОбязательныйДокумент.ВидДокумента = ВидДокумента;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура СортироватьПоКодуВидаДокумента(Объект)
	ВременнаяТаблица = Объект.ПодтверждающиеДокументы.Выгрузить();
	ВременнаяТаблица.Колонки.Добавить("Код");
	
	Для Каждого СтрокаТаблицы Из ВременнаяТаблица Цикл 
		СтрокаТаблицы.Код = СтрокаТаблицы.ВидДокумента.Код;
	КонецЦикла;
	
	ВременнаяТаблица.Сортировать("Код");
	
	Объект.ПодтверждающиеДокументы.Загрузить(ВременнаяТаблица);
КонецПроцедуры

Функция ПодтверждающиеВидыДокументов(Ссылка) Экспорт
	ПодтверждающиеДокументыТипПлатежа = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТипыПлатежейФЗ275ПодтверждающиеДокументы.ВидДокумента КАК ВидДокумента
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275.ПодтверждающиеДокументы КАК ТипыПлатежейФЗ275ПодтверждающиеДокументы
	|ГДЕ
	|	ТипыПлатежейФЗ275ПодтверждающиеДокументы.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДокумента");
КонецФункции
