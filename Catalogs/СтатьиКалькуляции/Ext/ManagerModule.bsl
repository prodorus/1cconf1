Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЗаполнитьОграничениеВыбора(Параметры);
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		ЗаполнитьОграничениеВыбора(Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОграничениеВыбора(Параметры)
	
	ТребуемыеСвойства = Новый Структура("КонтрактСЗаказчиком", Неопределено);
	ЗаполнитьЗначенияСвойств(ТребуемыеСвойства, Параметры);
	
	Если ЗначениеЗаполнено(ТребуемыеСвойства.КонтрактСЗаказчиком) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КонтрактСЗаказчиком", ТребуемыеСвойства.КонтрактСЗаказчиком);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.СтатьяКалькуляции
		|ИЗ
		|	Справочник.КонтрактыСЗаказчиками.КалькуляцияЗатрат КАК Т
		|ГДЕ
		|	Т.Ссылка = &КонтрактСЗаказчиком
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Ссылка
		|ИЗ
		|	Справочник.КонтрактыСЗаказчиками КАК Т
		|ГДЕ
		|	Т.Ссылка = &КонтрактСЗаказчиком
		|	И Т.Ссылка.КонтрактСУчастникомГОЗ";
		
		Пакет = Запрос.ВыполнитьПакет();
		Если Не Пакет[1].Пустой() Тогда
			Параметры.Отбор.Вставить("Ссылка", Пакет[0].Выгрузить().ВыгрузитьКолонку("СтатьяКалькуляции"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПредопределенноеОписаниеСтатьи() Экспорт
	
	Макет = ПолучитьМакет("ОписаниеСтатей");
	Для Сч = 5 по Макет.ВысотаТаблицы Цикл
		Код = Макет.Область(Сч, 1).Текст;
		Описание = Макет.Область(Сч, 3).Текст;
		Ссылка = Справочники.СтатьиКалькуляции.НайтиПоКоду(Код);
		Если Ссылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		Объект = Ссылка.ПолучитьОбъект();
		Объект.Описание = Описание;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
	КонецЦикла;
	
КонецПроцедуры
