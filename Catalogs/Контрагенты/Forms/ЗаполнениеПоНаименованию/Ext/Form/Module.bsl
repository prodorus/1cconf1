
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Наименование = Параметры.Наименование;
	
	ТР = УправлениеКонтактнойИнформацией.ТаблицаРегионов();
	
	Для Каждого СтрокаРегиона Из ТР Цикл

		Элементы.Регион.СписокВыбора.Добавить(СтрокаРегиона.Код, СтрокаРегиона.ПолноеНаименование);
		
	КонецЦикла;
	
	Элементы.Регион.СписокВыбора.СортироватьПоПредставлению();
	Элементы.Регион.СписокВыбора.Вставить(0, "", НСтр("ru='Все регионы'"));
	Регион = "";

КонецПроцедуры

&НаКлиенте
Процедура НайтиКонтрагентов(Команда)

	ОписаниеОшибки = "";
	НайтиРеквизитыНаСервере(ОписаниеОшибки);
	
	// Обработка ошибок
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			ТекстВопроса = НСтр("ru='Для автоматического заполнения реквизитов контрагентов
			|необходимо подключиться к интернет-поддержке пользователей.
			|Подключиться сейчас?'");
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, "", КодВозвратаДиалога.Нет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				#Если ТолстыйКлиентОбычноеПриложение Тогда
				ИнтернетПоддержкаПользователейКлиент.СтартоватьМеханизмИзМеню();
				#КонецЕсли
			КонецЕсли;
		ИначеЕсли ОписаниеОшибки = "НеУказанПароль" Тогда
			ТекстВопроса = НСтр("ru='Необходимо указать пароль к Интернет-поддержке пользователей
			|и установить флажок ""Запомнить меня"".
			|Указать сейчас?'");
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, "", КодВозвратаДиалога.Нет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				#Если ТолстыйКлиентОбычноеПриложение Тогда
				ИнтернетПоддержкаПользователейКлиент.СтартоватьМеханизмИзМеню();
				#КонецЕсли
			КонецЕсли;
		ИначеЕсли ОписаниеОшибки = "Сервис1СКонтрагентНеПодключен" Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ИдентификаторМестаВызова", "zapolnenie_rekvizitov");
			ОткрытьФорму("ОбщаяФорма.Сервис1СКонтрагентНеПодключен", ПараметрыФормы, ЭтаФорма);
		Иначе
			Предупреждение(ОписаниеОшибки);
		КонецЕсли;
	ИначеЕсли КоличествоНайденных > 20 Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Найдено слишком много похожих контрагентов (%1). Уточните реквизиты для поиска.'"),
			КоличествоНайденных);
		Предупреждение(ТекстПредупреждения);
	ИначеЕсли НЕ ПустаяСтрока(Наименование) И КоличествоНайденных = 0 Тогда
		Предупреждение(НСтр("ru='Ничего не найдено. Уточните реквизиты для поиска.'"));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НайтиРеквизитыНаСервере(ОписаниеОшибки)

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Информация = "";
	Контрагенты.Очистить();
	КоличествоНайденных = 0;
	
	РеквизитыКонтрагентов = СервисДанныхЕдиныхГосРеестров.РеквизитыЮрЛицПоНаименованию(Наименование, Регион, Адрес);
	Если ЗначениеЗаполнено(РеквизитыКонтрагентов.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыКонтрагентов.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	Для Каждого РеквизитыЮрЛица Из РеквизитыКонтрагентов.РеквизитыЮрЛиц Цикл
	
		НоваяСтрока = Контрагенты.Добавить();
		НоваяСтрока.ИНН = РеквизитыЮрЛица.ИНН;
		НоваяСтрока.Наименование = РеквизитыЮрЛица.НаименованиеПолное;
		Если РеквизитыЮрЛица.ЮридическийАдрес <> Неопределено Тогда
			НоваяСтрока.ЮридическийАдрес = РеквизитыЮрЛица.ЮридическийАдрес.Представление;
		КонецЕсли;
		Если РеквизитыЮрЛица.Руководитель <> Неопределено Тогда
			НоваяСтрока.Руководитель = РеквизитыЮрЛица.Руководитель.Фамилия
				+ " " + РеквизитыЮрЛица.Руководитель.Имя
				+ " " + РеквизитыЮрЛица.Руководитель.Отчество;
		КонецЕсли;
	
	КонецЦикла;
	
	КоличествоНайденных = РеквизитыКонтрагентов.КоличествоНайденных;
	
	Если КоличествоНайденных > 20 Тогда
		ТекущийЭлемент = Элементы.Наименование;
		Информация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Показаны первые 20 контрагентов из %1 найденных. Уточните реквизиты для поиска.'"), 
			КоличествоНайденных);
	ИначеЕсли КоличествоНайденных > 0 Тогда
		ТекущийЭлемент = Элементы.Контрагенты;
		Элементы.Контрагенты.ТекущаяСтрока = Контрагенты[0].ПолучитьИдентификатор();
	Иначе
		ТекущийЭлемент = Элементы.Наименование;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКонтрагента(Команда)
	
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыборКонтрагента()
	
	ТекДанные = Элементы.Контрагенты.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Если Контрагенты.Количество() = 0 Тогда
			ТекстПредупреждения = НСтр("ru='Ничего не найдено. Уточните реквизиты и нажмите ""Найти"".'");
			Предупреждение(ТекстПредупреждения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Параметры.РеквизитыЗаполнены Тогда
		ТекстВопроса = НСтр("ru='Перезаполнить текущие реквизиты?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, "", КодВозвратаДиалога.Нет);
	Иначе
		Ответ = КодВозвратаДиалога.Да;
	КонецЕсли;

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ИНН = ТекДанные.ИНН;
		ОповеститьОВыборе("ВыбранКонтрагент");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РегионПриИзменении(Элемент)
	
	Если Элементы.Регион.СписокВыбора.НайтиПоЗначению(Регион) = Неопределено Тогда
		Регион = "";
	КонецЕсли;
	
КонецПроцедуры
