&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ОтправкаКаталогаВключена(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	АдресВоВременномХранилище = Неопределено;
	ЭлектронныеДокументыКлиентПереопределяемый.ПоместитьКаталогТоваровВоВременноеХранилище(
													АдресВоВременномХранилище,
													ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор);
	
	Если АдресВоВременномХранилище = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьНовуюВерсиюЭД(ПараметрКоманды);
	
	МассивСсылок = ЭлектронныеДокументыСлужебныйКлиент.ПолучитьМассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТоварыКаталога", АдресВоВременномХранилище);
	
	ЭлектронныеДокументыСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "СформироватьУтвердитьПодписатьОтправить",
		СтруктураПараметров);
	
	Оповестить("ОбновитьСостояниеЭД");
	
КонецПроцедуры

&НаСервере
Функция ОтправкаКаталогаВключена(ПараметрКоманды)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Результат
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &Ссылка
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусПодключения = &СтатусПодключения
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ИсходящийДокумент
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать = ИСТИНА";
	Запрос.УстановитьПараметр("Ссылка", ПараметрКоманды);
	Запрос.УстановитьПараметр("ИсходящийДокумент", Перечисления.ВидыЭД.КаталогТоваров);
	Запрос.УстановитьПараметр("СтатусПодключения", Перечисления.СтатусыУчастниковОбменаЭД.Присоединен);
	Результат = Запрос.Выполнить();
	
	ОтправкаВключена = Истина;
	Если Результат.Пустой() Тогда
		
		ПараметрыЭД = Новый Структура;
		ПараметрыЭД.Вставить("Организация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "Организация"));
		ПараметрыЭД.Вставить("Контрагент", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "Контрагент"));
		ПараметрыЭД.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Исходящий);
		ПараметрыЭД.Вставить("ВидЭД", Перечисления.ВидыЭД.КаталогТоваров);
		ЭлектронныеДокументыСлужебный.СообщитьОбОтсутствииСоглашенияЭД(ПараметрыЭД, ПараметрКоманды);
		
		ОтправкаВключена = Ложь;
		
	КонецЕсли;
	
	Возврат ОтправкаВключена;
	
КонецФункции
