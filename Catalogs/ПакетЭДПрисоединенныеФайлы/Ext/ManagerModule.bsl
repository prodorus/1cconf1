
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПеренестиДанныеПрисоединенныхФайловПакетов()Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтараяТаблица.Ссылка
	|ИЗ
	|	Справочник.УдалитьПакетЭДПрисоединенныеФайлы КАК СтараяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПакетЭДПрисоединенныеФайлы КАК НоваяТаблица
	|		ПО СтараяТаблица.Наименование = НоваяТаблица.Наименование
	|			И СтараяТаблица.ВладелецФайла = НоваяТаблица.ВладелецФайла
	|ГДЕ
	|	НоваяТаблица.Ссылка ЕСТЬ NULL");
	
	Результат = Запрос.Выполнить();
	
	Пока Не Результат.Пустой() Цикл
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Попытка
				
				НачатьТранзакцию();
				
				СтарыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
				НовыйОбъект  = Справочники.ПакетЭДПрисоединенныеФайлы.СоздатьЭлемент();
				
				// Шапка.
				ЗаполнитьЗначенияСвойств(НовыйОбъект, СтарыйОбъект, , "Родитель, Владелец");
				
				// Табличные части.
				Для Каждого СтараяСтрока Из СтарыйОбъект.СертификатыШифрования Цикл
					НоваяСтрока = НовыйОбъект.СертификатыШифрования.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтараяСтрока);
				КонецЦикла;
				
				Для Каждого СтараяСтрока Из СтарыйОбъект.ЭлектронныеЦифровыеПодписи Цикл
					НоваяСтрока = НовыйОбъект.ЭлектронныеЦифровыеПодписи.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтараяСтрока);
				КонецЦикла;
				
				// Запись нового объекта.
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НовыйОбъект);
				
				// Подмена ссылки на новый объект.
				
				// Читаем старые данные.
				НаборЗаписей = РегистрыСведений.ПрисоединенныеФайлы.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ПрисоединенныйФайл.Установить(Выборка.Ссылка);
				НаборЗаписей.Прочитать();
				
				Если НаборЗаписей.Выбран() И НаборЗаписей.Количество() Тогда
					// Подменяем ссылку.
					НаборЗаписей.Отбор.ПрисоединенныйФайл.Установить(НовыйОбъект.Ссылка);
					НаборЗаписей[0].ПрисоединенныйФайл = НовыйОбъект.Ссылка;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
				
					// Удаляем старые данные.
					НаборЗаписей.Отбор.ПрисоединенныйФайл.Установить(Выборка.Ссылка);
					НаборЗаписей.Очистить();
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьОшибку(
					Нстр("ru='Перенос данных присоединенных файлов пакетов электронных документов'"), ПодробныйТекстОшибки);
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли