////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция устанавливает новое наименование серии по значениям реквизитов.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - сформированное наименование.
//
Функция СформироватьНаименование() Экспорт

	Строка = "";

	Если ЗначениеЗаполнено(СерийныйНомер) Тогда
		Строка = Строка + СерийныйНомер + ", ";
	КонецЕсли;

	Если ЗначениеЗаполнено(СрокГодности) Тогда
		Строка = Строка + Формат(СрокГодности,"ДФ=""дд.ММ.гггг""") + ", ";
	КонецЕсли;

	Если ЗначениеЗаполнено(НомерГТД) Тогда
		Строка = Строка + СокрЛП(НомерГТД.Код) + ", ";
	КонецЕсли;

	Если ЗначениеЗаполнено(СтранаПроисхождения) Тогда
		Строка = Строка + СтранаПроисхождения.Наименование + ", ";
	КонецЕсли;

	ЕстьЕГАИС = Константы.ВестиСведенияДляДекларацийПоАлкогольнойПродукции.Получить();
	
	Если ЕстьЕГАИС Тогда
		
		Если ЗначениеЗаполнено(ПроизводительЕГАИС) Тогда
			Строка = Строка + ПроизводительЕГАИС.Наименование + ", ";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Справка2ЕГАИС) Тогда
			Строка = Строка + Справка2ЕГАИС.РегистрационныйНомер + ", ";
		КонецЕсли;
		
	КонецЕсли;
	
	Строка = Лев(Строка, СтрДлина(Строка) - 2);

	Если ПустаяСтрока(Строка) Тогда
		Строка = "<Свойства не назначены>";
	КонецЕсли;

	Возврат Строка;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		
		ОсновноеИзображение = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается перед записью элемента справочника.
//
Процедура ПередЗаписью(Отказ)

	// Проверим можно ли изменять реквизиты договора.
	// Проверка осуществляется только если записывается уже существующий договор
	Если НЕ ОбменДанными.Загрузка И НЕ ЭтоНовый() Тогда

		// Проверим возможность смены способа ведения взаиморасчетов и валюты взаиморасчетов
		Если НомерГТД <> Ссылка.НомерГТД
		 ИЛИ СтранаПроисхождения <> Ссылка.СтранаПроисхождения Тогда

			Если ПолныеПрава.СерииНоменклатуры_СуществуютСсылки(Ссылка) Тогда

				Сообщить("Существуют документы, проведенные по серии """ + Наименование + """.
						 |Реквизиты ""Номер ГТД"" и ""Страна происхождения"" не могут быть изменены, элемент не записан.", 
						 СтатусСообщения.Важное);
				Отказ = Истина;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительЕГАИС");
		МассивНепроверяемыхРеквизитов.Добавить("Справка2ЕГАИС");
	ИначеЕсли НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "АлкогольнаяПродукция") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводительЕГАИС");
		МассивНепроверяемыхРеквизитов.Добавить("Справка2ЕГАИС");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

