&НаСервере
Процедура СформироватьДерево()
	
	СписокЛистов.Очистить();
	
	Если Не ЗначениеЗаполнено(АдресВременногоХранилищаЛистов) Тогда
		ВызватьИсключение НСтр("ru = 'При выводе листов произошла ошибка: адрес во временном хранилище не заполнен.'");
	КонецЕсли;
	
	РезультатФоновогоЗадания = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаЛистов);
	
	Листы = РезультатФоновогоЗадания.Листы;
	ВсегоЛистов = Листы.Количество();
	
	СведенияОбУведомлении.Вставить("ВсегоЛистов", ВсегоЛистов);
	
	Титульный = СписокЛистов.Добавить();
	Титульный.НазваниеГруппировки = "Титульный лист";
	Титульный.НачальныйНомерЛиста = 1;
	Титульный.КонечныйНомерЛиста = 1;
	НомерПервогоЛиста = 2;
	
	Если Листы.Количество()>1 
		И Листы[1].Раздел = "ТитульныйЛистФизическоеЛицо" Тогда
		ТитульныйФЛ = СписокЛистов.Добавить();
		ТитульныйФЛ.НазваниеГруппировки = "Сведения о физлице";
		ТитульныйФЛ.НачальныйНомерЛиста = 2;
		ТитульныйФЛ.КонечныйНомерЛиста = 2;
		НомерПервогоЛиста = 3;
	КонецЕсли;
	
	ПоследнийЛистДляПечати = ВсегоЛистов;
	
	Если ВсегоЛистов < 150 Тогда 
		КоличествоВГруппировке = 10;
	ИначеЕсли ВсегоЛистов < 250 Тогда 
		КоличествоВГруппировке = 20;
	ИначеЕсли ВсегоЛистов < 500 Тогда 
		КоличествоВГруппировке = 50;
	ИначеЕсли ВсегоЛистов < 1000 Тогда 
		КоличествоВГруппировке = 100;
	ИначеЕсли ВсегоЛистов < 5000 Тогда
		КоличествоВГруппировке = 200;
	Иначе
		КоличествоВГруппировке = 500;
	КонецЕсли;
	
	ВставленоВДерево = НомерПервогоЛиста - 1;
	
	Пока ВставленоВДерево < ВсегоЛистов Цикл 
		
		Строка = СписокЛистов.Добавить();
		Строка.НачальныйНомерЛиста = ВставленоВДерево + 1;
		
		Если ВставленоВДерево + КоличествоВГруппировке > ВсегоЛистов Тогда 
			Строка.КонечныйНомерЛиста = ВсегоЛистов;
		ИначеЕсли ВставленоВДерево < КоличествоВГруппировке Тогда 
			Строка.КонечныйНомерЛиста = КоличествоВГруппировке;
		Иначе 
			Строка.КонечныйНомерЛиста = ВставленоВДерево + КоличествоВГруппировке;
		КонецЕсли;
		ВставленоВДерево = Строка.КонечныйНомерЛиста;
		Строка.НазваниеГруппировки = "Листы №" + Строка.НачальныйНомерЛиста
											+ "-" + Строка.КонечныйНомерЛиста;
		
	КонецЦикла;
	
	Строка = Новый Структура("Имя,Начало,Конец", 
		Титульный.НазваниеГруппировки, Титульный.НачальныйНомерЛиста, Титульный.КонечныйНомерЛиста);
	СформироватьМакет(Строка);
	
КонецПроцедуры

&НаСервере
Функция СформироватьЛистыУведомления()
	
	СведенияОбУведомлении = Документы.УведомлениеОКонтролируемыхСделках.ПолучитьСведенияОбУведомлении(Уведомление);
	
	ДатаАктуальностиСведений = КонецГода(СведенияОбУведомлении.ОтчетныйГод);
	
	ИБФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	ДлительныеОперации.ОтменитьВыполнениеЗадания(УИДЗаданиеФормированиеЛистов);
	УИДЗаданиеФормированиеЛистов = Неопределено;
	УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "НеИспользовать");
	
	Если ИБФайловая Тогда
		ЛистыУведомления = КонтролируемыеСделки.ПолучитьЛистыУведомления(Уведомление);
		АдресВременногоХранилищаЛистов = ПоместитьВоВременноеХранилище(ЛистыУведомления, ЭтаФорма.УникальныйИдентификатор);
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор, 
			"КонтролируемыеСделки.ПолучитьЛистыУведомленияВФоне", 
			Уведомление, 
			НСтр("ru = 'Создание списка контролируемых сделок'"));
						
		АдресВременногоХранилищаЛистов = РезультатВыполнения.АдресХранилища;
		УИДЗаданиеФормированиеЛистов = РезультатВыполнения.ИдентификаторЗадания;
	КонецЕсли;

	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		СформироватьДерево();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура СформироватьЛисты(НомерНачало, НомерКонец, ТабДок)
	
	Если Не ЗначениеЗаполнено(АдресВременногоХранилищаЛистов) Тогда
		ВызватьИсключение НСтр("ru = 'При выводе листов произошла ошибка: адрес во временном хранилище не заполнен.'");
	КонецЕсли;
	
	ЛистыУведомления = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаЛистов);
	
	КонтролируемыеСделки.СформироватьЛистыУведомления(ЛистыУведомления, СведенияОбУведомлении, НомерНачало, НомерКонец, ТабДок);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьМакет(Строка)
	
	ОтображениеЛистов.Очистить();
	ОтображениеЛистов.ОбластьПечати = Неопределено;
	
	СформироватьЛисты(Строка.Начало, Строка.Конец, ОтображениеЛистов);
	
	ОтображениеЛистов.ОбластьПечати = ОтображениеЛистов.Область();
	ОтображениеЛистов.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ОтображениеЛистов.АвтоМасштаб = Истина;

КонецПроцедуры

//выполняет непосредственную печать на принетр уведомления
&НаКлиенте
Процедура СформироватьИНапечататьЛисты()
	
	ЛистДляПечати = ?(Переключатель=1, 1, ПервыйЛистДляПечати);
	ПоследнийЛист = ?(Переключатель=1, ВсегоЛистов, ПоследнийЛистДляПечати);
	КоличествоВКомплекте = 50;
	Пока ЛистДляПечати <= ПоследнийЛист Цикл 
		
		ОтображениеДляНепосредственнойПечати.Очистить();
		ОтображениеДляНепосредственнойПечати.ОбластьПечати = Неопределено;
		
		Если ЛистДляПечати = 1 Тогда 
			СтраницаКонец = 1;
		ИначеЕсли ЛистДляПечати = 2 Тогда 
			СтраницаКонец = 2;
		Иначе 
			СтраницаКонец = ?(ЛистДляПечати + КоличествоВКомплекте - 1 > ПоследнийЛист,
					ПоследнийЛист,
					ЛистДляПечати + КоличествоВКомплекте-1);
		КонецЕсли;
		СформироватьЛисты(ЛистДляПечати, СтраницаКонец, ОтображениеДляНепосредственнойПечати);
		ЛистДляПечати = СтраницаКонец + 1;
		
		ОтображениеДляНепосредственнойПечати.ОбластьПечати = ОтображениеДляНепосредственнойПечати.Область();
		ОтображениеДляНепосредственнойПечати.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ОтображениеДляНепосредственнойПечати.АвтоМасштаб = Истина;
		ОтображениеДляНепосредственнойПечати.Напечатать(РежимИспользованияДиалогаПечати.НеИспользовать);
		
	КонецЦикла;
	
КонецПроцедуры

//ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Уведомление = Параметры.Уведомление.Ссылка;
	Если Не ЗначениеЗаполнено(Уведомление) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НомераКонтролируемыхСделокКорректны = КонтролируемыеСделки.НомераКонтролируемыхСделокУведомленияКоректны(Уведомление);
	
	ПервыйЛистДляПечати = 1;
	ПоследнийЛистДляПечати = 1;

	Переключатель = 1;
	Элементы.ПервыйЛистДляПечати.Доступность = Ложь;
	Элементы.ПоследнийЛистДляПечати.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьНепосредственно(Команда)
	
	Если ПервыйЛистДляПечати > ПоследнийЛистДляПечати И 
		Переключатель = 2 Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоЛистов = ?(Переключатель = 2, ПоследнийЛистДляПечати - ПервыйЛистДляПечати + 1, ВсегоЛистов);
	Вопрос = "Распечатать уведомление о контролируемых сделках в объеме " + КоличествоЛистов + " страниц?";
	
	Если Вопрос(Вопрос, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда 
		СформироватьИНапечататьЛисты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательПриИзменении(Элемент)
	
	Элементы.ПервыйЛистДляПечати.Доступность = (Переключатель = 2);
	Элементы.ПоследнийЛистДляПечати.Доступность = (Переключатель = 2);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЛистыУведомленияВФоне()
	
	РезультатВыполнения = СформироватьЛистыУведомления();
	
	Если Не РезультатВыполнения.ЗаданиеВыполнено Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "ФормированиеОтчета");
	Иначе
		УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "НеИспользовать");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено
	 ИЛИ Элемент.ТекущаяСтрока = ТекущаяСтрокаРазделовОтчета Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	
	Строка = Новый Структура("Имя,Начало,Конец", Элемент.ТекущиеДанные.НазваниеГруппировки,
					Элемент.ТекущиеДанные.НачальныйНомерЛиста, Элемент.ТекущиеДанные.КонечныйНомерЛиста);
	СформироватьМакет(Строка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("СформироватьЛистыУведомленияСПроверкойНумерации", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(УИДЗаданиеФормированиеЛистов) Тогда 
			СформироватьДерево();
			УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "НеИспользовать");
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "НеИспользовать");
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПеренумероватьЛисты1А(Уведомление)
	
	КонтролируемыеСделки.ПеренумерацияКонтролируемыхСделокУведомления(Уведомление);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЛистыУведомленияСПроверкойНумерации()
	
	Если НЕ НомераКонтролируемыхСделокКорректны Тогда
		ТекстВопроса = НСтр("ru = 'Нумерация листов 1А не корректна.
			|Перенумеровать листы 1А?
			|(операция может занять продолжительное время)'");
		Перенумеровать = Вопрос(СтрЗаменить(ТекстВопроса, "#РазделительСтрок#", Символы.ПС), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да;
		Если Перенумеровать Тогда
			ПеренумероватьЛисты1А(Уведомление);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьСостояниеПоляТабличногоДокумента(Элементы.ОтображениеЛистов, "ФормированиеОтчета");
	ПодключитьОбработчикОжидания("СформироватьЛистыУведомленияВФоне", 0.1, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеПоляТабличногоДокумента(ПолеТабличногоДокумента, Состояние = "НеИспользовать") Экспорт
	
	Если ТипЗнч(ПолеТабличногоДокумента) = Тип("ПолеФормы") 
		И ПолеТабличногоДокумента.Вид = ВидПоляФормы.ПолеТабличногоДокумента Тогда
		ОтображениеСостояния = ПолеТабличногоДокумента.ОтображениеСостояния;
		Если ВРег(Состояние) = "НЕИСПОЛЬЗОВАТЬ" Тогда
			ОтображениеСостояния.Видимость                      = Ложь;
			ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
			ОтображениеСостояния.Картинка                       = Новый Картинка;
			ОтображениеСостояния.Текст                          = "";
		ИначеЕсли ВРег(Состояние) = "НЕАКТУАЛЬНОСТЬ" Тогда
			ОтображениеСостояния.Видимость                      = Истина;
			ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
			ОтображениеСостояния.Картинка                       = Новый Картинка;
			ОтображениеСостояния.Текст                          = НСтр("ru = 'Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета.'");;
		ИначеЕсли ВРег(Состояние) = "ФОРМИРОВАНИЕОТЧЕТА" Тогда  
			ОтображениеСостояния.Видимость                      = Истина;
			ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
			ОтображениеСостояния.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
			ОтображениеСостояния.Текст                          = НСтр("ru = 'Отчет формируется...'");
		Иначе
			ВызватьИсключение(НСтр("ru = 'Недопустимое значение параметра (параметр номер ''2'')'"));
		КонецЕсли;
	Иначе
		ВызватьИсключение(НСтр("ru = 'Недопустимое значение параметра (параметр номер ''1'')'"));
	КонецЕсли;
	
КонецПроцедуры