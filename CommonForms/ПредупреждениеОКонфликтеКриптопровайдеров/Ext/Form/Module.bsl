&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Криптопровайдер1СОтчетности = Параметры.Сертификаты[0].Криптопровайдер;
	ПредставлениеКриптопровайдера1СОтчетности = ?(ЗначениеЗаполнено(Криптопровайдер1СОтчетности),
		Криптопровайдер1СОтчетности.Представление, "");
	
	Отпечатки = Новый Массив;
	Для Каждого Сертификат Из Параметры.Сертификаты	Цикл
		Отпечатки.Добавить(Сертификат.Отпечаток);
	КонецЦикла;

	Количество = 0;
	
	ТекстКриптопровайдеры = "На вашем компьютере одновременно установлены %1 криптопровайдера: %2";
	КриптопровайдерыСтрока = "";
	ПредставленияКриптопровайдеров = Новый Массив;
	Для Каждого Криптопровайдер Из Параметры.Криптопровайдеры Цикл
		Если ПредставленияКриптопровайдеров.Найти(Криптопровайдер.Представление) = Неопределено Тогда
			КриптопровайдерыСтрока = КриптопровайдерыСтрока + Криптопровайдер.Представление + ", ";
			Количество = Количество + 1;
			ПредставленияКриптопровайдеров.Добавить(Криптопровайдер.Представление);
		КонецЕсли;
		Если Криптопровайдер.Представление <> ПредставлениеКриптопровайдера1СОтчетности Тогда
			КриптопровайдерДляДругихЦелей = Криптопровайдер;
		КонецЕсли;
	КонецЦикла;
	КриптопровайдерыСтрока = Лев(КриптопровайдерыСтрока, СтрДлина(КриптопровайдерыСтрока) - 2);
	ТекстКриптопровайдеры = СтрЗаменить(ТекстКриптопровайдеры, "%1", Количество);
	ТекстКриптопровайдеры = СтрЗаменить(ТекстКриптопровайдеры, "%2", КриптопровайдерыСтрока);
	
	Элементы.ТекстКриптопровайдеры.Заголовок = ТекстКриптопровайдеры;
	
	Рекомендация1 = "1. Если криптопровайдер %1 установлен по ошибке или он более не требуется - удалите его.
					|После этого выполните переустановку криптопровайдера %2 (используется для 1С-Отчетности).
					| ";
	Рекомендация1 = СтрЗаменить(Рекомендация1, "%1", КриптопровайдерДляДругихЦелей.Представление);
	Рекомендация1 = СтрЗаменить(Рекомендация1, "%2", ПредставлениеКриптопровайдера1СОтчетности);
	Элементы.ТекстРекомендации1.Заголовок = Рекомендация1;
	
	Индекс = 2;
	Рекомендация2 = "";
	Если ЗначениеЗаполнено(Криптопровайдер1СОтчетности) И ЗначениеЗаполнено(КриптопровайдерДляДругихЦелей)
		И (ПредставлениеКриптопровайдера1СОтчетности = КриптопровайдерCryptoProПредставление()
		ИЛИ ПредставлениеКриптопровайдера1СОтчетности = КриптопровайдерViPNetПредставление())
		И (КриптопровайдерДляДругихЦелей.Представление = КриптопровайдерCryptoProПредставление()
		ИЛИ КриптопровайдерДляДругихЦелей.Представление = КриптопровайдерViPNetПредставление()) Тогда
		Рекомендация2 = "2. Если криптопровайдер %1 требуется, например, для работы Клиент-Банка, то вы можете перейти на использование
						|единого криптопровайдера, отправив заявление на подключение к 1С-Отчетности заново.
						| ";
		Рекомендация2 = СтрЗаменить(Рекомендация2, "%1", КриптопровайдерДляДругихЦелей.Представление);
		Индекс = 3;
		
		Элементы.ТекстРекомендации2.Видимость = Истина;
	Иначе
		Элементы.ТекстРекомендации2.Видимость = Ложь;
	КонецЕсли;
	Элементы.ТекстРекомендации2.Заголовок = Рекомендация2;
	
	Рекомендация3 = СтрЗаменить("%1. Настройте отправку 1С-Отчетности с другого компьютера по ", "%1", Индекс);
	Элементы.ТекстРекомендации3.Заголовок = Рекомендация3;
	
	Индекс = Индекс + 1;
	
	Если ЕстьСертификат1СОтчетности(Отпечатки) Тогда
		Рекомендация4 = СтрЗаменить("%1. Обратиться в техподдержку 1С-Отчетности по т. 8 800 700-86-68.", "%1", Индекс);
		Элементы.ТекстРекомендации4.Видимость = Истина;
	Иначе
		Элементы.ТекстРекомендации4.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ТекстРекомендации4.Заголовок = Рекомендация4;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.ФормаЗакрыть;
	
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеПоказыватьПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаИнструкцияНажатие(Элемент)
	
	ЗапуститьПриложение("http://its.1c.ru/db/elreps#content:29:hdoc/");
	
КонецПроцедуры

&НаКлиенте
Процедура НеПоказыватьБольшеЭтоПредупреждение(Команда)
	
	НеПоказыватьБольшеЭтоПредупреждениеНаСервере(БольшеНеПоказывать);
	Закрыть();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ФормаНеПоказыватьБольшеЭтоПредупреждение.Доступность = Форма.БольшеНеПоказывать;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НеПоказыватьБольшеЭтоПредупреждениеНаСервере(БольшеНеПоказывать)
	
	ХранилищеОбщихНастроек.Сохранить("ПредупреждениеОКонфликтеКриптопровайдеров", "БольшеНеПоказывать", БольшеНеПоказывать);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьСертификат1СОтчетности(Сертификаты)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сертификаты", Сертификаты);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	УчетныеЗаписиДокументооборота.Ссылка,
	|	УчетныеЗаписиДокументооборота.СертификатРуководителя,
	|	УчетныеЗаписиДокументооборота.СпецоператорСвязи
	|ИЗ
	|	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	|ГДЕ
	|	УчетныеЗаписиДокументооборота.СпецоператорСвязи В (ЗНАЧЕНИЕ(Перечисление.СпецоператорыСвязи.КалугаАстрал), ЗНАЧЕНИЕ(Перечисление.СпецоператорыСвязи.Форус))
	|	И УчетныеЗаписиДокументооборота.СертификатРуководителя В(&Сертификаты)";
	                          
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает представление криптопровайдера CryptoPro CSP.
Функция КриптопровайдерCryptoProПредставление() Экспорт
	
	Возврат "CryptoPro CSP";
	
КонецФункции

&НаСервереБезКонтекста
// Возвращает представление криптопровайдера ViPNet CSP.
Функция КриптопровайдерViPNetПредставление() Экспорт
	
	Возврат "ViPNet CSP";
	
КонецФункции
