Функция ОстаткиЛимитовПоКонтрактам() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаОстаткиЛимитовПоКонтрактам();
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция ПланируемыеКонтрактыКОплате() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПланируемыеКонтрактыКОплате();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция РасходЛимитовПоТипу3Миллиона() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаРасходЛимитовПоТипу3Миллиона();
	
	Запрос.УстановитьПараметр("ТипПлатежа", Справочники.ТипыПлатежейФЗ275.ОплатаИныхРасходовНаСуммуНеБолееТрехМиллионовРублейВМесяц);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ТекущаяДата()));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ТекущаяДата()));
	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат РезультатЗапрос.Выгрузить();
	
КонецФункции

Функция ТекстЗапросаОстаткиЛимитовПоКонтрактам() Экспорт
	
	ТекстЗапроса = ТекстЗапросаРаспределеноВЗаявки(Истина);

	ТекстЗапросаОстаткиЛимитовПоКонтрактам = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрактыСЗаказчикамиКалькуляцияЗатрат.Ссылка КАК КонтрактСЗаказчиком,
	|	КонтрактыСЗаказчикамиКалькуляцияЗатрат.СтатьяКалькуляции,
	|	КонтрактыСЗаказчикамиКалькуляцияЗатрат.Сумма КАК СуммаПлан,
	|	ЕСТЬNULL(ДвиженияДенежныхСредствОбороты.РасходОборот, 0) КАК СуммаФакт
	|ПОМЕСТИТЬ ОстаткиПоОплате
	|ИЗ
	|	Справочник.КонтрактыСЗаказчиками.КалькуляцияЗатрат КАК КонтрактыСЗаказчикамиКалькуляцияЗатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДСпоГосконтрактам.Обороты КАК ДвиженияДенежныхСредствОбороты
	|		ПО КонтрактыСЗаказчикамиКалькуляцияЗатрат.Ссылка = ДвиженияДенежныхСредствОбороты.КонтрактСЗаказчиком
	|			И КонтрактыСЗаказчикамиКалькуляцияЗатрат.СтатьяКалькуляции = ДвиженияДенежныхСредствОбороты.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.КонтрактСЗаказчиком,
	|	ВложенныйЗапрос.КонтрактСЗаказчиком.БанковскийСчет КАК БанковскийСчет,
	|	ВложенныйЗапрос.СтатьяКалькуляции,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ), МЕСЯЦ) + 1 КАК КоличествоМесяцевКонтракта,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаОкончания, МЕСЯЦ), МЕСЯЦ) + 1 КАК ВсегоМесяцев,
	|	СУММА(ВложенныйЗапрос.СуммаПлан) КАК СуммаПлан,
	|	СУММА(ВложенныйЗапрос.СуммаФакт) КАК СуммаФакт,
	|	СУММА(ВложенныйЗапрос.СуммаФактВЗаявках) КАК СуммаФактВЗаявках,
	|	СУММА(ВложенныйЗапрос.СуммаПлан) - СУММА(ВложенныйЗапрос.СуммаФакт) - СУММА(ВложенныйЗапрос.СуммаФактВЗаявках) КАК ДоступныйОстаток
	|{ВЫБРАТЬ
	|	КонтрактСЗаказчиком.*,
	|	БанковскийСчет.*,
	|	СтатьяКалькуляции.*,
	|	СуммаПлан,
	|	СуммаФакт,
	|	СуммаФактВЗаявках,
	|	ДоступныйОстаток,
	|	КоличествоМесяцевКонтракта,
	|	ВсегоМесяцев}
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиПоОплате.КонтрактСЗаказчиком КАК КонтрактСЗаказчиком,
	|		ОстаткиПоОплате.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		ОстаткиПоОплате.СуммаПлан КАК СуммаПлан,
	|		ОстаткиПоОплате.СуммаФакт КАК СуммаФакт,
	|		0 КАК СуммаФактВЗаявках
	|	ИЗ
	|		ОстаткиПоОплате КАК ОстаткиПоОплате
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОстатковПоЗаявкам.КонтрактСЗаказчиком,
	|		ТаблицаОстатковПоЗаявкам.СтатьяКалькуляции,
	|		0,
	|		0,
	|		ТаблицаОстатковПоЗаявкам.Сумма
	|	ИЗ
	|		ТаблицаОстатковПоЗаявкам КАК ТаблицаОстатковПоЗаявкам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтрактыСЗаказчиками КАК Фильтр
	|		ПО ТаблицаОстатковПоЗаявкам.КонтрактСЗаказчиком = Фильтр.Ссылка
	|
	|) КАК ВложенныйЗапрос
	|{ГДЕ
	|	ВложенныйЗапрос.КонтрактСЗаказчиком.*,
	|	ВложенныйЗапрос.КонтрактСЗаказчиком.БанковскийСчет.* КАК БанковскийСчет,
	|	ВложенныйЗапрос.СтатьяКалькуляции.*,
	|	(РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ), МЕСЯЦ) + 1) КАК КоличествоМесяцевКонтракта,
	|	(РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаОкончания, МЕСЯЦ), МЕСЯЦ) + 1) КАК ВсегоМесяцев,
	|	ВложенныйЗапрос.СуммаПлан,
	|	ВложенныйЗапрос.СуммаФакт,
	|	ВложенныйЗапрос.СуммаФактВЗаявках,
	|	(СУММА(ВложенныйЗапрос.СуммаПлан) - СУММА(ВложенныйЗапрос.СуммаФакт) - СУММА(ВложенныйЗапрос.СуммаФактВЗаявках)) КАК ДоступныйОстаток}
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.КонтрактСЗаказчиком,
	|	ВложенныйЗапрос.КонтрактСЗаказчиком.БанковскийСчет,
	|	ВложенныйЗапрос.СтатьяКалькуляции,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ), МЕСЯЦ) + 1,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаНачала, МЕСЯЦ), НАЧАЛОПЕРИОДА(ВложенныйЗапрос.КонтрактСЗаказчиком.ДатаОкончания, МЕСЯЦ), МЕСЯЦ) + 1";
	
	ТекстЗапроса = ТекстЗапроса + ";" + ТекстЗапросаОстаткиЛимитовПоКонтрактам;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСведенияОКооперации() Экспорт
	Текст = 
	"ВЫБРАТЬ
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт КАК ГосударственныйКонтракт,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.Код КАК ГосударственныйКонтрактИдентификатор,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.Наименование КАК ГосударственныйКонтрактПредмет,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.ГодЗаключения КАК ГосударственныйКонтрактСрокиГодЗаключения,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.ГодОкончанияСрокаДействия КАК ГосударственныйКонтрактСрокиГодОкончанияСрокаДействия,
	|	0 КАК ГосударственныйКонтрактСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.СпособОпределенияПоставщика КАК ГосударственныйКонтрактСпособОпределенияПоставщика,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Владелец КАК ГоловнойИсполнитель,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Владелец.НаименованиеПолное КАК ГоловнойИсполнительНаименованиеПолное,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Контрагент КАК ГосударственныйЗаказчик,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Контрагент.НаименованиеПолное КАК ГосударственныйЗаказчикНаименованиеПолное,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком КАК КонтрактСЗаказчиком,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.ДатаНачала КАК КонтрактСЗаказчикомДатаНачала,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.ДатаОкончания КАК КонтрактСЗаказчикомДатаОкончания,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.СуммаКонтракта КАК КонтрактСЗаказчикомСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Наименование КАК КонтрактСЗаказчикомПредмет,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец КАК Исполнитель,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.НеЯвляетсяРезидентом КАК ЭтоИностранныйИсполнитель,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.НаименованиеПолное КАК ИсполнительНаименованиеПолное,
	|	ЕСТЬNULL(КонтрагентыКонтактнаяИнформация.Представление, """") КАК ИсполнительМестоНахождения,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.ИНН КАК ИсполнительИНН,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.КПП КАК ИсполнительКПП,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.КодПоОКПО КАК ИсполнительРегистрационныйНомер,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка КАК КонтрактСИсполнителем,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Основание КАК КонтрактСИсполнителемОснование,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Дата КАК КонтрактСИсполнителемДата,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Номер КАК КонтрактСИсполнителемНомер,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Наименование КАК КонтрактСИсполнителемПредмет,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.СуммаКонтракта КАК КонтрактСИсполнителемСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.СпособОпределенияПоставщика КАК КонтрактСИсполнителемСпособОпределенияПоставщика
	|{ВЫБРАТЬ
	|	ГосударственныйКонтракт.*,
	|	ГосударственныйКонтрактИдентификатор,
	|	ГосударственныйКонтрактПредмет,
	|	ГосударственныйКонтрактСрокиГодЗаключения,
	|	ГосударственныйКонтрактСрокиГодОкончанияСрокаДействия,
	|	ГосударственныйКонтрактСумма,
	|	ГосударственныйКонтрактСпособОпределенияПоставщика.*,
	|	ГоловнойИсполнитель.*,
	|	ГоловнойИсполнительНаименованиеПолное,
	|	ГосударственныйЗаказчик.*,
	|	ГосударственныйЗаказчикНаименованиеПолное,
	|	КонтрактСЗаказчиком.*,
	|	КонтрактСЗаказчикомДатаНачала,
	|	КонтрактСЗаказчикомДатаОкончания,
	|	КонтрактСЗаказчикомСумма,
	|	Исполнитель.*,
	|	ЭтоИностранныйИсполнитель,
	|	ИсполнительНаименованиеПолное,
	|	ИсполнительМестоНахождения,
	|	ИсполнительИНН,
	|	ИсполнительКПП,
	|	ИсполнительРегистрационныйНомер,
	|	КонтрактСИсполнителем.*,
	|	КонтрактСИсполнителемОснование,
	|	КонтрактСИсполнителемДата,
	|	КонтрактСИсполнителемНомер,
	|	КонтрактСИсполнителемПредмет,
	|	КонтрактСИсполнителемСумма,
	|	КонтрактСИсполнителемСпособОпределенияПоставщика.*}
	|ИЗ
	|	Справочник.КонтрактыИсполнителей.НомераКонтрактов КАК КонтрактыИсполнителейНомераКонтрактов
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|		ПО КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец = КонтрагентыКонтактнаяИнформация.Объект
	|			И (КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента))}
	|ГДЕ
	|	&ТекстОтбораПоКонтрактуЗаказчика
	|{ГДЕ
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.* КАК ГосударственныйКонтракт,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.Код КАК ГосударственныйКонтрактИдентификатор,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.Наименование КАК ГосударственныйКонтрактПредмет,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.ГодЗаключения КАК ГосударственныйКонтрактСрокиГодЗаключения,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.ГодОкончанияСрокаДействия КАК ГосударственныйКонтрактСрокиГодОкончанияСрокаДействия,
	|	(0) КАК ГосударственныйКонтрактСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Владелец.* КАК ГоловнойИсполнитель,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Владелец.НаименованиеПолное КАК ГоловнойИсполнительНаименованиеПолное,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Контрагент.* КАК ГосударственныйЗаказчик,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.Контрагент.НаименованиеПолное КАК ГосударственныйЗаказчикНаименованиеПолное,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.* КАК КонтрактСЗаказчиком,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.ДатаНачала КАК КонтрактСЗаказчикомДатаНачала,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.ДатаОкончания КАК КонтрактСЗаказчикомДатаОкончания,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.СуммаКонтракта КАК КонтрактСЗаказчикомСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.* КАК Исполнитель,
	|	(КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.НеЯвляетсяРезидентом) КАК ЭтоИностранныйИсполнитель,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.НаименованиеПолное КАК ИсполнительНаименованиеПолное,
	|	(ЕСТЬNULL(КонтрагентыКонтактнаяИнформация.Представление, """")) КАК ИсполнительМестоНахождения,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.ИНН КАК ИсполнительИНН,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.КПП КАК ИсполнительКПП,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Владелец.КодПоОКПО КАК ИсполнительРегистрационныйНомер,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.* КАК КонтрактСИсполнителем,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Основание КАК КонтрактСИсполнителемОснование,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Дата КАК КонтрактСИсполнителемДата,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Номер КАК КонтрактСИсполнителемНомер,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.Наименование КАК КонтрактСИсполнителемПредмет,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.СуммаКонтракта КАК КонтрактСИсполнителемСумма,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.ГосударственныйКонтракт.СпособОпределенияПоставщика.* КАК СпособОпределенияПоставщика,
	|	КонтрактыИсполнителейНомераКонтрактов.Ссылка.СпособОпределенияПоставщика.*}";
	
	Возврат Текст;
КонецФункции

Функция ТекстЗапросаПланируемыеКонтрактыКОплате() Экспорт
	Текст = 
	"ВЫБРАТЬ
	|	ЗаявкиНаРасходованиеДенежныхСредствОстатки.ЗаявкаНаРасходование КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|	ЗаявкиНаРасходованиеДенежныхСредствОстатки.СуммаОстаток
	|ПОМЕСТИТЬ ОстаткиЗаявок
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаРасходованиеСредств.Остатки КАК ЗаявкиНаРасходованиеДенежныхСредствОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ЗаявкаНаРасходованиеДенежныхСредствКонтракты.КонтрактСЗаказчиком,
	|	ЗаявкаНаРасходованиеДенежныхСредствКонтракты.Сумма
	|ПОМЕСТИТЬ ОстаткиПоКонтрактам
	|ИЗ
	|	ОстаткиЗаявок КАК ОстаткиЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеСредств.Контракты КАК ЗаявкаНаРасходованиеДенежныхСредствКонтракты
	|		ПО ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств = ЗаявкаНаРасходованиеДенежныхСредствКонтракты.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ВложенныйЗапрос.КонтрактСЗаказчиком,
	|	-ВложенныйЗапрос.Расход
	|ИЗ
	|	ОстаткиЗаявок КАК ОстаткиЗаявок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		ДвиженияДенежныхСредств.Регистратор.ДокументОснование КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|		ДвиженияДенежныхСредств.КонтрактСЗаказчиком КАК КонтрактСЗаказчиком,
	|		ДвиженияДенежныхСредств.Расход КАК Расход
	|	ИЗ
	|		РегистрНакопления.ДвиженияДСпоГосконтрактам КАК ДвиженияДенежныхСредств
	|	ГДЕ
	|		ДвиженияДенежныхСредств.Регистратор.ДокументОснование В
	|				(ВЫБРАТЬ
	|					ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств
	|				ИЗ
	|					ОстаткиЗаявок)
	|		И НЕ ДвиженияДенежныхСредств.Регистратор ЕСТЬ NULL ) КАК ВложенныйЗапрос
	|	ПО ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств = ВложенныйЗапрос.ЗаявкаНаРасходованиеДенежныхСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоКонтрактам.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка,
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком,
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком.БанковскийСчет КАК БанковскийСчет,
	|	СУММА(ОстаткиПоКонтрактам.Сумма) КАК Сумма
	|{ВЫБРАТЬ
	|	Заявка.*,
	|	КонтрактСЗаказчиком.*,
	|	БанковскийСчет.*,
	|	Сумма}
	|ИЗ
	|	ОстаткиПоКонтрактам КАК ОстаткиПоКонтрактам
	|{ГДЕ
	|	ОстаткиПоКонтрактам.ЗаявкаНаРасходованиеДенежныхСредств.* КАК Заявка,
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком.*,
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком.БанковскийСчет.* КАК БанковскийСчет,
	|	ОстаткиПоКонтрактам.Сумма}
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком,
	|	ОстаткиПоКонтрактам.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ОстаткиПоКонтрактам.КонтрактСЗаказчиком.БанковскийСчет
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиПоКонтрактам.Сумма) > 0";
	
	Возврат Текст;
КонецФункции

Функция ТекстЗапросаРасходЛимитовПоТипу3Миллиона() Экспорт
	ТекстЗапроса = ТекстЗапросаРаспределеноВЗаявки(Истина);

	ТекстЗапросаРасходЛимитовПоТипу3Миллиона = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОбороты.БанковскийСчетОрганизации,
	|	ДвиженияДенежныхСредствОбороты.РасходОборот КАК РасходФакт,
	|	0 КАК РасходФактВЗаявках
	|ПОМЕСТИТЬ ПолныеДанные
	|ИЗ
	|	РегистрНакопления.ДвиженияДСпоГосконтрактам.Обороты(&НачалоМесяца, &КонецМесяца, , 
	|			ТипПлатежа = ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ОплатаИныхРасходовНаСуммуНеБолееТрехМиллионовРублейВМесяц) {(СтатьяКалькуляции).* КАК СтатьяКалькуляцииВЦеляхПолученияОстатковЛимитовВРамках3Млн}) КАК ДвиженияДенежныхСредствОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатковПоЗаявкам.БанковскийСчет,
	|	0,
	|	ТаблицаОстатковПоЗаявкам.Сумма
	|ИЗ
	|	ТаблицаОстатковПоЗаявкам КАК ТаблицаОстатковПоЗаявкам
	|
	|	ГДЕ ТаблицаОстатковПоЗаявкам.ЗаявкаНаРасходованиеДенежныхСредств.ТипПлатежаФЗ275 = 
	|		ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ОплатаИныхРасходовНаСуммуНеБолееТрехМиллионовРублейВМесяц)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолныеДанные.БанковскийСчетОрганизации,
	|	СУММА(ПолныеДанные.РасходФакт) КАК РасходФакт,
	|	СУММА(ПолныеДанные.РасходФактВЗаявках) КАК РасходФактВЗаявках
	|{ВЫБРАТЬ
	|	БанковскийСчетОрганизации.*,
	|	РасходФакт,
	|	РасходФактВЗаявках}
	|ИЗ
	|	ПолныеДанные КАК ПолныеДанные
	|{ГДЕ
	|	ПолныеДанные.БанковскийСчетОрганизации.*,
	|	ПолныеДанные.РасходФакт,
	|	ПолныеДанные.РасходФактВЗаявках}
	|
	|СГРУППИРОВАТЬ ПО
	|	ПолныеДанные.БанковскийСчетОрганизации";
	
	ТекстЗапроса = ТекстЗапроса + ";" + ТекстЗапросаРасходЛимитовПоТипу3Миллиона;
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРаспределеноВЗаявки(ВоВременнуюТаблицу = Ложь) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗаявкиНаРасходованиеДенежныхСредствОстатки.ЗаявкаНаРасходование КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|	ЗаявкиНаРасходованиеДенежныхСредствОстатки.СуммаОстаток
	|ПОМЕСТИТЬ ОстаткиЗаявок
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаРасходованиеСредств.Остатки КАК ЗаявкиНаРасходованиеДенежныхСредствОстатки
	|ГДЕ
	|	ЗаявкиНаРасходованиеДенежныхСредствОстатки.СуммаОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.Ссылка КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.Ссылка.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.МестоРазмещения КАК БанковскийСчет,
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.КонтрактСЗаказчиком,
	|	ЗаявкаНаРасходованиеДенежныхСредствФинансирование.СуммаПлатежа КАК Сумма
	|ПОМЕСТИТЬ ОплатыЗаявок
	|ИЗ
	|	ОстаткиЗаявок КАК ОстаткиЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеСредств.РазмещениеЗаявки КАК ЗаявкаНаРасходованиеДенежныхСредствФинансирование
	|		ПО ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств = ЗаявкаНаРасходованиеДенежныхСредствФинансирование.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ВложенныйЗапрос.СтатьяКалькуляции,
	|	ВложенныйЗапрос.БанковскийСчет,
	|	ВложенныйЗапрос.КонтрактСЗаказчиком,
	|	-ВложенныйЗапрос.Расход
	|ИЗ
	|	ОстаткиЗаявок КАК ОстаткиЗаявок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДвиженияДенежныхСредств.Регистратор.ДокументОснование КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|			ДвиженияДенежныхСредств.БанковскийСчетОрганизации КАК БанковскийСчет,
	|			ДвиженияДенежныхСредств.КонтрактСЗаказчиком КАК КонтрактСЗаказчиком,
	|			ДвиженияДенежныхСредств.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|			ДвиженияДенежныхСредств.Расход КАК Расход
	|		ИЗ
	|			РегистрНакопления.ДвиженияДСпоГосконтрактам КАК ДвиженияДенежныхСредств
	|		ГДЕ
	|			ДвиженияДенежныхСредств.Регистратор.ДокументОснование В
	|					(ВЫБРАТЬ
	|						ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств
	|					ИЗ
	|						ОстаткиЗаявок)
	|			И НЕ ДвиженияДенежныхСредств.Регистратор ЕСТЬ NULL ) КАК ВложенныйЗапрос
	|		ПО ОстаткиЗаявок.ЗаявкаНаРасходованиеДенежныхСредств = ВложенныйЗапрос.ЗаявкаНаРасходованиеДенежныхСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатыЗаявок.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ОплатыЗаявок.БанковскийСчет,
	|	ОплатыЗаявок.КонтрактСЗаказчиком,
	|	ОплатыЗаявок.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СУММА(ОплатыЗаявок.Сумма) КАК Сумма
	|
	|ПОМЕСТИТЬ ТаблицаОстатковПоЗаявкам
	|
	|ИЗ
	|	ОплатыЗаявок КАК ОплатыЗаявок
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплатыЗаявок.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ОплатыЗаявок.БанковскийСчет,
	|	ОплатыЗаявок.КонтрактСЗаказчиком,
	|	ОплатыЗаявок.СтатьяКалькуляции
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОплатыЗаявок.Сумма) > 0";
	
	Если Не ВоВременнуюТаблицу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаОстатковПоЗаявкам", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПаспортКонтракта() Экспорт
	Текст = "ВЫБРАТЬ
	|	КонтрактыИсполнителей.Ссылка КАК Контракт,
	|	КонтрактыИсполнителей.Номер КАК КонтрактНомер,
	|	КонтрактыИсполнителей.Дата КАК КонтрактДата,
	|	КонтрактыИсполнителей.Наименование КАК КонтрактПредмет,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК КонтрактДатаНачала,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК КонтрактДатаОкончания,
	|	КонтрактыИсполнителей.КонтрактСУчастникомГОЗ КАК КонтрактСУчастникомГОЗ,
	|	КонтрактыИсполнителей.Ответственный КАК КонтрактОтветственный,
	|	КонтрактыИсполнителей.СуммаКонтракта КАК КонтрактСуммаКонтракта,
	|	0 КАК КонтрактПрибыль,
	|	КонтрактыИсполнителей.Организация КАК КонтрактЗаказчик,
	|	КонтрактыИсполнителей.Владелец КАК КонтрактИсполнитель,
	|	КонтрактыИсполнителей.ПлатежиПо275ФЗ КАК КонтрактПлатежиПо275ФЗ,
	|	КонтрактыИсполнителей.ТипПлатежаФЗ275 КАК КонтрактТипПлатежаФЗ275,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт КАК ГК,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.Код КАК ИГК,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.Наименование КАК ГКПредмет,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.ГоловнойИсполнитель КАК ГКГоловнойИсполнитель,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.ДатаЗаключения КАК ГКДатаЗаключения,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.ГодЗаключения КАК ГКГодЗаключения,
	|	КонтрактыИсполнителей.ГосударственныйКонтракт.ГодОкончанияСрокаДействия КАК ГКГодОкончания,
	|	КонтрактыИсполнителей.Организация.НаименованиеПолное КАК ЗаказчикПолноеНаименование,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия) КАК ЗаказчикСтранаРегистрации,
	|	0 КАК ЗаказчикСтранаРегистрацииКод,
	|	ЛОЖЬ КАК ЗаказчикИностранный,
	|	КонтрактыИсполнителей.Организация.ИНН КАК ЗаказчикИНН,
	|	КонтрактыИсполнителей.Организация.КПП КАК ЗаказчикКПП,
	|	"""" КАК ЗаказчикРегистрационныйНомер,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ЗаказчикДатаРегистрации,
	|	КонтрактыИсполнителей.Владелец.Наименование КАК ИсполнительПолноеНаименование,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия) КАК ИсполнительСтранаРегистрации,
	|	0 КАК ИсполнительСтранаРегистрацииКод,
	|	ЛОЖЬ КАК ИсполнительИностранный,
	|	КонтрактыИсполнителей.Владелец.ИНН КАК ИсполнительИНН,
	|	КонтрактыИсполнителей.Владелец.КПП КАК ИсполнительКПП,
	|	"""" КАК ИсполнительРегистрационныйНомер,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ИсполнительДатаРегистрации
	|ПОМЕСТИТЬ ОсновнаяИнформацияОКонтракте
	|ИЗ
	|	Справочник.КонтрактыИсполнителей КАК КонтрактыИсполнителей
	|ГДЕ
	|	КонтрактыИсполнителей.Ссылка В(&Контракт)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтрактыСЗаказчиками.Ссылка,
	|	КонтрактыСЗаказчиками.Номер,
	|	КонтрактыСЗаказчиками.Дата,
	|	КонтрактыСЗаказчиками.Наименование,
	|	КонтрактыСЗаказчиками.ДатаНачала,
	|	КонтрактыСЗаказчиками.ДатаОкончания,
	|	КонтрактыСЗаказчиками.КонтрактСУчастникомГОЗ,
	|	КонтрактыСЗаказчиками.Ответственный,
	|	КонтрактыСЗаказчиками.СуммаКонтракта,
	|	КонтрактыСЗаказчиками.СуммаПрибыли,
	|	КонтрактыСЗаказчиками.Контрагент,
	|	КонтрактыСЗаказчиками.Владелец,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПустаяСсылка),
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.Код,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.Наименование,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.ГоловнойИсполнитель,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.ДатаЗаключения,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.ГодЗаключения,
	|	КонтрактыСЗаказчиками.ГосударственныйКонтракт.ГодОкончанияСрокаДействия,
	|	КонтрактыСЗаказчиками.Контрагент.Наименование,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия),
	|	0,
	|	ЛОЖЬ,
	|	КонтрактыСЗаказчиками.Контрагент.ИНН,
	|	КонтрактыСЗаказчиками.Контрагент.КПП,
	|	"""",
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	КонтрактыСЗаказчиками.Владелец.НаименованиеПолное,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия),
	|	0,
	|	ЛОЖЬ,
	|	КонтрактыСЗаказчиками.Владелец.ИНН,
	|	КонтрактыСЗаказчиками.Владелец.КПП,
	|	"""",
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	Справочник.КонтрактыСЗаказчиками КАК КонтрактыСЗаказчиками
	|ГДЕ
	|	КонтрактыСЗаказчиками.Ссылка В(&Контракт)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контракт,
	|	КонтрактЗаказчик,
	|	КонтрактИсполнитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик КАК Заказчик,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА КонтрагентыКонтактнаяИнформация.Представление
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЗаказчикПредставлениеКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА КонтрагентыКонтактнаяИнформация.Вид
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЗаказчикВидКонтактнойИнформации,
	|	ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель КАК Исполнитель,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА КонтрагентыКонтактнаяИнформация.Представление
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИсполнительПредставлениеКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА КонтрагентыКонтактнаяИнформация.Вид
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИсполнительВидКонтактнойИнформации
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|		ПО (ВЫБОР
	|				КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|						И ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик = КонтрагентыКонтактнаяИнформация.Объект
	|					ТОГДА ИСТИНА
	|				КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|						И ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель = КонтрагентыКонтактнаяИнформация.Объект
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ОрганизацииКонтактнаяИнформация.Представление
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ОрганизацииКонтактнаяИнформация.Вид
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ОрганизацииКонтактнаяИнформация.Представление
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ОрганизацииКонтактнаяИнформация.Вид
	|		ИНАЧЕ """"
	|	КОНЕЦ
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|		ПО (ВЫБОР
	|				КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|						И ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель = ОрганизацииКонтактнаяИнформация.Объект
	|					ТОГДА ИСТИНА
	|				КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|						И ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик = ОрганизацииКонтактнаяИнформация.Объект
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.КонтрактОтветственный КАК Ответственный,
	|	ПользователиКонтактнаяИнформация.Представление КАК ОтветственныйПредставлениеКонтактнойИнформации,
	|	ПользователиКонтактнаяИнформация.Вид КАК ОтветственныйВидКонтактнойИнформации,
	|	ПользователиКонтактнаяИнформация.Вид.Тип КАК ОтветственныйТипКонтактнойИнформации
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|		ПО ОсновнаяИнформацияОКонтракте.КонтрактОтветственный = ПользователиКонтактнаяИнформация.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик КАК Заказчик,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет КАК ЗаказчикБанковскийСчет,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет.НомерСчета КАК ЗаказчикБанковскийСчетНомерСчета,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет.Банк.Наименование КАК ЗаказчикБанковскийСчетНаименованиеБанка,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет.Банк.Код КАК ЗаказчикБанковскийСчетБИКБанка,
	|	"""" КАК ЗаказчикБанковскийСчетСВИФТБанка,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет.Банк.КоррСчет КАК ЗаказчикБанковскийСчетКоррСчетБанка,
	|	КонтрактыИсполнителейНомераКонтрактов.КонтрактСЗаказчиком.БанковскийСчет.Банк.Город КАК ЗаказчикБанковскийСчетГородБанка
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтрактыИсполнителей.НомераКонтрактов КАК КонтрактыИсполнителейНомераКонтрактов
	|		ПО ОсновнаяИнформацияОКонтракте.Контракт = КонтрактыИсполнителейНомераКонтрактов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель КАК Исполнитель,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет
	|	КОНЕЦ КАК ИсполнительБанковскийСчет,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента.НомерСчета
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет.НомерСчета
	|	КОНЕЦ КАК ИсполнительБанковскийСчетНомерСчета,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента.Банк.Наименование
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК ИсполнительБанковскийСчетНаименованиеБанка,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента.Банк.Код
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет.Банк.Код
	|	КОНЕЦ КАК ИсполнительБанковскийСчетБИКБанка,
	|	"""" КАК ИсполнительБанковскийСчетСВИФТБанка,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента.Банк.КоррСчет
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет.Банк.КоррСчет
	|	КОНЕЦ КАК ИсполнительБанковскийСчетКоррСчетБанка,
	|	ВЫБОР
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыИсполнителей
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыИсполнителей).БанковскийСчетКонтрагента.Банк.Город
	|		КОГДА ОсновнаяИнформацияОКонтракте.Контракт ССЫЛКА Справочник.КонтрактыСЗаказчиками
	|			ТОГДА ВЫРАЗИТЬ(ОсновнаяИнформацияОКонтракте.Контракт КАК Справочник.КонтрактыСЗаказчиками).БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК ИсполнительБанковскийСчетГородБанка
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.Контракт,
	|	КонтрактыСЗаказчикамиПланДоходов.НомерСтроки КАК ГрафикНомерСтроки,
	|	КонтрактыСЗаказчикамиПланДоходов.Дата КАК ГрафикДата,
	|	КонтрактыСЗаказчикамиПланДоходов.Сумма КАК ГрафикСумма,
	|	КонтрактыСЗаказчикамиПланДоходов.Комментарий КАК ГрафикКомментарий,
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыКонтрактов.ПустаяСсылка) КАК ГрафикЭтапКонтракта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЭтаповКонтрактов.ПустаяСсылка) КАК ГрафикТипЭтапа,
	|	"""" КАК ГрафикПунктКонтрактаОплаты,
	|	"""" КАК ГрафикПунктКонтрактаПредмета,
	|	"""" КАК ГрафикНаименованиеПлатежа
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтрактыСЗаказчиками.ПланДоходов КАК КонтрактыСЗаказчикамиПланДоходов
	|		ПО ОсновнаяИнформацияОКонтракте.Контракт = КонтрактыСЗаказчикамиПланДоходов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.Контракт,
	|	1,
	|	ЭтапыКонтрактов.Дата,
	|	ЭтапыКонтрактов.СуммаОплаты,
	|	"""",
	|	ЭтапыКонтрактов.Ссылка,
	|	ЭтапыКонтрактов.ТипЭтапа,
	|	ЭтапыКонтрактов.ПунктКонтрактаОплаты,
	|	ЭтапыКонтрактов.ПунктКонтрактаПредмета,
	|	ЭтапыКонтрактов.Наименование
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыКонтрактов КАК ЭтапыКонтрактов
	|		ПО ОсновнаяИнформацияОКонтракте.Контракт = ЭтапыКонтрактов.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.Контракт,
	|	ОсновнаяИнформацияОКонтракте.КонтрактПрибыль,
	|	ОсновнаяИнформацияОКонтракте.КонтрактСуммаКонтракта,
	|	СУММА(КонтрактыСЗаказчикамиКалькуляцияЗатрат.Сумма) КАК КонтрактСуммаЗатрат,
	|	СУММА(КонтрактыСЗаказчикамиКалькуляцияЗатрат.СуммаКВозмещению) КАК КонтрактСтоимостьЗапасовЗаСчетСобственныхСредств
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтрактыСЗаказчиками.КалькуляцияЗатрат КАК КонтрактыСЗаказчикамиКалькуляцияЗатрат
	|		ПО ОсновнаяИнформацияОКонтракте.Контракт = КонтрактыСЗаказчикамиКалькуляцияЗатрат.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнаяИнформацияОКонтракте.Контракт,
	|	ОсновнаяИнформацияОКонтракте.КонтрактПрибыль,
	|	ОсновнаяИнформацияОКонтракте.КонтрактСуммаКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяИнформацияОКонтракте.Контракт,
	|	ОсновнаяИнформацияОКонтракте.КонтрактНомер,
	|	ОсновнаяИнформацияОКонтракте.КонтрактДата,
	|	ОсновнаяИнформацияОКонтракте.КонтрактПредмет,
	|	ОсновнаяИнформацияОКонтракте.КонтрактДатаНачала,
	|	ОсновнаяИнформацияОКонтракте.КонтрактДатаОкончания,
	|	ОсновнаяИнформацияОКонтракте.КонтрактСУчастникомГОЗ,
	|	ОсновнаяИнформацияОКонтракте.КонтрактОтветственный,
	|	ОсновнаяИнформацияОКонтракте.КонтрактСуммаКонтракта,
	|	ОсновнаяИнформацияОКонтракте.КонтрактПрибыль,
	|	ОсновнаяИнформацияОКонтракте.КонтрактЗаказчик,
	|	ОсновнаяИнформацияОКонтракте.КонтрактИсполнитель,
	|	ОсновнаяИнформацияОКонтракте.КонтрактПлатежиПо275ФЗ,
	|	ОсновнаяИнформацияОКонтракте.КонтрактТипПлатежаФЗ275,
	|	ОсновнаяИнформацияОКонтракте.ГК,
	|	ОсновнаяИнформацияОКонтракте.ИГК,
	|	ОсновнаяИнформацияОКонтракте.ГКПредмет,
	|	ОсновнаяИнформацияОКонтракте.ГКГоловнойИсполнитель,
	|	ОсновнаяИнформацияОКонтракте.ГКДатаЗаключения,
	|	ОсновнаяИнформацияОКонтракте.ГКГодЗаключения,
	|	ОсновнаяИнформацияОКонтракте.ГКГодОкончания,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикПолноеНаименование,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикСтранаРегистрации,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикСтранаРегистрацииКод,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикИностранный,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикИНН,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикКПП,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикРегистрационныйНомер,
	|	ОсновнаяИнформацияОКонтракте.ЗаказчикДатаРегистрации,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительПолноеНаименование,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительСтранаРегистрации,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительСтранаРегистрацииКод,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительИностранный,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительИНН,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительКПП,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительРегистрационныйНомер,
	|	ОсновнаяИнформацияОКонтракте.ИсполнительДатаРегистрации
	|ИЗ
	|	ОсновнаяИнформацияОКонтракте КАК ОсновнаяИнформацияОКонтракте";
	
	Возврат Текст;
КонецФункции

//#Область "Формирование платежных поручений"
Функция НайтиПодчиненныеПП(Заявки) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка КАК ПП,
	               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.ДокументПланированияПлатежа КАК Заявка
	               |ИЗ
	               |	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ПлатежноеПоручениеИсходящееРасшифровкаПлатежа
	               |ГДЕ
	               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.ДокументПланированияПлатежа В(&МассивЗаявок)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.ДокументПланированияПлатежа,
	               |	ПлатежноеПоручениеИсходящееРасшифровкаПлатежа.Ссылка
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПП)
	               |ПО
	               |	Заявка";
				   
	Если ТипЗнч(Заявки) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеСредств") Тогда
		МассивЗаявок = Новый Массив(1);
		МассивЗаявок[0] = Заявки;
	Иначе
		МассивЗаявок    = Заявки;
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивЗаявок", МассивЗаявок);
	РезультатЗапроса = Запрос.Выполнить();
	Результат = Новый Соответствие();
	ВыборкаЗаявок = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаявок.Следующий() Цикл
		МассивПП = Новый Массив();
		ВыборкаПП = ВыборкаЗаявок.Выбрать();
		Пока ВыборкаПП.Следующий() Цикл
			МассивПП.Добавить(ВыборкаПП.ПП);
		КонецЦикла;
		Если МассивПП.Количество() > 0 Тогда
			Результат.Вставить(ВыборкаЗаявок.Заявка, МассивПП);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Процедура УдалитьПодчиненныеПП(Платежки) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Не ТипЗнч(Платежки) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ПП Из Платежки Цикл
		ПредставлениеПП = Строка(ПП);
		ППОбъект = ПП.ПолучитьОбъект();
		ППОбъект.Удалить();
		Сообщить(СтрШаблон("     Удален документ: %1", ПредставлениеПП));
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Функция СформироватьПП(Заявка, ПлатежкиПоЗаявке) Экспорт
	
	УдалитьПодчиненныеПП(ПлатежкиПоЗаявке);
	
	ЗаявкаОбъект = Заявка.ПолучитьОбъект();
	
	Всего = ЗаявкаОбъект.РазмещениеЗаявки.Итог("СуммаПлатежа");
	
	СчПП = 0;
	Для Каждого СтрокаРазмещения Из ЗаявкаОбъект.РазмещениеЗаявки Цикл
		ПП = Документы.ПлатежноеПоручениеИсходящее.СоздатьДокумент();
		
		// Шапка
		ПП.Дата = ТекущаяДата();
		ПП.Заполнить(Заявка);
		ПП.ДатаОплаты = СтрокаРазмещения.ДатаПлатежа;
		ПП.СчетОрганизации = СтрокаРазмещения.МестоРазмещения;
		ПП.СуммаДокумента = СтрокаРазмещения.СуммаПлатежа;
		
		Если ЗаявкаОбъект.ПлатежПоФЗ275 Тогда
			Договор = ЗаявкаОбъект.ТекущийДоговорКонтрагента();
			Контракт = ОбщегоНазначения275КлиентСервер.НайтиПодчиненныйКонтракт(Договор);
			Если ТипЗнч(Контракт) = Тип("СправочникСсылка.КонтрактыИсполнителей") Тогда
				Если ЗначениеЗаполнено(Контракт.БанковскийСчетКонтрагента) Тогда
					ПП.СчетКонтрагента = Контракт.БанковскийСчетКонтрагента;
				КонецЕсли;
				Если ЗначениеЗаполнено(Контракт.ГосударственныйКонтракт) Тогда
					ПП.ИдентификаторПлатежа = Контракт.ГосударственныйКонтракт.Код;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ПП.ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПеречислениеНалога Тогда
			ПП.ПеречислениеВБюджет = Истина;
			ПП.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
			СтруктураРеквизитов = Документы.ПлатежноеПоручениеИсходящее.РеквизитыПлатежаВБюджетПоУмолчанию(
				ПП.Дата, ПП.Организация, ПП.СчетКонтрагента, ПП.ПеречислениеВБюджет);
			ЗаполнитьЗначенияСвойств(ПП, СтруктураРеквизитов);
		КонецЕсли;

		АвтоЗначенияРеквизитов = ФормированиеПечатныхФорм.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
			ПП.Организация, ПП.СчетОрганизации, ПП.Контрагент, ПП.СчетКонтрагента, 
				ПП.ВидОперации, ПП.ПеречислениеВБюджет);
				
		ПП.ТекстПлательщика = АвтоЗначенияРеквизитов.ТекстПлательщика;
		ПП.ИННПлательщика   = АвтоЗначенияРеквизитов.ИННПлательщика;
		ПП.КПППлательщика   = АвтоЗначенияРеквизитов.КПППлательщика;

		ПП.ТекстПолучателя  = АвтоЗначенияРеквизитов.ТекстПолучателя;
		ПП.ИННПолучателя    = АвтоЗначенияРеквизитов.ИННПолучателя;
		ПП.КПППолучателя    = АвтоЗначенияРеквизитов.КПППолучателя;
		
		БУ = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОтражатьДокументыВБухгалтерскомУчете");
		НУ = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОтражатьДокументыВНалоговомУчете");
		ПП.ОтражатьВБухгалтерскомУчете = ЗаявкаОбъект.Организация.ОтражатьВРегламентированномУчете И БУ;
		ПП.ОтражатьВНалоговомУчете = ЗаявкаОбъект.Организация.ОтражатьВРегламентированномУчете И НУ;
		
		//Расшифровка платежа
		КоличествоСтрок = ЗаявкаОбъект.РасшифровкаПлатежа.Количество();
		Доля = СтрокаРазмещения.СуммаПлатежа / Всего;
		ПП.РасшифровкаПлатежа.Очистить();
		Для Сч = 0 По КоличествоСтрок - 2 Цикл
			СтрокаРасшифровки = ЗаявкаОбъект.РасшифровкаПлатежа[Сч];
			НоваяСтрока = ПП.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасшифровки);
			
			НоваяСтрока.СуммаПлатежа = СтрокаРасшифровки.СуммаПлатежа * Доля;
			НоваяСтрока.СуммаВзаиморасчетов = СтрокаРасшифровки.СуммаВзаиморасчетов * Доля;
			НоваяСтрока.СуммаПлатежаПлан = НоваяСтрока.СуммаПлатежа;
			НоваяСтрока.СтавкаНДС = ЗаявкаОбъект.СтавкаНДС;
			УправлениеДенежнымиСредствами.ПересчитатьСуммуНДС(НоваяСтрока);
			УправлениеДенежнымиСредствами.ЗаполнитьСчетаУчетаПлатежейБУ(ПП,глЗначениеПеременной("глТекущийПользователь"),Истина);
			УправлениеДенежнымиСредствами.УстановитьСтатьюДДСПоУмолчанию(НоваяСтрока, ПП.ВидОперации);
			НоваяСтрока.ДокументПланированияПлатежа = Заявка;
		КонецЦикла;
		// Последняя строка
		Остаток = ПП.СуммаДокумента - ПП.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		
		СтрокаРасшифровки = ЗаявкаОбъект.РасшифровкаПлатежа[КоличествоСтрок - 1];
		НоваяСтрока = ПП.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасшифровки);
		
		НоваяСтрока.СуммаПлатежа = Остаток;
		НоваяСтрока.СуммаВзаиморасчетов = Остаток;
		НоваяСтрока.СуммаПлатежаПлан = НоваяСтрока.СуммаПлатежа;
		НоваяСтрока.СтавкаНДС = ЗаявкаОбъект.СтавкаНДС;
		УправлениеДенежнымиСредствами.ПересчитатьСуммуНДС(НоваяСтрока);
		УправлениеДенежнымиСредствами.ЗаполнитьСчетаУчетаПлатежейБУ(ПП,глЗначениеПеременной("глТекущийПользователь"),Истина);
		УправлениеДенежнымиСредствами.УстановитьСтатьюДДСПоУмолчанию(НоваяСтрока, ПП.ВидОперации);
		НоваяСтрока.ДокументПланированияПлатежа = Заявка;
	
		ПП.Записать();
		Попытка
			ПП.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("     Создан документ: "+Строка(ПП));
			СчПП = СчПП + 1;
		Исключение
			Инфо = ИнформацияОбОшибке();
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось провести "+Строка(ПП));
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат СчПП;
	
КонецФункции
//#КонецОбласти "Формирование платежных поручений"
