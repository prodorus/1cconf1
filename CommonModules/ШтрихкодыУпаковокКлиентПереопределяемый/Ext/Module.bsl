//#Область ПрограммныйИнтерфейс

// Получает данные для печати и открывает форму обработки печати этикеток и ценников.
//
// Параметры:
//	ОписаниеКоманды - Структура - структура с описанием команды.
//
// Возвращаемое значение:
//	Неопределено
//
Функция ПечатьШтрихкодовУпаковок(ОбъектыПечати, Форма) Экспорт
	
	//++ НЕ ГОСИС
	
	ОписаниеКоманды = Новый Структура;
	ОписаниеКоманды.Вставить("Вид",           "Печать");
	ОписаниеКоманды.Вставить("Идентификатор", "ШтрихкодыУпаковокПоПереданнымПараметрам");
	ОписаниеКоманды.Вставить("Обработчик",    "ПечатьШтрихкодовУпаковок.ПечатьШтрихкодовУпаковок");
	ОписаниеКоманды.Вставить("ОбъектыПечати", ОбъектыПечати);
	ОписаниеКоманды.Вставить("Представление", НСтр("ru = 'Печать штрихкодов упаковок!'"));
	ОписаниеКоманды.Вставить("Форма",         Форма);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ШтрихкодыУпаковокТоваров_ШтрихкодыУпаковок";
	
	ПараметрыМакетов = Справочники.ШтрихкодыУпаковокТоваров.ПараметрыМакетовДляПечати();
	
	Для каждого ОбъектПечати Из ОбъектыПечати Цикл
		
		Если ОбъектПечати.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.SSCC
			И ОбъектПечати.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.Code128
			И ОбъектПечати.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.GS1_128
			И ОбъектПечати.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked
			И ОбъектПечати.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.PDF417 Тогда
			
			ИнтеграцияЕГАИСКлиентСервер.СообщитьПользователюВФорму(,
				СтрШаблон(НСтр("ru = 'Штрихкод %1 не распечатан, т.к. тип штрихкода %2 не поддерживается.'"), 
				ИнтеграцияЕГАИСКлиентСервер.ПредставлениеШтрихкода(ОбъектПечати.Штрихкод), 
				ОбъектПечати.ТипШтрихкода));
				
			Продолжить;
			
		КонецЕсли;
		
		ПараметрыШтрихкодовУпаковок = Справочники.ШтрихкодыУпаковокТоваров.ПараметрыШтрихкодовУпаковокДляПечати();
		ЗаполнитьЗначенияСвойств(ПараметрыШтрихкодовУпаковок, ОбъектПечати);
		ПараметрыШтрихкодовУпаковок.ЗначениеШтрихкода = ОбъектПечати.Штрихкод;
		
		Справочники.ШтрихкодыУпаковокТоваров.ДобавитьШтрихкодВТабличныйДокумент(
			ТабличныйДокумент,
			ПараметрыМакетов,
			ПараметрыШтрихкодовУпаковок);
		
	КонецЦикла;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабличныйДокумент,,, "Штрихкоды упаковок");
	//-- НЕ ГОСИС
	
КонецФункции

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыДляШтрихкодовУпаковок(ОбъектыПечати, Форма)
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище"       , ПоместитьВоВременноеХранилище(ОбъектыПечати, Форма.УникальныйИдентификатор));
	ПараметрыПечати.Вставить("КоличествоЭкземпляров" , 1);
	
	Возврат ПараметрыПечати;
	
КонецФункции

//#КонецОбласти
