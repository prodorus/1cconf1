/////////////////////////////////////////////////////////////////////
//
// Адаптационный модуль. При необходимости здесь можно реализовать 
//  переход на используемую библиотеку.
//
/////////////////////////////////////////////////////////////////////
Функция НайтиНаФормеподменюПечать(Знач Элемент)
	
	Если ТипЗнч(Элемент) <> Тип("ТаблицаФормы")
		И ТипЗнч(Элемент) <> Тип("УправляемаяФорма")
		И ТипЗнч(Элемент) <> Тип("ГруппаФормы") Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Элемент) = Тип("ТаблицаФормы")
		Или ТипЗнч(Элемент) = Тип("УправляемаяФорма") Тогда
		
		Результат = НайтиНаФормеПодменюПечать(Элемент.КоманднаяПанель);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Подчиненные = Элемент.ПодчиненныеЭлементы;
	Результат = Подчиненные.Найти("ПодменюПечать");
	
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для каждого ПодчиненныйЭлемент Из Подчиненные Цикл
		
		Результат = НайтиНаФормеподменюПечать(ПодчиненныйЭлемент);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции // НайтиНаФормеподменюПечать(Форма)

Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	Если ТипЗнч(Форма) =Тип("УправляемаяФорма") Тогда
		
		ИмяФормы = Форма.ИмяФормы;
		
		Если (СтрНайти(ИмяФормы, "ЗапросАкцизныхМарокЕГАИС") > 0
			Или СтрНайти(ИмяФормы, "ШтрихкодыУпаковокТоваров") > 0)
			И СтрНайти(ИмяФормы, "Выбора") = 0 Тогда
			
			ПодменюПечать = НайтиНаФормеподменюПечать(Форма);
			
			Если ПодменюПечать <> Неопределено Тогда
				
				КомандаПечати = Форма.Команды.Добавить("ФормаПечатьЭтикетокШтрихкода");
				КомандаПечати.Действие  = "Подключаемый_ВыполнитьКоманду";
				КомандаПечати.Заголовок = "Печать этикеток штрихкода";
				
				КнопкаПечати = Форма.Элементы.Добавить("ПечатьЭтикетокШтрихкода", Тип("КнопкаФормы"), ПодменюПечать);
				КнопкаПечати.ИмяКоманды = "ФормаПечатьЭтикетокШтрихкода";
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Форма, Контекст, Объект, Результат) Экспорт
КонецПроцедуры
