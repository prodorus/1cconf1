////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры работы с номенклатурой и связанными справочниками
//
////////////////////////////////////////////////////////////////////////////////

// Программный интерфейс

// Формирует наименование элемента справочника по заданному для вида номенклатуры шаблону.
//
// Параметры:
//	ШаблонНаименования - Строка - шаблон наименования, заданный в виде номенклатуры;
//	ОбъектСправочник - СправочникОбъект.Номенклатура, СправочникОбъект.ХарактеристикиНоменклатуры,
//		СправочникСсылка.Номенклатура, СправочникСсылка.ХарактеристикиНоменклатуры - номенклатура или характеристика, для 
//		которой нужно получить наименование по заданному в настройках шаблону.
//
// Возвращаемое значение:
//	Строка - Наименование полученное по алгоритму расчета;
//	Пустая строка - если не удалось сформировать наименование или не заполнены операнды алгоритма;
//
Функция НаименованиеПоШаблону (Знач ШаблонНаименования, ОбъектСправочник) Экспорт
	
	ФормулаНаименованияСтруктура = ФормулаНаименования(ШаблонНаименования, ОбъектСправочник);
	ФормулаНаименования = """"" + " + СтрЗаменить(ФормулаНаименованияСтруктура.ФормулаНаименования,
		"МассивЗначенийРеквизитов[",
		"Параметры[");
	
	Наименование = "";
	
	Если ЗначениеЗаполнено(ФормулаНаименования) Тогда
		
		Попытка
			
			Наименование = ОбщегоНазначения.ВычислитьВБезопасномРежиме(
				ФормулаНаименования,
				ФормулаНаименованияСтруктура.МассивЗначенийРеквизитов);
			
		Исключение
			
			ШаблонСообщенияОбОшибке = НСтр("ru = 'Невозможно сформировать наименование по заданному для вида номенклатуры ""%ВидНоменклатуры%"" шаблону. Проверьте правильность шаблона.'");
			
			Если ТипЗнч(ОбъектСправочник) = Тип("СправочникОбъект.Номенклатура")
			 ИЛИ ТипЗнч(ОбъектСправочник) = Тип("СправочникОбъект.СерииНоменклатуры") Тогда
				
				ВидНоменклатуры = ОбъектСправочник.ВидНоменклатуры;
				
			Иначе
				
				Если ТипЗнч(ОбъектСправочник.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
					
					ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСправочник.Владелец, "ВидНоменклатуры");
					
				ИначеЕсли ТипЗнч(ОбъектСправочник.Владелец) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
					
					ВидНоменклатуры = ОбъектСправочник.Владелец;
					
				КонецЕсли;
				
			КонецЕсли;
			
			СообщениеОбОшибке = СтрЗаменить(ШаблонСообщенияОбОшибке, "%ВидНоменклатуры%", ВидНоменклатуры);
			ВызватьИсключение СообщениеОбОшибке;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

// Подставляет в шаблон наименования значения реквизитов.
//
// Параметры:
//  ШаблонНаименования	 - Строка							 - шаблон наименования, заданный в виде номенклатуры
//  ОбъектСправочник	 - СправочникОбъект, СправочникСсылка	 - объект, для которого нужно расчитать наименование.
// 
// Возвращаемое значение:
//  Структура - Структура с ключами:
//  *ФормулаНаименования - Строка - программный код, который нужно выполнить с помощью фукнции Выполнить для расчета наименования
//  *МассивЗначенийРеквизитов - Массив - значения реквизитов, используемых в формуле наименования
//  *ИндексыНаименованияВМассивеЗначенийРеквизитов - Массив - массив индексов элементов МассивЗначенийРеквизитов,
//  	которые хранят значение реквизита "Наименование".
//
Функция ФормулаНаименования(Знач ШаблонНаименования, Знач ОбъектСправочник) Экспорт
	
	Результат = Новый Структура(
		"ФормулаНаименования, МассивЗначенийРеквизитов, ИндексыНаименованияВМассивеЗначенийРеквизитов",
		"" + ШаблонНаименования, Новый Массив, Новый Массив);
		
	ПримитивныеТипы = Новый ОписаниеТипов("Число,Строка,Дата,Булево,ХранилищеЗначения,УникальныйИдентификатор");
	
	Для СчетчикОперандов = 0 По СтрДлина(ШаблонНаименования) Цикл
		
		// Ищем в формуле очередной операнд вида [...]
		ПервыйСимвол    = СтрНайти(ШаблонНаименования, "[");
		ПоследнийСимвол = СтрНайти(ШаблонНаименования, "]");
		
		Если ПервыйСимвол = 0 Или ПоследнийСимвол = 0 ИЛИ ПервыйСимвол > ПоследнийСимвол Тогда
			Прервать; // больше операндов в формуле нет
		КонецЕсли;
		
		// Свойства текущего операнда
		ИмяОперанда 	 = Сред(ШаблонНаименования, ПервыйСимвол + 1, ПоследнийСимвол - ПервыйСимвол - 1);
		ЗначениеОперанда = ОбъектСправочник;
		
		// Разложим имя операнда на его составляющие (реквизиты и доп. реквизиты), разделенные символом "."
		// Ограничение ни имена доп. реквизитов: нельзя использовать символ ".", иначе будет ошибка расчета значения операнда.
		МассивРеквизитовОперанда = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяОперанда, ".");
		
		Для СчетчикРеквизитов = 0 По МассивРеквизитовОперанда.Количество()-1 Цикл
			
			ИмяРеквизита = МассивРеквизитовОперанда[СчетчикРеквизитов];
			
			Если СчетчикРеквизитов > 0 И НЕ ЗначениеЗаполнено(ЗначениеОперанда) Тогда
				// Если реквизит предыдущего уровня имеет пустое значение, то дальше нечего вычислять.
				Прервать;
			КонецЕсли;
			
			Если НЕ ПримитивныеТипы.СодержитТип(ТипЗнч(ЗначениеОперанда)) Тогда
				ИмяОбъектаМетаданных = ЗначениеОперанда.Метаданные().ПолноеИмя();
			Иначе
				// Ошибка в значении реквизита предыдущего уровня - у него нет метода Метаданные().
				// Например если значение имеет тип Строка, а у него пытаются получить какое-то свойство через ".".
				ВызватьИсключениеПоОшибкеШаблонаНаименования(ИмяОперанда, ИмяРеквизита);
			КонецЕсли;
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта(ИмяОбъектаМетаданных, ИмяРеквизита) Тогда
				
				// Это реквизит
				Если СчетчикРеквизитов > 0 ИЛИ ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЗначениеОперанда)) Тогда
					ЗначениеОперанда = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеОперанда, ИмяРеквизита);
				Иначе
					ЗначениеОперанда = ЗначениеОперанда[ИмяРеквизита];
				КонецЕсли;
				
				Если ИмяРеквизита = "Наименование" И МассивРеквизитовОперанда.Количество() = 1 Тогда
					Результат.ИндексыНаименованияВМассивеЗначенийРеквизитов.Добавить(СчетчикОперандов);
				КонецЕсли;
				
			Иначе
				
				// Это доп. реквизит
				ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяРеквизита, Истина);
				
				Если НЕ ЗначениеЗаполнено(ДопРеквизит) Тогда
					// Ошибка в операнде - текущий реквизит не является не реквизитом предыдущего значения, и его не доп. реквизитом.
					ВызватьИсключениеПоОшибкеШаблонаНаименования(ИмяОперанда, ИмяРеквизита);
				КонецЕсли;
				
				Если СчетчикРеквизитов > 0 ИЛИ ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЗначениеОперанда)) Тогда
					
					Запрос = Новый Запрос;
					Запрос.Текст =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ДополнительныеРеквизиты.Значение КАК Значение
					|ИЗ
					|	" + ИмяОбъектаМетаданных + ".ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
					|ГДЕ
					|	ДополнительныеРеквизиты.Ссылка = &Ссылка
					|	И ДополнительныеРеквизиты.Свойство = &Свойство";
					
					Запрос.УстановитьПараметр("Ссылка",   ЗначениеОперанда);
					Запрос.УстановитьПараметр("Свойство", ДопРеквизит);
					
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						ЗначениеОперанда = Выборка.Значение;
					Иначе
						ЗначениеОперанда = "";
					КонецЕсли;
					
				Иначе
					
					СтрокаДопРеквизита = ЗначениеОперанда.ДополнительныеРеквизиты.Найти(ДопРеквизит, "Свойство");
					ЗначениеОперанда   = ?(СтрокаДопРеквизита <> Неопределено, СтрокаДопРеквизита.Значение, "");
					
				КонецЕсли;
				
				
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.МассивЗначенийРеквизитов.Добавить(
			?(МассивРеквизитовОперанда.Количество() > 0 И ЗначениеЗаполнено(ЗначениеОперанда), ЗначениеОперанда, ""));
		
		Результат.ФормулаНаименования = СтрЗаменить(
			Результат.ФормулаНаименования,
			"[" + ИмяОперанда + "]",
			"МассивЗначенийРеквизитов[" + СчетчикОперандов + "]");
		ШаблонНаименования = СтрЗаменить(
			ШаблонНаименования,
			"[" + ИмяОперанда + "]",
			"");
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Процедура устанавливает параметры выбора для номенклатуры.
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа,
//	ЭлементНоменклатура - ПолеФормы - Поле для ввода номенклатуры,
//	ИспользоватьНаборы - Булево, Истина - признак использования наборов номенклатуры.
//
Процедура УстановитьПараметрыВыбораНоменклатуры(ХозяйственнаяОперация, ЭлементНоменклатура, ИспользоватьНаборы = Ложь) Экспорт
	
	МассивПараметров = Новый Массив;
	
	МассивТиповНоменклатуры = Новый Массив();
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	СписокОперацийБезУслуг = СписокХозяйственныхОперацийИсключающихУслуги();
	Если СписокОперацийБезУслуг.НайтиПоЗначению(ХозяйственнаяОперация) = Неопределено Тогда
		МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
		МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	КонецЕсли;
	
	Если ИспользоватьНаборы Тогда
		МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Набор"));
	КонецЕсли;
	
	ФиксированныйМассивТиповНоменклатуры = Новый ФиксированныйМассив(МассивТиповНоменклатуры);
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", ФиксированныйМассивТиповНоменклатуры));
	
	ЭлементНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры // УстановитьПараметрыВыбораНоменклатуры()

// Функция возвращает список хозяйственных операций в которых не используется номенклатура
// с типом "Услуга" или "Работа".
//
// Возвращаемое значение:
//	СписокЗначений - список операций.
//
Функция СписокХозяйственныхОперацийИсключающихУслуги() Экспорт
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПриемНаКомиссию);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка);
	//++ НЕ УТКА
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаДавальцу);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОтДавальца);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья);
	//-- НЕ УТКА
	
	Возврат СписокОпераций;
	
КонецФункции

// Функция возвращает параметры проверки заполнения характеристик номенклатуры
//
//	Возвращаемое значение:
//		Структура  - структура со следующими ключами:
//			*ИмяТЧ - Строка - значение по умолчанию "Товары"
//			*СуффиксДопРеквизита - Строка - значение по умолчанию "" - если в ТЧ два реквизита "Характеристика", то второй назван с суффиком. 
//											если суффикс передан, то проверяются оба реквизита
//          *СписокСтрок - Массив, Неопределенно - значение по умолчанию Неопределенно
//			*ВыводитьНомераСтрок - Булево - значение по умолчанию Истина
//			*ВыдаватьСообщения - Булево - значение по умолчанию пустая ИСТИНА - если ЛОЖЬ, но не будут выдаваться сообщения, просто выставится Отказ.
//			*ПутьКДаннымТаблицыФормы - Строка - значение по умолчанию "Объект" - путь к реквизиту формы, содержащий проверяемую табличную часть.
//                                                                               Если проверяется таблица, которая сама является реквизитом формы, нужно
//                                                                               передать "" (т.е. пустую строку)
//			*ИмяКолонкиХарактеристикаТаблицыФормы - Структура - значение по умолчанию "Характеристика" - имя колонки таблицы формы, рядом с которой нужно
//																вывести сообщение. Нужно переопределять, когда вместо колонки характеристики отображатеся какая-то
//																другая колонка, например, с какими-то строковыми представлениями.
//
Функция ПараметрыПроверкиЗаполненияХарактеристик() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ИмяТЧ",                 "Товары");	
	ПараметрыПроверки.Вставить("ПредставлениеТЧ",       "");
	ПараметрыПроверки.Вставить("СуффиксДопРеквизита",   "");	
	ПараметрыПроверки.Вставить("СписокСтрок",           Неопределено);
	ПараметрыПроверки.Вставить("ВыводитьНомераСтрок",   Истина);
	ПараметрыПроверки.Вставить("ОтборПроверяемыхСтрок", Новый Структура);
	ПараметрыПроверки.Вставить("ВыдаватьСообщения", Истина);
	ПараметрыПроверки.Вставить("ПутьКДаннымТаблицыФормы", "Объект");
	ПараметрыПроверки.Вставить("ИмяКолонкиХарактеристикаТаблицыФормы", "Характеристика");
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Процедура проверки заполнения колонок "Характеристика" в объектах.
//
// Параметры:
//	Объект - ДокументОбъект (СправочникОбъект и т.п.) - объект, для которого требуется проверить заполнение 
//		колонки "Характеристика" в табличной части;
//	МассивНепроверяемыхРеквизитов -Массив - реквизиты, которые не нужно проверять платформенной проверкой;
//	Отказ - Булево, Истина - признак отказа продолжения операции;
//	ПараметрыПроверки - Структура - см. НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик.
//
Процедура ПроверитьЗаполнениеХарактеристик(Объект,МассивНепроверяемыхРеквизитов,Отказ,ПараметрыПроверки = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользованиеХарактеристикНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ПараметрыПроверкиЗаполненияХарактеристик();
	КонецЕсли;
	
	ИмяТЧ                 = ПараметрыПроверки.ИмяТЧ;
	СуффиксДопРеквизита   = ПараметрыПроверки.СуффиксДопРеквизита;
	СписокСтрок           = ПараметрыПроверки.СписокСтрок;
	ВыводитьНомераСтрок   = ПараметрыПроверки.ВыводитьНомераСтрок;
	
	МассивНепроверяемыхРеквизитов.Добавить(ИмяТЧ+".Характеристика");
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		МассивНепроверяемыхРеквизитов.Добавить(ИмяТЧ+".Характеристика"+СуффиксДопРеквизита);
	КонецЕсли;
	
	КлючДанных = ИнтеграцияЕГАИСПереопределяемый.КлючДанныхДляСообщенияПользователю(Объект);
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки,";
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|	ТаблицаТоваров.Номенклатура"+СуффиксДопРеквизита+" КАК Номенклатура"+СуффиксДопРеквизита+",
	|	ТаблицаТоваров.Характеристика"+СуффиксДопРеквизита+" КАК Характеристика"+СуффиксДопРеквизита+",";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ СтрокиСОшибками
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|	ИЛИ ТаблицаТоваров.Характеристика"+СуффиксДопРеквизита+" = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиСОшибками.НомерСтроки,";
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "	
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура" + СуффиксДопРеквизита +" КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
		|			И СтрокиСОшибками.Характеристика"+СуффиксДопРеквизита+" = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеЗаполненаХарактеристика" + СуффиксДопРеквизита +",";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
	|			И СтрокиСОшибками.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаполненаХарактеристика
	|ИЗ
	|	СтрокиСОшибками КАК СтрокиСОшибками
	|ГДЕ
	|	ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
	| 	И СтрокиСОшибками.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|	ИЛИ ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура" + СуффиксДопРеквизита +" КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
	|			И СтрокиСОшибками.Характеристика"+СуффиксДопРеквизита+" = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	СтрокаДопРеквизитов = ?(Не ЗначениеЗаполнено(СуффиксДопРеквизита), "", ",Номенклатура"+СуффиксДопРеквизита+",Характеристика"+СуффиксДопРеквизита);
	
	ОтборСтрокДляПроверки = Неопределено;
	
	Если СписокСтрок = Неопределено
		И ПараметрыПроверки.ОтборПроверяемыхСтрок.Количество() > 0 Тогда
		ОтборСтрокДляПроверки = ПараметрыПроверки.ОтборПроверяемыхСтрок;
	Иначе
		ОтборСтрокДляПроверки = СписокСтрок;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",
		Объект[ИмяТЧ].Выгрузить(ОтборСтрокДляПроверки, "НомерСтроки,Номенклатура,Характеристика"+СтрокаДопРеквизитов));
	
	Если ВыводитьНомераСтрок Тогда
		ШаблонСообщения = НСтр("ru='Не заполнена колонка ""%Характеристика%"" в строке %НомерСтроки% списка ""%ТаблицаТовары%"".'");
	Иначе
		ШаблонСообщения = НСтр("ru='Не заполнена колонка ""%Характеристика%"" в списке ""%ТаблицаТовары%"".'");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не ПараметрыПроверки.ВыдаватьСообщения Тогда
		
		Если Не РезультатЗапроса.Пустой() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если ТипЗнч(Объект) = Тип("УправляемаяФорма") Тогда
		МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	Иначе
		МетаданныеОбъекта = Объект.Метаданные();
	КонецЕсли;
	
	ПредставлениеТЧ = ?(Не ПараметрыПроверки.Свойство("ПредставлениеТЧ") ИЛИ Не ЗначениеЗаполнено(ПараметрыПроверки.ПредставлениеТЧ),
		МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним,
		ПараметрыПроверки.ПредставлениеТЧ);
	ПредставлениеРеквизитаХарактеристика = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты.Характеристика.Синоним;
	ПредставлениеРеквизитаХарактеристикаДоп = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты["Характеристика"+СуффиксДопРеквизита].Синоним;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеЗаполненаХарактеристика Тогда
			
			ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Характеристика%", ПредставлениеРеквизитаХарактеристика);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТаблицаТовары%", ПредставлениеТЧ);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ,
																	Выборка.НомерСтроки,
																	ПараметрыПроверки.ИмяКолонкиХарактеристикаТаблицыФормы);
			                                                                           
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
																КлючДанных,
																Поле,
																ПараметрыПроверки.ПутьКДаннымТаблицыФормы,
																Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СуффиксДопРеквизита)
			И Выборка["НеЗаполненаХарактеристика" + СуффиксДопРеквизита] Тогда
			
			ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Характеристика%", ПредставлениеРеквизитаХарактеристикаДоп);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТаблицаТовары%", ПредставлениеТЧ);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ,
																	Выборка.НомерСтроки,
																	ПараметрыПроверки.ИмяКолонкиХарактеристикаТаблицыФормы + СуффиксДопРеквизита);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
																КлючДанных,
																Поле,
																ПараметрыПроверки.ПутьКДаннымТаблицыФормы,
																Отказ);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

// Процедура проверки заполнения колонок "Содержание" в формах.
//
// Параметры:
//	Объект - ДокументОбъект (СправочникОбъект и т.п.) - объект, для которого требуется проверить заполнение 
//		колонки "Содержание" в табличной части;
//	Отказ - Булево, Истина - признак отказа продолжения операции;
//	ИмяТЧ - Строка - имя табличной части в объекте.
//
Процедура ПроверитьЗаполнениеСодержания(Объект, Отказ, ИмяТЧ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки,
	|	ТаблицаТоваров.Номенклатура
	|ПОМЕСТИТЬ СтрокиСОшибками
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Содержание = """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиСОшибками.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	СтрокиСОшибками КАК СтрокиСОшибками
	|ГДЕ
	|	СтрокиСОшибками.Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.АктВыполненныхРабот)
	|";
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ИмяТЧ].Выгрузить(,"НомерСтроки,Номенклатура,Содержание"));
	Выборка = Запрос.Выполнить().Выбрать();
	
	КлючДанных = ИнтеграцияЕГАИСПереопределяемый.КлючДанныхДляСообщенияПользователю(Объект);
	
	МетаданныеОбъекта = Объект.Метаданные();

	ПредставлениеТЧ                 = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним;

	ШаблонСообщения = НСтр("ru='Не заполнена колонка """"Содержание"""" в строке %НомерСтроки% списка ""%ТаблицаТовары%"".'");

	Пока Выборка.Следующий() Цикл
					
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ТаблицаТовары%", ПредставлениеТЧ);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
					
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Выборка.НомерСтроки, "Содержание");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,"Объект",Отказ);

	КонецЦикла;
		
КонецПроцедуры

// Функция определяет наличие в табличной части работ.
//
// Параметры:
//	ТЧ - ДанныеФормыКоллекция - проверяемая табличная часть.
//
// Возвращаемое значение:
//	Структура:
//		* ЕстьРаботы - Булево, Истина - в ТЧ используются работы.
//
Функция ПроверитьНаличиеРабот(ТЧ) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ТаблицаТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ 
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 
	|	ИСТИНА КАК ЕстьРаботы
	|ИЗ 
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ 
	|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры 
	|	= ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	Запрос.УстановитьПараметр("ТаблицаТовары", ТЧ.Выгрузить(, "Номенклатура"));
	РезультатЗапроса = Запрос.Выполнить();
	ТипыНоменклатуры = Новый Структура("ЕстьРаботы", Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ТипыНоменклатуры.ЕстьРаботы = Истина;
		
	КонецЕсли;
	
	Возврат ТипыНоменклатуры;
	
КонецФункции //ПроверитьНаличиеРабот()

// Функция возвращает параметры проверки заполнения упаковок
//
//	Возвращаемое значение:
//		Структура - структура со следующими ключами:
//			*ИмяТЧ - Строка - значение по умолчанию "Товары"
//			*ИмяПоляУпаковка - Строка - значение по умолчанию "Упаковка"
//			*ВыводитьНомераСтрок - Булево - значение по умолчанию Истина
//			*ОтборПроверяемыхСтрок - Структура - значение по умолчанию пустая Структура.
//
Функция ПараметрыПроверкиЗаполненияУпаковок() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ИмяТЧ",                 "Товары");
	ПараметрыПроверки.Вставить("ИмяПоляУпаковка",       "Упаковка");
	ПараметрыПроверки.Вставить("ВыводитьНомераСтрок", Истина);
	ПараметрыПроверки.Вставить("ОтборПроверяемыхСтрок", Новый Структура);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Процедура проверки заполнения колонок "Упаковка" в документах по адресному складу. Указание упаковок обязательно для товаров
// и необязательно для многоборотной тары.
//
// Параметры:
//	Объект - ДокументОбъект (СправочникОбъект и т.п.) - объект, для которого требуется проверить заполнение 
//		колонки "Упаковка" в табличной части;
//	МассивНепроверяемыхРеквизитов -Массив - реквизиты, которые не нужно проверять платформенной проверкой;
//	Отказ - Булево, Истина - признак отказа продолжения операции;
//	ПараметрыПроверки - Структура - см. НоменклатураСервер.ПараметрыПроверкиЗаполненияУпаковок.
//
Процедура ПроверитьЗаполнениеУпаковок(Объект,МассивНепроверяемыхРеквизитов,Отказ,ПараметрыПроверки = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ПараметрыПроверкиЗаполненияУпаковок();
	КонецЕсли;
	
	ИмяТЧ                 = ПараметрыПроверки.ИмяТЧ;
	ИмяПоляУпаковка       = ПараметрыПроверки.ИмяПоляУпаковка;
	ВыводитьНомераСтрок   = ПараметрыПроверки.ВыводитьНомераСтрок;
	ОтборПроверяемыхСтрок = ПараметрыПроверки.ОтборПроверяемыхСтрок;
	
	Если МассивНепроверяемыхРеквизитов.Найти(ИмяТЧ+"." + ИмяПоляУпаковка) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить(ИмяТЧ+"." + ИмяПоляУпаковка);
	КлючДанных = ИнтеграцияЕГАИСПереопределяемый.КлючДанныхДляСообщенияПользователю(Объект);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	&ИмяПоляУпаковка КАК Упаковка
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения).ТипИзмеряемойВеличины, ЛОЖЬ) <> ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
	|	И ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|	И ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения.ТипИзмеряемойВеличины В (ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук), ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ИмяПоляУпаковка","ТаблицаТоваров." + ИмяПоляУпаковка);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Если ОтборПроверяемыхСтрок.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ТаблицаТоваров",
			Объект[ИмяТЧ].Выгрузить(ОтборПроверяемыхСтрок, "НомерСтроки,Номенклатура,"+ИмяПоляУпаковка));
	Иначе
		Запрос.УстановитьПараметр("ТаблицаТоваров",
			Объект[ИмяТЧ].Выгрузить(, "НомерСтроки,Номенклатура,"+ИмяПоляУпаковка));
	КонецЕсли;
	
	Если ВыводитьНомераСтрок Тогда
		ШаблонСообщения = НСтр("ru='Не заполнена колонка ""%Упаковка%"" в строке %НомерСтроки% списка ""%ТаблицаТовары%"".'");
	Иначе
		ШаблонСообщения = НСтр("ru='Не заполнена колонка ""%Упаковка%"" в списке ""%ТаблицаТовары%"".'");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ТипЗнч(Объект) = Тип("УправляемаяФорма") Тогда
		МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	Иначе
		МетаданныеОбъекта = Объект.Метаданные();
	КонецЕсли;
	ПредставлениеТЧ                = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним;
	ПредставлениеРеквизитаУпаковка = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты[ИмяПоляУпаковка].Синоним;
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Упаковка%", ПредставлениеРеквизитаУпаковка);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТаблицаТовары%", ПредставлениеТЧ);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Выборка.НомерСтроки, ИмяПоляУпаковка);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,"Объект",Отказ);
	КонецЦикла;
	
КонецПроцедуры

// Процедура проверки соответствия вида номенклатуры товаров другого качества виду номенклатуры исходного качества.
// Для номенклатуры, по которой ведется учет серий, товары другого качества должны быть одного вида с исходными.
//
// Параметры:
//	Объект - ДокументОбъект - документ, для которого проверяется соответствие номенклатуры;
//	Отказ - Булево - отказ продолжения операции;
//	ИмяТЧДляПроверки - Строка - значение по умолчанию "Товары";
//	ТаблицаДляПроверки - ТаблицаЗначений, Неопределено - если передана таблица значений, то проверяется она, попытка выгрузки из объекта не делается:
//		* НомерСтроки - Число;
//		* Номенклатура - СправочникСсылка.Номенклатура;
//		* НоменклатураОприходование - СправочникСсылка.Номенклатура.
//
Процедура ПроверитьВидНоменклатурыОприходования(Объект, Отказ, ИмяТЧДляПроверки = "Товары", ТаблицаДляПроверки = Неопределено) Экспорт
	
	// Номенклатура с разным качеством должна быть совместимма по настройкам серий:
	// - или у серий должен быть один владелец (тогдан настройки учета совпадают)
	// - или учета серий быть не должно.
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|	Товары.НоменклатураОприходование КАК НоменклатураБрак
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДляЗапроса.НомерСтроки,
	|	ТоварыДляЗапроса.Номенклатура,
	|	ТоварыДляЗапроса.НоменклатураБрак
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК ТоварыДляЗапроса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДляЗапроса.НомерСтроки,
	|	ТоварыДляЗапроса.Номенклатура КАК Номенклатура,
	|	ТоварыДляЗапроса.НоменклатураБрак КАК НоменклатураБрак
	|ИЗ
	|	ТоварыДляЗапроса КАК ТоварыДляЗапроса";
	
	Если ТаблицаДляПроверки <> Неопределено Тогда
		Запрос.УстановитьПараметр("Товары", ТаблицаДляПроверки);
	Иначе
		Запрос.УстановитьПараметр("Товары",
			Объект[ИмяТЧДляПроверки].Выгрузить(,"НомерСтроки,Номенклатура,НоменклатураОприходование"));
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = НСтр("ru='По номенклатурам %Номенклатура% и %НоменклатураБрак%(испорченный товар) различаются настройки учета по сериям.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", Выборка.Номенклатура);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НоменклатураБрак%", Выборка.НоменклатураБрак);
		Если ТаблицаДляПроверки = Неопределено Тогда
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧДляПроверки,Выборка.НомерСтроки,"НоменклатураОприходование");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Объект,Поле,,Отказ);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает значение коэффициента пересчета единицы измерения в метрические тонны.
// Например, для килограмма коэфициент = 0.001.
//
// Параметры:
//	УпаковкаЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка / единица измерения, 
//		для которой осуществляется пересчет.
//
// Возвращаемое значение:
//	Число - коэффициент.
//
Функция КоэффициентПересчетаВТонны(УпаковкаЕдиницаИзмерения) Экспорт
	
	КоэффициентПересчетаВТонны = 0;
	
	Если НЕ ЗначениеЗаполнено(УпаковкаЕдиницаИзмерения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не удалось рассчитать коэффициент пересчета в тонны, т.к. единица измерения веса не заполнена. Обратитесь к администратору.'"));
		Возврат КоэффициентПересчетаВТонны;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
	|				И УпаковкиЕдиницыИзмерения.Знаменатель <> 0
	|			ТОГДА УпаковкиЕдиницыИзмерения.Числитель / УпаковкиЕдиницыИзмерения.Знаменатель
	|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
	|				И УпаковкиЕдиницыИзмерения.ВесЕдиницаИзмерения.Знаменатель <> 0
	|			ТОГДА УпаковкиЕдиницыИзмерения.Вес * УпаковкиЕдиницыИзмерения.ВесЕдиницаИзмерения.Числитель / УпаковкиЕдиницыИзмерения.ВесЕдиницаИзмерения.Знаменатель
	|		ИНАЧЕ 0
	|	КОНЕЦ / 1000 КАК КоэффициентПересчетаВТонны
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|ГДЕ
	|	УпаковкиЕдиницыИзмерения.Ссылка = &УпаковкаЕдиницаИзмерения";
	
	Запрос.УстановитьПараметр("УпаковкаЕдиницаИзмерения", УпаковкаЕдиницаИзмерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоэффициентПересчетаВТонны = Выборка.КоэффициентПересчетаВТонны;
	КонецЕсли;
	
	Если КоэффициентПересчетаВТонны = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось рассчитать коэффициент пересчета в тонны для единицы ""%1"". Обратитесь к администратору.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Строка(УпаковкаЕдиницаИзмерения));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат КоэффициентПересчетаВТонны;
	
КонецФункции

// Возвращает значение коэффициента единицы измерения в кубические метры.
//
// Параметры:
//  УпаковкаЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка / единица измерения,
//  	для которой осуществляется пересчет.
// 
// Возвращаемое значение:
//  Число - коэффициент пересчета.
//
Функция КоэффициентПересчетаВКубическиеМетры(УпаковкаЕдиницаИзмерения) Экспорт
	
	КоэффициентПересчетаВКубическиеМетры = 0;
	
	Если НЕ ЗначениеЗаполнено(УпаковкаЕдиницаИзмерения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не удалось рассчитать коэффициент пересчета в кубические метры, т.к. единица измерения объема не заполнена. Обратитесь к администратору.';"));
		Возврат КоэффициентПересчетаВКубическиеМетры;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Объем)
	|				И УпаковкиЕдиницыИзмерения.Знаменатель <> 0
	|			ТОГДА УпаковкиЕдиницыИзмерения.Числитель / УпаковкиЕдиницыИзмерения.Знаменатель
	|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
	|				И УпаковкиЕдиницыИзмерения.ОбъемЕдиницаИзмерения.Знаменатель <> 0
	|			ТОГДА УпаковкиЕдиницыИзмерения.Объем * УпаковкиЕдиницыИзмерения.ОбъемЕдиницаИзмерения.Числитель / УпаковкиЕдиницыИзмерения.ОбъемЕдиницаИзмерения.Знаменатель
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентПересчетаВКубическиеМетры
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|ГДЕ
	|	УпаковкиЕдиницыИзмерения.Ссылка = &УпаковкаЕдиницаИзмерения";
	
	Запрос.УстановитьПараметр("УпаковкаЕдиницаИзмерения", УпаковкаЕдиницаИзмерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоэффициентПересчетаВКубическиеМетры = Выборка.КоэффициентПересчетаВКубическиеМетры;
	КонецЕсли;
	
	Если КоэффициентПересчетаВКубическиеМетры = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось рассчитать коэффициент пересчета в кубические метры для единицы ""%1"". Обратитесь к администратору.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Строка(УпаковкаЕдиницаИзмерения));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат КоэффициентПересчетаВКубическиеМетры;
	
КонецФункции

// Процедура заполняет страну происхождения.
//
// Параметры:
//	ТаблицаФормы - ДанныеФормыКоллекция - табличная часть, в которой осуществляется заполнение 
//		колонки "СтранаПроисхождения".
//
Процедура ЗаполнитьСтрануПроисхождения(ТаблицаФормы) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеТовары") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаФормы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки,
	                      |	ВЫРАЗИТЬ(Таблица.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	                      |	ВЫРАЗИТЬ(Таблица.НомерГТД КАК Справочник.НомераГТД) КАК НомерГТД,
	                      |	ВЫРАЗИТЬ(Таблица.ВедетсяУчетПоГТД КАК БУЛЕВО) КАК ВедетсяУчетПоГТД
	                      |ПОМЕСТИТЬ Таблица
	                      |ИЗ
	                      |	&Таблица КАК Таблица
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Таблица.НомерСтроки КАК НомерСтроки,
	                      |	Таблица.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения
	                      |ИЗ
	                      |	Таблица КАК Таблица
	                      |ГДЕ
	                      |	Таблица.ВедетсяУчетПоГТД");
	
	Запрос.УстановитьПараметр(
		"Таблица",
		ТаблицаФормы.Выгрузить(,"НомерСтроки, Номенклатура, НомерГТД, ВедетсяУчетПоГТД"));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаФормы[Выборка.НомерСтроки-1], Выборка, "СтранаПроисхождения");
	КонецЦикла;
	
КонецПроцедуры

// Процедуры работы с сериями

// Процедура заполняет статусы указания серий в строках товарной табличной части.
//
// Параметры:
//	Объект					- ДанныеФормыСтуктура	- Основной реквизит формы объекта.
//	ПараметрыУказанияСерий	- Структура				- Состав полей определен в функции 
//														НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Процедура ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий, СтрокиТоваровДляОбработки = Неопределено,
													СтрокиСерийДляОбработки = Неопределено) Экспорт
	
	Если Не (ПараметрыУказанияСерий.ТоварВШапке
		Или Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Количество() <> 0 ) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		
		Если ПолучитьФункциональнуюОпцию("ИспользованиеСерийНоменклатуры") Тогда
			Если ПараметрыУказанияСерий.ТоварВШапке Тогда
				Объект.СтатусУказанияСерий = 0;	
			Иначе
				Для Каждого СтрТабл Из Объект[ПараметрыУказанияСерий.ИмяТЧТовары] Цикл
					Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
						СтрТабл.СтатусУказанияСерий = 0;
					Иначе
						Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
							СтрТабл[ИмяПоляСтатус] = 0;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено
		Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено;
	
	Если Не СкладыВТЧ
		И ПараметрыУказанияСерий.ИмяПоляСклад <> Неопределено Тогда
		
		Если ПараметрыУказанияСерий.Свойство("ИмяПоляСкладОтправитель")
			И ПараметрыУказанияСерий.ИмяПоляСкладОтправитель <> Неопределено
			И ПараметрыУказанияСерий.ИмяПоляСкладПолучатель <> Неопределено Тогда
			Склад = Новый Структура("Отправитель,Получатель");
			Склад.Отправитель = Объект[ПараметрыУказанияСерий.ИмяПоляСкладОтправитель];
			Склад.Получатель  = Объект[ПараметрыУказанияСерий.ИмяПоляСкладПолучатель];
		Иначе
			Склад = Объект[ПараметрыУказанияСерий.ИмяПоляСклад];
		КонецЕсли;
		
	Иначе
		Склад = Неопределено;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Серия",Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
		ТаблицаТоваров.Колонки.Добавить("НомерСтроки",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный)));
		
		Если ЗначениеЗаполнено(ПараметрыУказанияСерий.ИмяТЧСерии) Тогда
			ТаблицаСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии];
		Иначе
			ТаблицаСерий = Новый ТаблицаЗначений;
			ТаблицаСерий.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
			ТаблицаСерий.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
			ТаблицаСерий.Колонки.Добавить("Серия",Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
			ТаблицаСерий.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
			
			СтрокаСерии = ТаблицаСерий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСерии, Объект);
		КонецЕсли;
		
		ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров);
		
		СтрокаТовара = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовара,Объект);
		СтрокаТовара.НомерСтроки = 1;
		
		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТовара);
		НайденныеСтрокиСерий = ТаблицаСерий.НайтиСтроки(СтруктураПоиска);
		
		Выборка = ВыборкаИзЗапросаРасчетаСтатусовУказанияСерий(ПараметрыУказанияСерий,
																ТаблицаТоваров,
																ТаблицаСерий,
																Склад,
																,
																НайденныеСтрокиСерий);
		
		Если Выборка.Следующий() Тогда
			
			Объект.СтатусУказанияСерий = Выборка.СтатусУказанияСерий;
			
			Если Выборка.СтатусУказанияСерий <> 14 Тогда
				Объект.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
	Иначе
		
		Выборка = ВыборкаИзЗапросаРасчетаСтатусовУказанияСерий(ПараметрыУказанияСерий,
																Объект[ПараметрыУказанияСерий.ИмяТЧТовары],
																Объект[ПараметрыУказанияСерий.ИмяТЧСерии],
																Склад,
																СтрокиТоваровДляОбработки,
																СтрокиСерийДляОбработки);
		
		Пока Выборка.Следующий() Цикл
			СтрТабл = Объект[ПараметрыУказанияСерий.ИмяТЧТовары][Выборка.НомерСтроки - 1];
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				СтрТабл.СтатусУказанияСерий = Выборка.СтатусУказанияСерий;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					СтрТабл[ИмяПоляСтатус] = Выборка[ИмяПоляСтатус];
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	
	Возврат;
	
КонецПроцедуры

// Сравнивает значения переданных массивов
//
// Параметры:
//	Массив1, Массив2 - сравниваемые массивы
//	ПорядокИмеетЗначения - Булево - если Истина, то одинаковые значения должны иметь одинаковые индексы в массиве.
//
// Возвращаемое значение:
//	Истина, если массивы равны
//
Функция МассивыРавны(Массив1, Массив2, ПорядокИмеетЗначения = Истина) Экспорт
	
	Если Не Массив1.Количество() = Массив2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПорядокИмеетЗначения Тогда
		
		Для Индекс = 0 По Массив1.Количество() - 1 Цикл
			Если Не Массив1[Индекс] = Массив2[Индекс] Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого ТекущийЭлемент Из Массив1 Цикл
			Если Массив2.Найти(ТекущийЭлемент) = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Сравнивает значения переданных структур по указанным свойствам
// Параметры
// 	Структура1,Структура2 - сравниваемые структуры
//	Свойства - имена свойств, по которым нужно проводить сравнение, 
//			Тип - массив строк с именами свойств, или строка (имена перечисляются через запятую)
// Возвращаемое значение - Истина, если структуры равны по значениям переданных свойств.
//
Функция СтруктурыРавны(Структура1, Структура2, Свойства = Неопределено) Экспорт
	
	ПоВсемСвойствам = Ложь;
	
	Если ТипЗнч(Свойства) = Тип("Строка") Тогда
		МассивСвойств = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Свойства);
	ИначеЕсли Свойства <> Неопределено Тогда 
		МассивСвойств = Свойства;
	Иначе
		ПоВсемСвойствам = Истина;
	КонецЕсли;
	
	Если ПоВсемСвойствам Тогда
		
		Для Каждого КлючЗначение Из Структура1 Цикл
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("ТаблицаЗначений") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("Массив")
				И Не МассивыРавны(КлючЗначение.Значение, Структура2[КлючЗначение.Ключ]) Тогда
				Возврат Ложь;
			ИначеЕсли (ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
					ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Соответствие"))
				И Не СтруктурыРавны(КлючЗначение.Значение, Структура2[КлючЗначение.Ключ]) Тогда
				Возврат Ложь;
			ИначеЕсли Не (ТипЗнч(КлючЗначение.Значение) = Тип("Массив")
					ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
					ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Соответствие"))
					И Не КлючЗначение.Значение = Структура2[КлючЗначение.Ключ] Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Для Каждого СтрМас Из МассивСвойств Цикл
			Если ТипЗнч(Структура1[СтрМас]) = Тип("ТаблицаЗначений") Тогда
				Продолжить;
			КонецЕсли;
			Если ТипЗнч(Структура1[СтрМас]) = Тип("Массив")
				И Не МассивыРавны(Структура1[СтрМас], Структура2[СтрМас]) Тогда
				Возврат Ложь;
			ИначеЕсли (ТипЗнч(Структура1[СтрМас]) = Тип("Структура")
					ИЛИ ТипЗнч(Структура1[СтрМас]) = Тип("Соответствие"))
				И Не СтруктурыРавны(Структура1[СтрМас], Структура2[СтрМас]) Тогда
				Возврат Ложь;
			ИначеЕсли Не (ТипЗнч(Структура1[СтрМас]) = Тип("Массив")
					ИЛИ ТипЗнч(Структура1[СтрМас]) = Тип("Структура")
					ИЛИ ТипЗнч(Структура1[СтрМас]) = Тип("Соответствие"))
					И Не Структура1[СтрМас] = Структура2[СтрМас] Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Процедура пересчитывает статусы указания серий в строках товаров, если это необходимо,
//  переподчиняет строки серий другим строкам ТЧ "Товары".
//
// Параметры:
//  Объект						 - ДанныеФормыСтуктура	 - основной реквизит формы документа.
//  ПараметрыУказанияСерий		 - Структура - структура параметров указания серий, возвращаемая соотвествующей процедурой модуля менеджера документа.
//  ТекущаяСтрокаИдентификатор	 - Число - идентификатор текущей строки товаров в форме документа.
//  КэшированныеЗначения		 - Структура - структура кеша реквизитов текущей строки товаров.
//
Процедура ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект,
																			ПараметрыУказанияСерий,
																			ТекущаяСтрокаИдентификатор,
																			КэшированныеЗначения) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		Возврат; // Если ТЧ Серии нет, тогда все статусы пересчитываются при изменении реквизитов ТЧ, а не при окончании редактирования
	КонецЕсли;
	
	Если ТекущаяСтрокаИдентификатор <> Неопределено Тогда
		ТекущаяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	Иначе
		ТекущаяСтрока = Неопределено //значит строку удалили;
	КонецЕсли;
	
	ТекстПоляСвязи = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас  ;
	КонецЦикла;
	
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено; 
	
	// Если строка новая (в т.ч. скопированная) или используется разделение по вариантам продажи - будет закешированно Неопределено
	// Тогда не нужно искать строки со старыми значениями.
	Если КэшированныеЗначения.Номенклатура <> Неопределено Тогда
		
		СтруктураПоискаСтарыеЗначения = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
		ЗаполнитьЗначенияСвойств(СтруктураПоискаСтарыеЗначения,КэшированныеЗначения);
		
		НайденныеСтрокиТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоискаСтарыеЗначения);
		НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаСтарыеЗначения);
	Иначе
		НайденныеСтрокиТоваров = Новый Массив;
		НайденныеСтрокиСерий   = Новый Массив;
	КонецЕсли;
	
	// Если поменялись ключевые поля:
	// - возможно нужно переподчинять серии
	// - статус указания серий нужно пересчитвать в строках по новым ключевым полям и по старым.
	Если ТекущаяСтрока <> Неопределено 
		И Не СтруктурыРавны(КэшированныеЗначения, ТекущаяСтрока, "Номенклатура,Характеристика"+ТекстПоляСвязи) Тогда
		
		УчитыватьОстатки      = Ложь;
		ПереподчинитьСерии    = Ложь;
		ПересчитатьКоличество = Ложь;
		// Определелим, нужно ли переподчинять серии. Это нужно если:
		// - серии относились только к одной строке
		// - новые и старые ключевые поля поддерживают одну политику учета
		// - серии относились к нескольким строкам, но изменилось значение действия по отражению расхождения со строкой
		// Если строки нужно переподчинять, то определим, нужно ли пересчитывать количество.
		Если КэшированныеЗначения.Номенклатура <> Неопределено
			И ((КэшированныеЗначения.Свойство("Действие")
					И КэшированныеЗначения.Действие <> ТекущаяСтрока.Действие)
				Или НайденныеСтрокиТоваров.Количество() = 0) Тогда//т.к. строк с такими ключевыми полями не осталось, значит такая строка была одна
			
			Если НайденныеСтрокиТоваров.Количество() > 0 Тогда
				УчитыватьОстатки   = Истина;
				ПереподчинитьСерии = Истина;
			ИначеЕсли КэшированныеЗначения.Номенклатура = ТекущаяСтрока.Номенклатура Тогда //т.е. изменились поля, от которых политика учета не зависит
				ПереподчинитьСерии = Истина;
			Иначе //будем переподчинять, если не поменялся вид номенклатуры
				ВидНоменклатурыТекущий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КэшированныеЗначения.Номенклатура,"ВидНоменклатуры");
				ВидНоменклатурыНовый   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура, "ВидНоменклатуры");
				
				ПереподчинитьСерии = (ВидНоменклатурыТекущий = ВидНоменклатурыНовый);
			КонецЕсли;
			
			Если ПереподчинитьСерии
				И ЕстьУпаковки
				И КэшированныеЗначения.Упаковка <> ТекущаяСтрока.Упаковка Тогда
				
				ПересчитатьКоличество = Истина;
				
			КонецЕсли;
			
		КонецЕсли;	
		
		// Если строка удалена, то в качестве текущих значений будет передано Неопределено
		// Тогда не нужно искать строки с новыми значениями.
		СтруктураПоискаНовыеЗначения = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
		ЗаполнитьЗначенияСвойств(СтруктураПоискаНовыеЗначения,ТекущаяСтрока);
		
		НайденныеСтрокиТоваровНовые = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоискаНовыеЗначения);    
		
		// Добавим строки по новым ключевым полям в массив строк для пересчета статуса указания серий.
		
		// При объединении массивов будем обходить меньший массив
		Если НайденныеСтрокиТоваров.Количество() < НайденныеСтрокиТоваровНовые.Количество() Тогда
			Для Каждого СтрМас Из НайденныеСтрокиТоваров Цикл
				НайденныеСтрокиТоваровНовые.Добавить(СтрМас);
			КонецЦикла;
			НайденныеСтрокиТоваров = НайденныеСтрокиТоваровНовые;
		Иначе
			Для Каждого СтрМас Из НайденныеСтрокиТоваровНовые Цикл
				НайденныеСтрокиТоваров.Добавить(СтрМас);
			КонецЦикла;
		КонецЕсли;
		
		// Определим массив строк серий, который должен участвовать в пересчете статусов,
		Если ПереподчинитьСерии Тогда		
			// Сначала переподчиним серии
			
			Если УчитыватьОстатки Тогда
				Если ТекущаяСтрока.КоличествоПоДокументу > ТекущаяСтрока.Количество Тогда
					КоличествоОстаток = ТекущаяСтрока.Количество;
				Иначе
					
					ЕстьНовыеСерии = Ложь;
					
					КоличествоСтрокСИзлишками = 0;
					
					Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
						Если СтрМас.КоличествоПоДокументу = 0 Тогда
							ЕстьНовыеСерии = Истина;
						ИначеЕсли СтрМас.КоличествоПоДокументу < СтрМас.Количество Тогда
							КоличествоСтрокСИзлишками = КоличествоСтрокСИзлишками + 1;
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьНовыеСерии
						И КоличествоСтрокСИзлишками > 0 Тогда //Переподчиним количество серий за вычетом количества строк с излишками
						
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу - КоличествоСтрокСИзлишками;
						
					ИначеЕсли ЕстьНовыеСерии Тогда //Переподчиним количество серий равное количеству серий в документе
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу;
					Иначе //Переподчиним количество серий равное фактическому количеству серий в обрабатываемой строке
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу
											- (ТекущаяСтрока.Количество - ТекущаяСтрока.КоличествоПоДокументу);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				Если УчитыватьОстатки Тогда
					Если КоличествоОстаток > 0
						Или СтрМас.Количество <> СтрМас.КоличествоПоДокументу Тогда
						
						КоличествоОстаток = КоличествоОстаток - СтрМас.Количество;
						
						ЗаполнитьЗначенияСвойств(СтрМас, ТекущаяСтрока, ТекстПоляСвязи);
					КонецЕсли;
				Иначе
					ЗаполнитьЗначенияСвойств(СтрМас, ТекущаяСтрока, "Номенклатура,Характеристика" + ТекстПоляСвязи);
				КонецЕсли;
				
				Если ПересчитатьКоличество Тогда
					СтруктураДействий = Новый Структура;
					СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
					ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОбработатьСтрокуТЧ(СтрМас,СтруктураДействий,КэшированныеЗначения);
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не УчитыватьОстатки Тогда
				// Если серии переподчинены, то достаточно произвести поиск по новым полям поиска
				НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаНовыеЗначения);
			КонецЕсли;
			
		Иначе	
			НайденныеСтрокиСерийНовые = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаНовыеЗначения);
			
			// Если серии не переподчинены, то к строкам по старым ключевым полям нужно добавить строки по новым ключевым полям.
			
			// При объединении массивов будем обходить меньший массив
			Если НайденныеСтрокиСерий.Количество() < НайденныеСтрокиСерийНовые.Количество() Тогда
				Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
					НайденныеСтрокиСерийНовые.Добавить(СтрМас);
				КонецЦикла;
				НайденныеСтрокиСерий = НайденныеСтрокиСерийНовые;
			Иначе
				Для Каждого СтрМас Из НайденныеСтрокиСерийНовые Цикл
					НайденныеСтрокиСерий.Добавить(СтрМас);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НайденныеСтрокиТоваров.Количество() > 0 Тогда 
		ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий,НайденныеСтрокиТоваров,НайденныеСтрокиСерий);
	КонецЕсли;
	
КонецПроцедуры

// Процедура пересчитывает статусм указания серий для товара в шапке документа, если это необходимо, переподчиняет
// строки серий.
//
// Параметры:
//  Объект					 - ДанныеФормыСтуктура	 - основной реквизит формы документа.
//  ПараметрыУказанияСерий	 - Структура - структура параметров указания серий, возвращаемая соотвествующей процедурой модуля менеджера документа.
//  КэшированныеЗначения	 - Структура - структура кеша реквизитов текущей строки товаров.
//
Процедура ЗаполнитьСтатусыУказанияСерийПриИзмененииТовараВШапке(Объект, Знач ПараметрыУказанияСерий, КэшированныеЗначения) Экспорт
	
	ТекстПоляСвязи = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас;
	КонецЦикла;
	
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено; 
	
	// Если поменялись ключевые поля:
	// - возможно нужно переподчинять серии
	// - статус указания серий нужно пересчитвать в строках по новым ключевым полям и по старым.
	
	ИзменилисьКлючевыеПоля = Ложь;
	
	Если КэшированныеЗначения.НоменклатураШапка <> Объект.Номенклатура
		Или  КэшированныеЗначения.ХарактеристикаШапка <> Объект.Характеристика Тогда
		ИзменилисьКлючевыеПоля = Истина;
	Иначе	
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			Если КэшированныеЗначения[СтрМас+"Шапка"] <> Объект[СтрМас] Тогда
				ИзменилисьКлючевыеПоля = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПересчитатьСтатус = Ложь;

	Если ИзменилисьКлючевыеПоля Тогда
		
		ПереподчинитьСерии    = Ложь;
		ПересчитатьКоличество = Ложь;
		// Определелим, нужно ли переподчинять серии. Это нужно если:
		// - серии относились только к одной строке
		// - новые и старые ключевые поля поддерживают одну политику учета
		// Если строки нужно переподчинять, то определим, нужно ли пересчитывать количество.
		
		Если КэшированныеЗначения.НоменклатураШапка = Объект.Номенклатура Тогда //т.е. изменились поля, от которых политика учета не зависит
			ПереподчинитьСерии = Истина;
		Иначе //будем переподчинять
			
			ПереподчинитьСерии = Истина;
			ПересчитатьСтатус  = Ложь;
		КонецЕсли;
		
		Если ПереподчинитьСерии
			И ЕстьУпаковки
			И КэшированныеЗначения.УпаковкаШапка <> Объект.Упаковка Тогда
			
			ПересчитатьКоличество = Истина;
			
		КонецЕсли;
			
		Если ПереподчинитьСерии Тогда		
			СтруктураПоискаСтарыеЗначения = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
			Для Каждого КлючИЗначение Из СтруктураПоискаСтарыеЗначения Цикл
				СтруктураПоискаСтарыеЗначения[КлючИЗначение.Ключ] = КэшированныеЗначения[КлючИЗначение.Ключ+"Шапка"]	
			КонецЦикла;
			
			НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаСтарыеЗначения);
			
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				ЗаполнитьЗначенияСвойств(СтрМас,Объект, "Номенклатура,Характеристика"+ТекстПоляСвязи);
				
				Если ПересчитатьКоличество Тогда
					ОбработкаТабличнойЧастиСерверЕГАИСУТ.ПересчитатьКоличествоУпаковокВСтрокеТЧ(
						СтрМас,
						Новый Структура("ПересчитатьКоличествоУпаковок"),
						КэшированныеЗначения);
				КонецЕсли;	
				
			КонецЦикла;
		Иначе
			Объект.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьУпаковки Тогда
		Если КэшированныеЗначения.КоличествоУпаковокШапка <> Объект.КоличествоУпаковок Тогда
			ПересчитатьСтатус = Истина;
		КонецЕсли;
	Иначе
		Если КэшированныеЗначения.КоличествоШапка <> Объект.Количество Тогда
			ПересчитатьСтатус = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПересчитатьСтатус Тогда 
		ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий,Неопределено,Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Процедура удаляет строки ТЧ "Серии", которым по полям связи нет соотвествующих строк в ТЧ "Товары"
//  или в этих строках статус указания серий равен 0 (не указывать).
//
// Параметры:
//  ДокументОбъект			 - ДанныеФормыКоллекция или ДокументОбъект - ДокументОбъект, в котором нужно удалить неиспользуемые строки серий.
//  ПараметрыУказанияСерий	 - Структура - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Процедура ОчиститьНеиспользуемыеСерии(ДокументОбъект, ПараметрыУказанияСерий) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользованиеСерийНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.Свойство("Шапка")
		Или ПараметрыУказанияСерий.ТоварВШапке Тогда 
		ОчиститьНеиспользуемыеСерииСУчетомТовараВШапке(ДокументОбъект, ПараметрыУказанияСерий);
	Иначе
		ОчиститьНеиспользуемыеСерииВТЧТовары(ДокументОбъект, ПараметрыУказанияСерий, ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары]);
		ОчиститьНеиспользуемыеСерииВТЧСерии(ДокументОбъект, ПараметрыУказанияСерий, ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

// Если по сериям для переданных Номенклатуры/Склада учитывается себестоимость, то рассчитывает статус указания серий.
//  Проверяет принадлежность уже указанной серии переданной номенклатуре.
//
// Параметры:
//  ТекущаяСтрока			 - Структура - для которой расчитавается статус указания серий;
//  Склад					 - СправочникСсылка.Склады	 - склад, для которого осуществляется расчет статуса указания серий;
//  ПараметрыУказанияСерий	 - Структура				 - структура, описанная в фукнции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
// 
// Возвращаемое значение:
//  Структура - структура со следующими ключами:
//  *Серия - СправочникСсылка.СерииНоменклатуры - если серия указана и она может использоваться с новым значением номенклатуры,
//  	на указанном складе, то возвращается переданное значение, если нет - пустая ссылка;
//  *СтатусУказанияСерий - Число - если серии указываются в ТЧ "Товары", то возвращается рассчитанный статус,
//  	если для переданной номенклатуры/склада серии не используется - возвращается 0
//  	иначе возвращается переданный статус.
//
Функция ПроверитьСериюРассчитатьСтатусПриИзмененииРеквизитаВТЧ(ТекущаяСтрока, Склад, ПараметрыУказанияСерий) Экспорт
	МетаданныеОбъекта = ИнтеграцияЕГАИСПереопределяемый.МетаданныеОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
	
	Суффиксы = Новый Массив;
	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		Суффиксы.Добавить("");
	Иначе
		Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			Суффикс = НоменклатураКлиентСервер.СуффиксВИмениПоляСтатусУказанияСерий(ИмяПоляСтатус);
			
			Если МетаданныеОбъекта.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Реквизиты.Найти("Серия" + Суффикс) <> Неопределено Тогда
				Суффиксы.Добавить(Суффикс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Серия");
	СтруктураВозврата.Вставить("УказыватьСерии");
	
	СтруктураВозврата.Серия = ТекущаяСтрока.Серия;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК РазличаютсяВладельцыСерииИНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура,
	|	Справочник.СерииНоменклатуры КАК Серии
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|	И Серии.Ссылка = &Серия";
	Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрока.Номенклатура);
	Запрос.УстановитьПараметр("Серия", ТекущаяСтрока.Серия);
	Если Не Запрос.Выполнить().Пустой() Тогда
		СтруктураВозврата.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		СтруктураВозврата.Вставить("СтатусУказанияСерий");
	Иначе
		Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			СтруктураВозврата.Вставить(ИмяПоляСтатус);
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		
		СтруктураВозврата.Серия               = Справочники.СерииНоменклатуры.ПустаяСсылка();
		Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
			СтруктураВозврата.СтатусУказанияСерий = 0;
		Иначе
			Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
				СтруктураВозврата[ИмяПоляСтатус] = 0;
			КонецЦикла;
		КонецЕсли;
		СтруктураВозврата.УказыватьСерии      = Ложь;
		
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	Для Каждого Суффикс Из Суффиксы Цикл
		ТаблицаТоваров.Колонки.Добавить("Номенклатура"+Суффикс,Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("Характеристика"+Суффикс,Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Серия"+Суффикс,Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	КонецЦикла;
	
	ТаблицаТоваров.Колонки.Добавить(ПараметрыУказанияСерий.ИмяПоляКоличество,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный)));
	
	ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров, МетаданныеОбъекта);
	
	ТаблицаСерий = Новый ТаблицаЗначений;
	ТаблицаСерий.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаСерий.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаСерий.Колонки.Добавить(ПараметрыУказанияСерий.ИмяПоляКоличество,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	
	ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаСерий, МетаданныеОбъекта, Истина);
	
	СтрокаТовара = ТаблицаТоваров.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТовара,ТекущаяСтрока);
	СтрокаТовара.НомерСтроки = 1;
	СтрокаТовара.Серия = СтруктураВозврата.Серия;
	
	Выборка = ВыборкаИзЗапросаРасчетаСтатусовУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров, ТаблицаСерий, Склад);
	
	Если Выборка.Следующий() Тогда
		
		// Если серии указываются в отдельной ТЧ, то при изменении реквизитов
		// будут пересчитаты только статусы, связанные с сериями, указываемыми
		// в ТЧ "Товары" (т.е. по которым ведется учет себестоимости).
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(Выборка.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
			
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				СтруктураВозврата.СтатусУказанияСерий = Выборка.СтатусУказанияСерий;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					СтруктураВозврата[ИмяПоляСтатус] = Выборка[ИмяПоляСтатус];
				КонецЦикла;
			КонецЕсли;
						
		Иначе
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				СтруктураВозврата.СтатусУказанияСерий = ТекущаяСтрока.СтатусУказанияСерий;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					СтруктураВозврата[ИмяПоляСтатус] = ТекущаяСтрока[ИмяПоляСтатус];
				КонецЦикла;
			КонецЕсли;

		КонецЕсли;
		
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииНеУказываются(СтруктураВозврата.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
			СтруктураВозврата.УказыватьСерии = Ложь;
			СтруктураВозврата.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
	Иначе
		Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
			СтруктураВозврата.СтатусУказанияСерий = ТекущаяСтрока.СтатусУказанияСерий;
		Иначе
			Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
				СтруктураВозврата[ИмяПоляСтатус] = ТекущаяСтрока[ИмяПоляСтатус];
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Процедура подбирает серии по FEFO и заполняет подобранными значениями ТЧ "Серии"
//  Учитываются движения документа, заполненные серии перезаполняются.
//
// Параметры:
//  Объект							 - ДанныеФормыКоллекция или ДокументОбъект - объект, в котором нужно заполнить статусы.
//  ПараметрыУказанияСерий			 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//  СтатусыУказанияСерийЗаполнены	 - Булево - если статусы указания серий в ТЧ Товары заполнены.
//
Процедура ЗаполнитьСерииПоFEFO(Объект,ПараметрыУказанияСерий, СтатусыУказанияСерийЗаполнены = Истина) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	
	Если Не СтатусыУказанияСерийЗаполнены Тогда
        НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
    КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда 
		ЗаполнитьСерииПоFEFOВТЧТовары(Объект, ПараметрыУказанияСерий);
	Иначе
		ЗаполнитьСерииПоFEFOВТЧСерии(Объект, ПараметрыУказанияСерий);
	КонецЕсли;	
		
КонецПроцедуры

// Функция проверяет наличие в ТЧ "Товары" строк, по которым серии заполнены по FEFO.
//
// Параметры:
//  ТЧ	 - ДанныеФормыКоллеция	 - проверяемая ТЧ объекта.
// 
// Возвращаемое значение:
//  Булево - признак наличия в ТЧ "Товары" строк, заполненных по FEFO.
//
Функция ЕстьСтрокиСЗаполненнымиПоFEFOСериями(ТЧ) Экспорт
	Возврат ТЧ.НайтиСтроки(Новый Структура("СтатусУказанияСерий",6)).Количество() > 0;	
КонецФункции

// Функция проверяет наличие в ТЧ "Товары" строк, по которым серии можно заполнить по FEFO.
//
// Параметры:
//  ТЧ	 - ДанныеФормыКоллеция	 - проверяемая ТЧ объекта.
// 
// Возвращаемое значение:
//  Булево - признак наличия в ТЧ "Товары" строк, по которым серии можно заполнить по FEFO.
//
Функция ЕстьСтрокиСЗаполняемымиПоFEFOСериями(ТЧ) Экспорт
	Возврат ТЧ.НайтиСтроки(Новый Структура("СтатусУказанияСерий",6)).Количество() > 0
		Или ТЧ.НайтиСтроки(Новый Структура("СтатусУказанияСерий",5)).Количество() > 0
		Или ТЧ.НайтиСтроки(Новый Структура("СтатусУказанияСерий",25)).Количество() > 0;
КонецФункции

// Проверяет, предусматривает ли политика указания серий на переданном складе указание серий
//  и проверяет принадлежность серии.
//
// Параметры:
//  Склад							 - СправочникСсылка.Склад	 - склад, на котором хранится номенклатура;
//  Номенклатура					 - СправочникСсылка.Номенклатура - номенклатура;
//  Серия							 - СправочникСсылка.СерииНоменклатуры	 - серия номенклатуры;
//  ИмяПараметраПолитикиУчетаСерий	 - Строка								 - имя реквизита политики учета серий, по которому нужно проверить
//  								статус указания серий.
// 
// Возвращаемое значение:
//  Структура - структура со следующими ключами:
//  *СтатусУказанияСерий - Строка - статус указания серий;
//  *Серия - СправочникСсылка.СерииНоменклатуры - если серия принадлежит тому же виду номенклатуры,
//  	то переданная серия, иначе - пустая ссылка.
//
Функция СерияУказанаКорректно(Склад, Номенклатура, Серия, ИмяПараметраПолитикиУчетаСерий) Экспорт
	Результат = Новый Структура("СтатусУказанияСерий,Серия");
		
	Если ИмяПараметраПолитикиУчетаСерий = "УчетСебестоимостиПоСериям" Тогда
			Результат.СтатусУказанияСерий = 14;
	ИначеЕсли ИмяПараметраПолитикиУчетаСерий = "УчетСерийПоFEFO" Тогда
		Результат.СтатусУказанияСерий = 6;
	ИначеЕсли ИмяПараметраПолитикиУчетаСерий = "УказыватьПоФактуОтбора" Тогда
		Результат.СтатусУказанияСерий = 4;
	Иначе
			Результат.СтатусУказанияСерий = 2;
	КонецЕсли;
	
	Если Результат.СтатусУказанияСерий = 0 Тогда
		Результат.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	Иначе
		Результат.Серия = Серия;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// По указанным параметрам функция возвращает статус указания серий.
//
// Параметры:
//	Склад - СправочникСсылка.Склады - склад, для которого вычисляется статус указания серий;
//	Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой вычисляется статус указания серий;
//	ИмяПараметраПолитикиУчетаСерий - Строка - имя параметра политики, который нужно проверить.
//
// Возвращаемое значение:
//	Число - статус указания серий.
//
Функция СтатусУказанияСерии(Склад, Номенклатура, ИмяПараметраПолитикиУчетаСерий) Экспорт
	
	СтатусУказанияСерий = 0;
	
	Возврат СтатусУказанияСерий;
	
КонецФункции

// Функция возвращает параметры проверки заполнения характеристик номенклатуры
//
//	Возвращаемое значение:
//		Структура  - структура со следующими ключами:
//			*ВыдаватьСообщения - Булево - значение по умолчанию пустая ИСТИНА - если ЛОЖЬ, но не будут выдаваться сообщения, просто выставится Отказ.
//			*ПутьКДаннымТаблицыФормы - Строка - значение по умолчанию "Объект" - путь к реквизиту формы, содержащий проверяемую табличную часть.
//                                                                               Если проверяется таблица, которая сама является реквизитом формы, нужно
//                                                                               передать "" (т.е. пустую строку)
//			*ИмяКолонкиСерияТаблицыФормы - Структура - значение по умолчанию "Серия" - имя колонки таблицы формы, рядом с которой нужно
//																вывести сообщение. Нужно переопределять, когда вместо колонки серии отображатеся какая-то
//																другая колонка, например, с какими-то строковыми представлениями.
//
Функция ПараметрыПроверкиЗаполненияСерий() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ВыдаватьСообщения", Истина);
	ПараметрыПроверки.Вставить("ПутьКДаннымТаблицыФормы", "Объект");
	ПараметрыПроверки.Вставить("ИмяКолонкиСерияТаблицыФормы", "Серия");
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Процедура проверяет правильность указания серий товаров по статусам в ТЧ "Товары"
//  Если статусы
//  	1 - количество по сериям не совпадает с количеством товаров (движения по сериям делать не нужно)
//  	3 - количество по сериям не совпадает с количеством товаров (нужно делать движения по сериям)
//  	5 - количество по сериям не совпадает с количеством товаров (нужно делать движения по сериям, серии заполняются по FEFO)
//  	7 - количество по сериям не совпадает с количеством товаров (серии указываются при планировании отбора)
//  	9 - количество по сериям не совпадает с количеством товаров (серии указываются при планировании отгрузки)
//  	13 - количество по сериям не совпадает с количеством товаров или серия не указана (учет себестоимости по сериям)
// 			то выдается ошибка.
//
// Параметры:
//  ДокументОбъект					 - ДокументОбъект	 - объект, в котором нужно проверить указание серий
//  ПараметрыУказанияСерий			 - Структура		 - структура параметров указания серий, возвращаемая соотвествующей процедурой модуля менеджера документа
//  Отказ							 - Булево			 - признак ошибки проверки
//  МассивНепроверяемыхРеквизитов	 - Массив, Строка	 - массив имен реквизитов, которые нужно исключить из платформенной проверки
//  ВыдаватьСообщения				 - Булево			 - признак того, что нужно выдавать сообщения об ошибках проверки.
//
Процедура ПроверитьЗаполнениеСерий(ДокументОбъект,ПараметрыУказанияСерий,Отказ,МассивНепроверяемыхРеквизитов = Неопределено, ПараметрыПроверки = Неопределено) Экспорт
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ПараметрыПроверкиЗаполненияСерий(); 
	КонецЕсли;
	
	МетаданныеДокумента = ИнтеграцияЕГАИСПереопределяемый.МетаданныеОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
	
	Суффиксы = Новый Массив;
	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		Суффиксы.Добавить("");
	Иначе
		Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			Суффикс = НоменклатураКлиентСервер.СуффиксВИмениПоляСтатусУказанияСерий(ИмяПоляСтатус);
			Если МетаданныеДокумента.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Реквизиты.Найти("Серия" + Суффикс) <> Неопределено Тогда
				Суффиксы.Добавить(Суффикс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если МассивНепроверяемыхРеквизитов <> Неопределено Тогда
		Для Каждого Суффикс Из Суффиксы Цикл
			МассивНепроверяемыхРеквизитов.Добавить(ПараметрыУказанияСерий.ИмяТЧТовары  +".Серия" + Суффикс);
		КонецЦикла;
		МассивНепроверяемыхРеквизитов.Добавить("Серия");
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	КлючДанных = ИнтеграцияЕГАИСПереопределяемый.КлючДанныхДляСообщенияПользователю(ДокументОбъект);
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		
		Если НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана().Найти(ДокументОбъект.СтатусУказанияСерий) <> Неопределено
			И ДокументОбъект.СтатусУказанияСерий <> 13 Тогда
			
			ТекстСообщения = НСтр("ru = 'Количество товара ""%Товар%"" не соответствует указанному для него количеству серий. Исправьте серии.'");
				
			ТекстСообщения = СтрЗаменить(ТекстСообщения,
				"%Товар%",НоменклатураКлиентСервер.ПредставлениеНоменклатуры(ДокументОбъект.Номенклатура,ДокументОбъект.Характеристика));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				КлючДанных,
				"Номенклатура",
				ПараметрыПроверки.ПутьКДаннымТаблицыФормы,
				Отказ);
			
		ИначеЕсли ДокументОбъект.СтатусУказанияСерий = 13 Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Серия"" не заполнено'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				КлючДанных,
				ПараметрыПроверки.ИмяКолонкиСерияТаблицыФормы,
				ПараметрыПроверки.ПутьКДаннымТаблицыФормы,
				Отказ);
				
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		
		Если ПараметрыУказанияСерий.ОсобеннаяПроверкаСтатусовУказанияСерий Тогда
	
			МодульМенеджера = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
			ТекстЗапроса =	МодульМенеджера.ТекстЗапросаПроверкиЗаполненияСерий(ПараметрыУказанияСерий);
			
		Иначе
			
			ПроверитьКоличествоПоСериям(ДокументОбъект, ПараметрыУказанияСерий, Отказ, ПараметрыПроверки.ВыдаватьСообщения);
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаСерий.НомерСтроки КАК НомерСтроки,
			|	ТаблицаСерий.Серия КАК Серия,
			|	ТаблицаСерий.Номенклатура КАК Номенклатура,
			|	ТаблицаСерий.Характеристика КАК Характеристика,
			|	ТаблицаСерий.ПоляСвязи КАК ПоляСвязи,
			|	ТаблицаСерий.ИмяПоляКоличество КАК КоличествоУпаковок
			|ПОМЕСТИТЬ ТаблицаСерийДляЗапроса
			|ИЗ
			|	&ТаблицаСерий КАК ТаблицаСерий
			|ГДЕ
			|	ТаблицаСерий.СтатусУказанияСерий  > 0
			|	И НЕ ТаблицаСерий.СтатусУказанияСерий В (&СтатусыСерийСериюМожноУказать)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ТаблицаСерий.НомерСтроки) КАК НомерСтроки,
			|	ТаблицаСерий.Номенклатура КАК Номенклатура,
			|	СУММА(ТаблицаСерий.КоличествоУпаковок) КАК КоличествоУпаковок
			|ПОМЕСТИТЬ ТаблицаСерий
			|ИЗ
			|	ТаблицаСерийДляЗапроса КАК ТаблицаСерий
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаСерий.Серия,
			|	ТаблицаСерий.Номенклатура,
			|	ТаблицаСерий.ПоляСвязи,
			|	ТаблицаСерий.Характеристика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПРЕДСТАВЛЕНИЕ(Т.Номенклатура) КАК ТоварПредставление,
			|	Т.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияНеЗаполнена,
			|	ЛОЖЬ КАК ОшибкаКоличества,
			|	Т.НомерСтроки КАК НомерСтроки
			|ИЗ
			|	ТаблицаСерийДляЗапроса КАК Т
			|ГДЕ
			|	Т.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Т.НомерСтроки";
			
			ПоляСвязиВЫБРАТЬ = "";
			ПоляСвязиСГРУППИРОВАТЬПО = "";
			Для Каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
				ПоляСвязиВЫБРАТЬ = ПоляСвязиВЫБРАТЬ + "
				|	ТаблицаСерий." + ПолеСвязи + " КАК " + ПолеСвязи + ",";
				
				ПоляСвязиСГРУППИРОВАТЬПО = ПоляСвязиСГРУППИРОВАТЬПО + "
				|	ТаблицаСерий." + ПолеСвязи + ",";
			КонецЦикла;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаСерий.ПоляСвязи КАК ПоляСвязи,", ПоляСвязиВЫБРАТЬ);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаСерий.ПоляСвязи,", ПоляСвязиСГРУППИРОВАТЬПО);
		
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяПоляКоличество", ПараметрыУказанияСерий.ИмяПоляКоличество);
		
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("СтатусыСерийСериюМожноУказать", НоменклатураКлиентСервер.СтатусыСерийСериюМожноУказать());
		
		Если ПараметрыУказанияСерий.ОтборПроверяемыхСтрок <> Неопределено Тогда
			Запрос.УстановитьПараметр("ТаблицаСерий",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(ПараметрыУказанияСерий.ОтборПроверяемыхСтрок));
		Иначе
			Запрос.УстановитьПараметр("ТаблицаСерий",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
		КонецЕсли;
		
		Если ПараметрыПроверки.ВыдаватьСообщения Тогда
			Выборка = Запрос.Выполнить().Выбрать();
			
			ПредставлениеТЧ = МетаданныеДокумента.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Синоним;
			
			Пока Выборка.Следующий() Цикл
				
				Если Выборка.СерияНеЗаполнена Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""%ИмяРеквизитаСерия%"" в строке %НомерСтроки% списка ""%ПредставлениеТЧ%""'");
					
					ИмяРеквизитаСерия = МетаданныеДокумента.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Реквизиты["Серия"].Синоним;
					
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРеквизитаСерия%", ИмяРеквизитаСерия);
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(Выборка.НомерСтроки));
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеТЧ%", ПредставлениеТЧ);
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Выборка.НомерСтроки, ПараметрыПроверки.ИмяКолонкиСерияТаблицыФормы);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,ПараметрыПроверки.ПутьКДаннымТаблицыФормы,Отказ);
				КонецЕсли;
				
				Если Выборка.ОшибкаКоличества Тогда
					// Только для случая ПараметрыУказанияСерий.ОсобеннаяПроверкаСтатусовУказанияСерий, 
					ТекстСообщения = НСтр("ru = 'Политика учета серий товара ""%ТоварПредставление%"" предусматривает, что количество по любой серии этого товара всегда будет равно 1""'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(Выборка.НомерСтроки));
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТоварПредставление%", Выборка.ТоварПредставление);
					
					// Сделано неуниверсально, т.к. во всех документах, где справочные серии указываются только в той же ТЧ, что и
					// товары, есть поле КоличествоУпаковок.
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Выборка.НомерСтроки, "КоличествоУпаковок");
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,ПараметрыПроверки.ПутьКДаннымТаблицыФормы,Отказ);
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			Если Не Запрос.Выполнить().Пустой() Тогда
				Отказ = Истина;	
			КонецЕсли;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ТекстПоляВыбораТовары,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	&ИмяПоляКоличествоТовары КАК Количество,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.СтатусУказанияСерий В (&СтатусыСерийСерияНеУказана)
	|	ИЛИ &ВестиУчетМаркировкиПродукцииВГИСМ
	|		И ТаблицаТоваров.СтатусУказанияСерий = 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТекстПоляВыбораТовары,
	|	&ИмяПоляЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаТоваровДляЗапроса
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	&ТекстПоляВыбораТовары,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.СтатусУказанияСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТекстПоляВыбораСерии,
	|	ТаблицаСерий.Серия КАК Серия,
	|	ТаблицаСерий.Номенклатура КАК Номенклатура,
	|	ТаблицаСерий.Характеристика КАК Характеристика,
	|	&ИмяПоляКоличествоСерии КАК Количество
	|ПОМЕСТИТЬ ТаблицаСерий
	|ИЗ
	|	&ТаблицаСерий КАК ТаблицаСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТекстПоляВыбораСерии,
	|	ТаблицаСерий.Номенклатура КАК Номенклатура,
	|	ТаблицаСерий.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаСерийДляЗапроса
	|ИЗ
	|	ТаблицаСерий КАК ТаблицаСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	&ТекстПоляВыбораСерии,
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика";
	
	ТекстПоляВыбораТовары = "";
	ТекстПоляВыбораСерии = "";
	ТекстПоляСвязиСоединениеТоварыСерии = "";
	ТекстПоляСвязиСоединениеТоварыВсеТовары = "";
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляВыбораТовары = ТекстПоляВыбораТовары + "
		|	ТаблицаТоваров." + СтрМас + ",";
		ТекстПоляВыбораСерии = ТекстПоляВыбораСерии + "
		|	ТаблицаСерий." + СтрМас + ",";
		ТекстПоляСвязиСоединениеТоварыСерии = ТекстПоляСвязиСоединениеТоварыСерии + "
		|			И ТаблицаТоваров."+СтрМас+" = ТаблицаСерийДляЗапроса."+СтрМас;
		ТекстПоляСвязиСоединениеТоварыВсеТовары = ТекстПоляСвязиСоединениеТоварыВсеТовары + "
		|			И ТаблицаТоваров."+СтрМас+" = ТаблицаТоваровДляЗапроса." + СтрМас;
	КонецЦикла;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляВыбораТовары,", ТекстПоляВыбораТовары);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляВыбораСерии,", ТекстПоляВыбораСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (&ТекстПоляСвязиСоединениеТоварыСерии)", ТекстПоляСвязиСоединениеТоварыСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (&ТекстПоляСвязиСоединениеТоварыВсеТовары)", ТекстПоляСвязиСоединениеТоварыВсеТовары);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляКоличествоТовары", "ТаблицаТоваров." + ПараметрыУказанияСерий.ИмяПоляКоличество);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляКоличествоСерии", "ТаблицаСерий." + ПараметрыУказанияСерий.ИмяПоляКоличество);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляЕдиницаИзмерения",
		?(ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено, "ТаблицаТоваров.Упаковка","ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения"));
		
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = ТекстЗапроса;
	Если ПараметрыУказанияСерий.Свойство("ОтборПроверяемыхСтрок") Тогда
		Запрос.УстановитьПараметр("ТаблицаТоваров",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(ПараметрыУказанияСерий.ОтборПроверяемыхСтрок));
	Иначе
		Запрос.УстановитьПараметр("ТаблицаТоваров",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СтатусыСерийСерияНеУказана", НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана());
	
	Запрос.УстановитьПараметр("ТаблицаСерий",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить());
	СерииПриПланированииОтгрузкиУказываютсяВТЧТовары = Не ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии;
	
	ПредставлениеТЧ = МетаданныеДокумента.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Синоним;
	ЕстьРеквизитСерия = МетаданныеДокумента.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Реквизиты.Найти("Серия") <> Неопределено;
			
	Если ПараметрыПроверки.ВыдаватьСообщения Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ОшибкаНеУказанаСерия
				Или Выборка.ОшибкаНеРавноКоличество Тогда
				Если (Выборка.СтатусУказанияСерий = 13
						Или СерииПриПланированииОтгрузкиУказываютсяВТЧТовары И Выборка.СтатусУказанияСерий = 9)
					И  ЕстьРеквизитСерия Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Серия"" в строке %НомерСтроки% списка ""%ПредставлениеТЧ%""'");
					
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%",Выборка.НомерСтроки);
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеТЧ%",ПредставлениеТЧ);
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Выборка.НомерСтроки, ПараметрыПроверки.ИмяКолонкиСерияТаблицыФормы);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, КлючДанных , Поле, ПараметрыПроверки.ПутьКДаннымТаблицыФормы, Отказ);
				Иначе
					ТекстСообщения = НСтр("ru = 'Для товара ""%Товар%"" указано по сериям %КоличествоСерий% %ЕдиницаИзмерения%. Необходимо указать %КоличествоТоваров% %ЕдиницаИзмерения%. Исправьте серии.'");
					
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Товар%",НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Выборка.Номенклатура,
					Выборка.Характеристика));
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоСерий%",Выборка.КоличествоСерий);
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоТоваров%",Выборка.КоличествоТоваров);
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ЕдиницаИзмерения%",Выборка.ЕдиницаИзмерения);
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Выборка.НомерСтроки, "СтатусУказанияСерий");
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, КлючДанных , Поле, ПараметрыПроверки.ПутьКДаннымТаблицыФормы, Отказ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	Иначе
		Если Не Запрос.Выполнить().Пустой() Тогда
			Отказ = Истина;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура проверяет правильность указания количества для серий товаров,
// которые идентифицируют экземпляр товара по уникальному номеру и указываются в основной ТЧ документа (Товары).
// Если для такой серии указано количество не равное 1, то выдается ошибка.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - документ, в котором нужно проверить указание серий;
//	ПараметрыУказанияСерий - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа;
//	Отказ - Булево, Истина- признак ошибки проверки;
//	ВыдаватьСообщения - Булево, Истина - признак того, что нужно выдавать сообщения об ошибках проверки.
//
Процедура ПроверитьКоличествоПоСериям(ДокументОбъект, ПараметрыУказанияСерий, Отказ, ВыдаватьСообщения = Истина) Экспорт
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары <> ПараметрыУказанияСерий.ИмяТЧСерии 
		 ИЛИ ПараметрыУказанияСерий.ОсобеннаяПроверкаСтатусовУказанияСерий Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает параметры для округления
//
//	Возвращаемое значение:
//		Структура:
//			*ИмяТЧ - Строка - значение по умолчанию "Товары"
//			*СуффиксДопРеквизита - Строка - значение по умолчанию "" - если в ТЧ два реквизита "Количество", то второй назван с суффиксом. 
//											если суффикс передан, то проверяются оба реквизита
//			*УсловиеОтбораСтрокДляОкругления - Строка - Условие выбора строк для проверки округления.
//											в качестве имени таблицы нужно использовать имя переданное в ИмяТЧ.
//
Функция ПараметрыОкругленияКоличестваШтучныхТоваров() Экспорт
	
	ПараметрыОкругления = Новый Структура;
	ПараметрыОкругления.Вставить("ИмяТЧ",                           "Товары");	
	ПараметрыОкругления.Вставить("СуффиксДопРеквизита",             "");
	ПараметрыОкругления.Вставить("УсловиеОтбораСтрокДляОкругления", "");
	
	Возврат ПараметрыОкругления;
	
КонецФункции

// Округляет количество товара до целых штук при указании количества в документе в мерных единицах измерения.
//
Процедура ОкруглитьКоличествоШтучныхТоваров(Объект, РежимЗаписи = Неопределено, ПараметрыОкругления = Неопределено) Экспорт
	
	Если РежимЗаписи <> Неопределено 
		И РежимЗаписи <> РежимЗаписиДокумента.Проведение
		И ТипЗнч(РежимЗаписи) = Тип("РежимЗаписиДокумента")
		И Объект.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОкругления = Неопределено Тогда
		ПараметрыОкругления = ИнтеграцияЕГАИСПереопределяемый.ПараметрыПроверкиЗаполненияКоличества();
	КонецЕсли;
	
	СуффиксДопРеквизита = ПараметрыОкругления.СуффиксДопРеквизита;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧ.НомерСтроки КАК НомерСтроки,
	|	ТЧ.Номенклатура КАК Номенклатура,
	|	ТЧ.Упаковка,
	|	ВЫРАЗИТЬ(ТЧ.Количество КАК ЧИСЛО(15, 0)) КАК КоличествоОкругленное,
	|	ВЫРАЗИТЬ(&ИмяПоляКоличествоСуффикс КАК ЧИСЛО(15, 0)) КАК КоличествоСуффиксОкругленное
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЧ КАК ТЧ
	|ГДЕ
	|	ТЧ.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	И (ТЧ.Количество <> (ВЫРАЗИТЬ(ТЧ.Количество КАК ЧИСЛО(15, 0)))
	|			ИЛИ &ИмяПоляКоличествоСуффикс <> (ВЫРАЗИТЬ(&ИмяПоляКоличествоСуффикс КАК ЧИСЛО(15, 0))))
	|	И (&УсловиеОтбораСтрокДляОкругления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЧ.НомерСтроки КАК НомерСтроки,
	|	ТЧ.КоличествоОкругленное КАК КоличествоОкругленное,
	|	ТЧ.КоличествоСуффиксОкругленное КАК КоличествоСуффиксОкругленное
	|ИЗ
	|	ВТ КАК ТЧ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТНоменклатура
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ТУпаковкиЕдиницыХранения
	|			ПО (ТУпаковкиЕдиницыХранения.Ссылка = ТНоменклатура.ЕдиницаИзмерения)
	|				И (ТУпаковкиЕдиницыХранения.ТипИзмеряемойВеличины = &ШтучныйТип)
	|		ПО (ТНоменклатура.Ссылка = ТЧ.Номенклатура)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ТУпаковкиТЧ
	|		ПО (ТУпаковкиТЧ.Ссылка = ТЧ.Упаковка)
	|			И (ТУпаковкиТЧ.ТипИзмеряемойВеличины В (&МерныеТипы))";
	
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляКоличествоСуффикс", "ТЧ.Количество"+СуффиксДопРеквизита);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляКоличествоСуффикс", "0");
	КонецЕсли;
	
	Если ПараметрыОкругления.УсловиеОтбораСтрокДляОкругления <> "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораСтрокДляОкругления", 
									СтрЗаменить(ПараметрыОкругления.УсловиеОтбораСтрокДляОкругления, ПараметрыОкругления.ИмяТЧ + ".", "ТЧ."));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораСтрокДляОкругления", "ИСТИНА");
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
		
	Если ПараметрыОкругления.ИмяТЧ = "Объект" Тогда
		
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный)));	
		Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		Таблица.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
		Таблица.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));	
		
		СтрокаТаблицы = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Объект);
		
		Запрос.УстановитьПараметр("ТЧ", Таблица);
	Иначе	
		Запрос.УстановитьПараметр("ТЧ", Объект[ПараметрыОкругления.ИмяТЧ].Выгрузить());
	КонецЕсли;	
	
	МерныеТипы = Новый Массив;
	МерныеТипы.Добавить(Перечисления.ТипыИзмеряемыхВеличин.Вес);
	МерныеТипы.Добавить(Перечисления.ТипыИзмеряемыхВеличин.Объем);
	МерныеТипы.Добавить(Перечисления.ТипыИзмеряемыхВеличин.Площадь);
	МерныеТипы.Добавить(Перечисления.ТипыИзмеряемыхВеличин.Длина);
	Запрос.УстановитьПараметр("МерныеТипы", МерныеТипы);
	Запрос.УстановитьПараметр("ШтучныйТип", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ПараметрыОкругления.ИмяТЧ = "Объект" Тогда
			Объект.Количество = Выборка.КоличествоОкругленное;
			Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
				Объект["Количество" + СуффиксДопРеквизита] = Выборка.КоличествоСуффиксОкругленное;
			КонецЕсли;
		Иначе
			Объект[ПараметрыОкругления.ИмяТЧ][Выборка.НомерСтроки - 1].Количество = Выборка.КоличествоОкругленное;
			Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
				Объект[ПараметрыОкругления.ИмяТЧ][Выборка.НомерСтроки - 1]["Количество" + СуффиксДопРеквизита] = Выборка.КоличествоСуффиксОкругленное;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция определяет, какую форму для указания серий нужно открыть (регистрации или подбора),
// подготавливает параметры (в т.ч. помещает нужные данные во временное хранилище) для ее открытия и возвращает их.
//
// Параметры:
//	Объект						 - ДанныеФормыСтуктура	 - основной реквизит формы документа;
//	ПараметрыУказанияСерий		 - Структура			 - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий;
//	ТекущиеДанныеИдентификатор	 - Число				 - идентификатор текущей строки товаров в форме документа;
//	Форма						 - УправляемаяФорма		 - форма, из которой инициировано указание серий;
//	Метаданные					 - ОбъектМетаданных 	 - метаданные объекта, для которого помещаются серии в хранилище, значение по умолчанию - Неопределено.
//															Если параметр не указан - метаданные извлекаются из ссылки (Объект.Ссылка);
//	ЗаголовокКолонкиКоличество	 - Строка				 - заголовок колонки с количеством в открываемой форме указания серий, значение по умолчанию - "".
//															Если заголовок не передан, то колонка будет называться в форме "Количество".
//
// Возвращаемое значение:
//	Структура - структура с именем и параметрами формы указания серий:
// - Основные поля
//		* ИмяФормы - Строка - имя формы, которую нужно отрыть. Это или форма регистрации или формы подбора серий. Зависит от ПараметрыУказанияСерий
//		* АдресВоВременномХранилище - Строка - адрес во временном хранилище отобранных строк серий.
//												Если вызывается форма подбора серий - то во временном хранилище лежит структура(ТаблицаТоваров, ТаблицаСерий).
//												ТаблицаСерий - это ТЧ Серии, ТаблицаТоваров - сгруппированная по полям связи таблица товаров;
//		* РегистрироватьСерии - Булево - нужно ли согласно ПараметрыУказанияСерий давать возможность регистрировать серии (или только подбирать из имеющихся);
//		* ТолькоПросмотр - Булево, Истина - поле формы доступно только для просмотра;
//		* Количество - Число - количество товаров по срокам, для которых указываются серии. Имеет смысл, для формы регистрации серии, т.к. форма подбора открывается для всей ТЧ Товары;
//		* СерииВТЧТовары - Булево - признак, что серии указываются в той же ТЧ, что и товары;
//		* Регистратор - ДокументСсылка - имеет смысл для формы подбора серий. В этой форме отображаются остатки, при этом при отображении сторнируется
//										 изменение остатков текущим документом;
//		* ПараметрыУказанияСерий - Структура - значение параметра ПараметрыУказанияСерий данной фукнции для передачи в форму указания серий;
//		* ЗначенияПолейДляОпределенияРаспоряжения - Структура - имеет смысл для формы подбора серий, в которой показываются остатки. Для запроса остатков нужны
//													параметры. Возвращается функцией см. НоменклатураКлиентСервер.ЗначенияПолейДляОпределенияРаспоряжения.
// - Значения полей текущей строки или объекта, если в строке нет таких полей:
//		* Номенклатура - СправочникСсылка.Номенклатура - номенклатура;
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика номенклатуры;
//		* СтатусУказанияСерий - Число - статус указания серий;
//		* ХарактеристикиИспользуются - Булево, Истина - признак использования характеристик номенклатуры;
//		* Значения полей связи из ПараметрыУказанияСерий.ПоляСвязи;
//		* Склад - СправочникСсылка.Склады - склад;
//		* Помещение - СправочникСсылка.СкладскиеПомещения - складское помещение;
//		* УпаковкаДляПодстановки - СправочникСсылка.УпаковкиЕдиницыИзмерения, Неопределено - упаковка, в которой нужно выводить количество в форме регистрации серий.
//								Если в ТЧ "Товары" для всех строк товара одна упаковка и упаковка не входит в поля связи - передается это значение, если упаковки
//								разные - то Неопределено.
//
Функция ПараметрыФормыУказанияСерий(Объект,ПараметрыУказанияСерий,ТекущиеДанныеИдентификатор,Форма, Метаданные = Неопределено, ЗаголовокКолонкиКоличество = "") Экспорт
	МетаданныеДокумента = ИнтеграцияЕГАИСПереопределяемый.МетаданныеОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
	
	УникальныйИдентификаторФормы = Форма.УникальныйИдентификатор;
	
	// Если нужно будет изменять количество, то данные формы нужно заблокировать
	// Если заблокировать не удастся - вылетит исключение.
	Если Не ПараметрыУказанияСерий.ТолькоПросмотр
		И ПараметрыУказанияСерий.БлокироватьДанныеФормы Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ТолькоПросмотр Тогда
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	ТекстПоляСвязи = "";
	ТекстВыбораТоваров = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстВыбораТоваров = ТекстВыбораТоваров + "
		|	ТаблицаТоваров."  + СтрМас + ", ";
		
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас  ;
	КонецЦикла;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ТекущиеДанные = Объект;
	Иначе
		ТекущиеДанные = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	КонецЕсли;

	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено;
	ЕстьДействие = ПараметрыУказанияСерий.ПоляСвязи.Найти("Действие") <> Неопределено;
	УпаковкаДляПодстановки = Неопределено;
	ИдетОбработкаТоварныхМест = Ложь;
	
	РегистрироватьСерии = ПараметрыУказанияСерий.РегистрироватьСерии;
	
	Если РегистрироватьСерии Тогда
		НомераСтрокДокумента = "";
		
		КоэффициентУпаковки = 1;
		НужноОкруглятьКоличество = Ложь;
		
		Упаковки = Новый Массив();
		
		Если ПараметрыУказанияСерий.ТоварВШапке Тогда
			ЕстьУпаковкиВТЧТовары = Ложь;
			Если Объект.Свойство("Упаковка") Тогда
				Упаковки.Добавить(Объект.Упаковка);
			КонецЕсли;
		Иначе
			КолонкиТаблицы = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(Новый Массив);
			ЕстьУпаковкиВТЧТовары = КолонкиТаблицы.Колонки.Найти("Упаковка") <> Неопределено;
		КонецЕсли;
		
		КолонкиТаблицы = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить(Новый Массив);
		ЕстьУпаковкиВТЧСерии = КолонкиТаблицы.Колонки.Найти("Упаковка") <> Неопределено;
		
		Если ЕстьУпаковкиВТЧТовары Тогда
			УпаковкиТЧТовары = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(, "Упаковка").ВыгрузитьКолонку("Упаковка");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Упаковки, УпаковкиТЧТовары);
		КонецЕсли;
		Если ЕстьУпаковкиВТЧСерии Тогда
			УпаковкиТЧСерии = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить(, "Упаковка").ВыгрузитьКолонку("Упаковка");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Упаковки, УпаковкиТЧСерии);
		КонецЕсли;
		
		Упаковки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Упаковки);
		
		Если ПараметрыУказанияСерий.ТоварВШапке Тогда
			КоличествоВДокументе = Объект[ПараметрыУказанияСерий.ИмяПоляКоличество]*КоэффициентУпаковки;
			СтруктураПоиска = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,Объект);
		Иначе
			СтруктураПоиска = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,ТекущиеДанные);
			
			ТекстПоляСвязиУпаковка = ?(ЕстьУпаковкиВТЧТовары И Не ЕстьУпаковки, ", Упаковка", "");
			
			ВыгружаемыеПоля = "Номенклатура,Характеристика" + ТекстПоляСвязи + ТекстПоляСвязиУпаковка;
			ТаблицаТовары = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(, ВыгружаемыеПоля + ", " + ПараметрыУказанияСерий.ИмяПоляКоличество);
			ТаблицаТовары.Свернуть(ВыгружаемыеПоля, ПараметрыУказанияСерий.ИмяПоляКоличество);
			
			НайденныеСтрокиТоваровСгруппированные = ТаблицаТовары.НайтиСтроки(СтруктураПоиска);
			
			КоличествоВДокументе = 0;
			МинимальноеКоличествоЦелыхНаборов = 0;
			Для Каждого СтрМас Из НайденныеСтрокиТоваровСгруппированные Цикл
				
				Если ЕстьУпаковкиВТЧТовары Тогда
					
					ТоварныеМестаВСтроке = Ложь;
				
					// Расчитаем упаковку, в которой нужно отображать количество в форме редактирования упаковок
					// Если для всех строк указана одна упаковка - то в ней и будем отображать количество
					// Если упаковка входит в поля связи, то она и будет единственной для всех строк.
					Если УпаковкаДляПодстановки = Неопределено Тогда
						УпаковкаДляПодстановки = СтрМас.Упаковка;
					ИначеЕсли УпаковкаДляПодстановки <> СтрМас.Упаковка Тогда
						УпаковкаДляПодстановки = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
					КонецЕсли;
					
				КонецЕсли;
				
				Если НужноОкруглятьКоличество Тогда
					СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество] = Окр(СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество], 0, РежимОкругления.Окр15как20);
				КонецЕсли;
				
				Если ИдетОбработкаТоварныхМест Тогда
					// Количество упаковок, из которых состоит одна штука товара
					КоэффициентУпаковки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрМас.Упаковка, "КоличествоУпаковок");
					
					КоличествоЦелыхНаборов = СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество] / КоэффициентУпаковки;
					
					МинимальноеКоличествоЦелыхНаборов = ?(МинимальноеКоличествоЦелыхНаборов = 0,
															КоличествоЦелыхНаборов,
															Мин(КоличествоЦелыхНаборов, МинимальноеКоличествоЦелыхНаборов));
															
					КоличествоВДокументе = МинимальноеКоличествоЦелыхНаборов;
				Иначе
					КоличествоВДокументе = КоличествоВДокументе + СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество]*КоэффициентУпаковки;
				КонецЕсли;
				
			КонецЦикла;
			
			НайденныеСтрокиТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрМас Из НайденныеСтрокиТоваров Цикл
				Если ЕстьУпаковкиВТЧТовары Тогда
					
					ТоварныеМестаВСтроке = Ложь;
				КонецЕсли;
				
				НомераСтрокДокумента = НомераСтрокДокумента + Строка(СтрМас.НомерСтроки) + ", ";
			КонецЦикла;
			
			Если Не ПустаяСтрока(НомераСтрокДокумента) Тогда
				НомераСтрокДокумента = Лев(НомераСтрокДокумента, СтрДлина(НомераСтрокДокумента)-2);
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаСерий = Новый ТаблицаЗначений;
		ТаблицаСерий.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ТаблицаСерий.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
		ТаблицаСерий.Колонки.Добавить("КоличествоУпаковок", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
		
		НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоиска);
		
		Если ИдетОбработкаТоварныхМест Тогда
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				Если ЕстьУпаковкиВТЧСерии Тогда
					
					ТоварныеМестаВСтроке = Ложь;
					// Если форма указания серий вызывается для строки с упаковкой - товарным местом,
					// то нужно исключать из алгоритма строки с другими типами упаковок.
					Если Не ТоварныеМестаВСтроке Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				// При использовании товарных мест, необходимо получить свернутую таблицу с количеством приведенным
				// к максимуму - для правильного использования в последующем алгоритме распределения в методе ОбработатьУказаниеСерий.
				СтрокаСерий = ТаблицаСерий.Найти(СтрМас.Серия, "Серия");
				Если СтрокаСерий <> Неопределено Тогда
					
					СтрокаСерий.Количество = Макс(СтрокаСерий.Количество, СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество]);
					СтрокаСерий.КоличествоУпаковок = СтрокаСерий.Количество;
					
				Иначе
					
					НоваяСтрока = ТаблицаСерий.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрМас);
					
					// Сделано неуниверсально. Если упаковки есть в полях связи, то поля с количеством во всех документах называются стандартно.
					Если Не ЕстьУпаковки Тогда
						НоваяСтрока.Количество = СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество];
						НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				НоваяСтрока = ТаблицаСерий.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрМас);
				
				// Сделано неуниверсально. Если упаковки есть в полях связи, то поля с количеством во всех документах называются стандартно.
				Если Не ЕстьУпаковки Тогда
					НоваяСтрока.Количество = СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество];
					НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ТаблицаСерий,УникальныйИдентификаторФормы);
		
	Иначе
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТоваров.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
		Если НЕ ЕстьУпаковки Тогда
			ТаблицаТоваров.Колонки.Добавить("Упаковка",Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
		КонецЕсли;
		ТаблицаТоваров.Колонки.Добавить("НомераСтрокДокумента",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
		
		ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров, Метаданные); 
		
		Если ПараметрыУказанияСерий.ТоварВШапке Тогда
			СтрокаТовара = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовара,Объект);
		Иначе
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Количество,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.ЕдиницыИзмерения) КАК Упаковка,
			|	ТаблицаТоваров.НомерСтроки
			|ПОМЕСТИТЬ ТаблицаТоваров
			|ИЗ
			|	&ТаблицаТоваров КАК ТаблицаТоваров
			|ГДЕ
			|	ТаблицаТоваров.СтатусУказанияСерий В(&ОтборПоСтатусам)
			|	И &УсловиеПоДействию
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Количество КАК Количество,
			|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|		ТаблицаТоваров.Характеристика КАК Характеристика,
			|		МИНИМУМ(ТаблицаТоваров.Количество) КАК Количество,
			|		МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
			|	ИЗ
			|		ТаблицаТоваров КАК ТаблицаТоваров
			|	ГДЕ
			|		Ложь
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура,
			|		ТаблицаТоваров.Характеристика,
			|		ТаблицаТоваров.Количество
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура,
			|		ТаблицаТоваров.Характеристика,
			|		ТаблицаТоваров.Количество,
			|		ТаблицаТоваров.НомерСтроки
			|	ИЗ
			|		ТаблицаТоваров) КАК ТаблицаТоваров
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.НомерСтроки";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТоваров.ТекстВыбораТоваров,", ТекстВыбораТоваров); 
			
			Если Не ЕстьУпаковки Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТоваров.Упаковка,", ""); 
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДействию", "ИСТИНА");
			
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			
			Если ПараметрыУказанияСерий.НужноОкруглятьКоличество
				И СтрНайти(ПараметрыУказанияСерий.ИмяПоляКоличество, "КоличествоУпаковок") = 0 Тогда
				
				Если ПараметрыУказанияСерий.ТоварВШапке Тогда
				Иначе
					ПараметрыОкругления = ПараметрыОкругленияКоличестваШтучныхТоваров();
					ПараметрыОкругления.ИмяТЧ = ПараметрыУказанияСерий.ИмяТЧТовары;
					
					ДлинаИмяПоляКоличество = СтрДлина(ПараметрыУказанияСерий.ИмяПоляКоличество);
					
					Если ДлинаИмяПоляКоличество > 10 Тогда
						ПараметрыОкругления.СуффиксДопРеквизита = Прав(ПараметрыУказанияСерий.ИмяПоляКоличество, ДлинаИмяПоляКоличество - 10);
					КонецЕсли;
					
					ОкруглитьКоличествоШтучныхТоваров(Объект, Неопределено, ПараметрыОкругления);
				КонецЕсли;
				
			КонецЕсли;
			
			ТЧ = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить();
			
			Запрос.УстановитьПараметр("ТаблицаТоваров",ТЧ);

			ОтборПоСтатусамУказанияСерий = Новый Массив;
			ОтборПоСтатусамУказанияСерий.Добавить(3);
			ОтборПоСтатусамУказанияСерий.Добавить(23);
			ОтборПоСтатусамУказанияСерий.Добавить(4);
			ОтборПоСтатусамУказанияСерий.Добавить(5);
			ОтборПоСтатусамУказанияСерий.Добавить(25);
			ОтборПоСтатусамУказанияСерий.Добавить(6);
			ОтборПоСтатусамУказанияСерий.Добавить(7);
			ОтборПоСтатусамУказанияСерий.Добавить(27);
			ОтборПоСтатусамУказанияСерий.Добавить(8);
			Если ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии
				ИЛИ ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
				ОтборПоСтатусамУказанияСерий.Добавить(9);
				ОтборПоСтатусамУказанияСерий.Добавить(10);
				ОтборПоСтатусамУказанияСерий.Добавить(11);
			КонецЕсли;
			Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
				ОтборПоСтатусамУказанияСерий.Добавить(13);
				ОтборПоСтатусамУказанияСерий.Добавить(14);
				ОтборПоСтатусамУказанияСерий.Добавить(15);
				ОтборПоСтатусамУказанияСерий.Добавить(17);
				ОтборПоСтатусамУказанияСерий.Добавить(18);
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ОтборПоСтатусам", ОтборПоСтатусамУказанияСерий);
			
			ПоляГруппировки = "Номенклатура,Характеристика" + ТекстПоляСвязи;
			
			ТекущаяГруппировка = Новый Структура(ПоляГруппировки);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			ТаблицаТоваров.Колонки.Добавить("НомерСтрокиДляСортировки",
				Новый ОписаниеТипов("Число",
				Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный)));
			
			Количество               = 0;
			НомераСтрокДокумента     = "";
			НомерСтрокиДляСортировки = 0;
			
			НоваяСтрокаТоваров = Неопределено;
			
			Пока Выборка.Следующий() Цикл
				
				Если Не НоменклатураСервер.СтруктурыРавны(ТекущаяГруппировка, Выборка, ПоляГруппировки) Тогда
					
					Если НоваяСтрокаТоваров <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,ТекущаяГруппировка);
						НоваяСтрокаТоваров.Количество = Количество;
						
						НомераСтрокДокумента = Лев(НомераСтрокДокумента, СтрДлина(НомераСтрокДокумента)-2);
						НоваяСтрокаТоваров.НомераСтрокДокумента     = НомераСтрокДокумента;
						НоваяСтрокаТоваров.НомерСтрокиДляСортировки = НомерСтрокиДляСортировки;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(ТекущаяГруппировка,Выборка);
					НоваяСтрокаТоваров       = ТаблицаТоваров.Добавить();
					НомерСтрокиДляСортировки = Выборка.НомерСтроки;
					Количество           = 0;
					НомераСтрокДокумента = "";
					
				КонецЕсли;
				
				Количество           = Количество + Выборка.Количество;
				НомераСтрокДокумента = НомераСтрокДокумента + Выборка.НомерСтроки + ", ";
				
			КонецЦикла;
			
			Если НоваяСтрокаТоваров <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,ТекущаяГруппировка);
				НоваяСтрокаТоваров.Количество = Количество;
				
				НомераСтрокДокумента = Лев(НомераСтрокДокумента, СтрДлина(НомераСтрокДокумента)-2);
				НоваяСтрокаТоваров.НомераСтрокДокумента = НомераСтрокДокумента;
				НоваяСтрокаТоваров.НомерСтрокиДляСортировки = НомерСтрокиДляСортировки;
			КонецЕсли;
			
			ТаблицаТоваров.Сортировать("НомерСтрокиДляСортировки");
			ТаблицаТоваров.Колонки.Удалить("НомерСтрокиДляСортировки");
			
		КонецЕсли;
		СтруктураДляВременногоХранилища = Новый Структура;
		СтруктураДляВременногоХранилища.Вставить("ТаблицаТоваров",ТаблицаТоваров);
		
		ТаблицаСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить(); 
		
		Если ПараметрыУказанияСерий.ИспользуютсяТоварныеМеста Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаТоваров.НомерСтроки,
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.ЕдиницыИзмерения) КАК Упаковка,
			|	ТаблицаТоваров.Количество
			|ПОМЕСТИТЬ ТаблицаТоваров
			|ИЗ
			|	&ТаблицаТоваров КАК ТаблицаТоваров
			|ГДЕ
			|	ТаблицаТоваров.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия,
			|	Упаковки.Ссылка КАК Упаковка
			|ПОМЕСТИТЬ УпаковкиТоваровПоТоварнымМестам
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК Упаковки
			|	ПО ЛОЖЬ
			|ГДЕ
			|	НЕ Упаковки.ПометкаУдаления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия,
			|	Упаковки.Ссылка
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК Упаковки
			|	ПО ЛОЖЬ
			|ГДЕ
			|	НЕ Упаковки.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ТаблицаТоваров.Серия
			|ПОМЕСТИТЬ ПервыеНомераСтрокТоваровПоТоварнымМестам
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|ГДЕ
			|	ЛОЖЬ
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ТаблицаТоваров.Серия
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия,
			|	ТаблицаТоваров.Упаковка,
			|	СУММА(ТаблицаТоваров.Количество) КАК Количество
			|ПОМЕСТИТЬ ПолныеНаборыТоваровПоТоварнымМестам
			|ИЗ
			|	(ВЫБРАТЬ
			|		Упаковки.ТекстВыбораТоваров,
			|		Упаковки.Номенклатура,
			|		Упаковки.Характеристика,
			|		Упаковки.Серия,
			|		Упаковки.Упаковка,
			|		ЕСТЬNULL(ТаблицаТоваров.Количество, 0) КАК Количество
			|	ИЗ
			|		УпаковкиТоваровПоТоварнымМестам КАК Упаковки
			|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваров
			|			ПО Упаковки.Номенклатура = ТаблицаТоваров.Номенклатура
			|				И Упаковки.Характеристика = ТаблицаТоваров.Характеристика
			|				И Упаковки.Серия = ТаблицаТоваров.Серия
			|				И Упаковки.Упаковка = ТаблицаТоваров.Упаковка
			|				&УсловиеСоединения) КАК ТаблицаТоваров
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия,
			|	ТаблицаТоваров.Упаковка
			|
			|ИМЕЮЩИЕ
			|	СУММА(ТаблицаТоваров.Количество) > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ТаблицаТоваров.Серия,
			|	ТаблицаТоваров.Количество КАК Количество,
			|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки
			|ИЗ
			|	(ВЫБРАТЬ
			|		МИНИМУМ(Упаковки.НомерСтроки) КАК НомерСтроки,
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура,
			|		ТаблицаТоваров.Характеристика,
			|		ТаблицаТоваров.Упаковка,
			|		ТаблицаТоваров.Серия,
			|		МИНИМУМ(ТаблицаТоваров.Количество) КАК Количество
			|	ИЗ
			|		ПолныеНаборыТоваровПоТоварнымМестам КАК ТаблицаТоваров
			|			ЛЕВОЕ СОЕДИНЕНИЕ ПервыеНомераСтрокТоваровПоТоварнымМестам КАК Упаковки
			|			ПО ТаблицаТоваров.Номенклатура = Упаковки.Номенклатура
			|				И ТаблицаТоваров.Номенклатура = Упаковки.Номенклатура
			|				И ТаблицаТоваров.Характеристика = Упаковки.Характеристика
			|				И ТаблицаТоваров.Серия = Упаковки.Серия
			|				&УсловиеСоединения
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура,
			|		ТаблицаТоваров.Характеристика,
			|		ТаблицаТоваров.Упаковка,
			|		ТаблицаТоваров.Серия
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаТоваров.НомерСтроки,
			|		ТаблицаТоваров.ТекстВыбораТоваров,
			|		ТаблицаТоваров.Номенклатура,
			|		ТаблицаТоваров.Характеристика,
			|		ТаблицаТоваров.Упаковка,
			|		ТаблицаТоваров.Серия,
			|		ТаблицаТоваров.Количество
			|	ИЗ
			|		ТаблицаТоваров КАК ТаблицаТоваров
			|	ГДЕ
			|		ИСТИНА) КАК ТаблицаТоваров
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТаблицаТоваров.НомерСтроки,
			|	ТаблицаТоваров.ТекстВыбораТоваров,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка";
			
			ТекстУсловияСоединенияТаблиц = "";
			ТекстВыбораТоваров= СтрЗаменить(ТекстВыбораТоваров, "ТаблицаТоваров.Упаковка,", "");
			ТекстВыбораУпаковок = СтрЗаменить(ТекстВыбораТоваров, "ТаблицаТоваров", "Упаковки");
			
			Для Каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
				
				Если СтрНайти(ТекстВыбораТоваров, ПолеСвязи) = 0
					Или ПолеСвязи = "Упаковка" Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				ТекстУсловияСоединенияТаблиц = ТекстУсловияСоединенияТаблиц + "
					|	И Упаковки." + ПолеСвязи + " = ТаблицаТоваров." + ПолеСвязи;
				
			КонецЦикла;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТоваров.ТекстВыбораТоваров,", ТекстВыбораТоваров);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Упаковки.ТекстВыбораТоваров,", ТекстВыбораУпаковок);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединения", ТекстУсловияСоединенияТаблиц);
			
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаСерий);
			ТаблицаСерий = Запрос.Выполнить().Выгрузить();
			
		Иначе
			
			СтрокиСерийКУдалению = ТаблицаСерий.НайтиСтроки(Новый Структура("Серия",Справочники.СерииНоменклатуры.ПустаяСсылка()));
			
			Для Каждого СтрМас Из СтрокиСерийКУдалению Цикл
				ТаблицаСерий.Удалить(СтрМас);
			КонецЦикла;
			
		КонецЕсли;
		
		СтруктураДляВременногоХранилища.Вставить("ТаблицаСерий",ТаблицаСерий);
		
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(СтруктураДляВременногоХранилища,УникальныйИдентификаторФормы);
	КонецЕсли;
	
	ПараметрыФормыУказанияСерий = Новый Структура("Номенклатура,Характеристика,СтатусУказанияСерий,ХарактеристикиИспользуются"+ТекстПоляСвязи);
	ПараметрыФормыУказанияСерий.Вставить("НомераСтрокДокумента", НомераСтрокДокумента); 
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыФормыУказанияСерий,Объект);
	Иначе
		ЗаполнитьЗначенияСвойств(ПараметрыФормыУказанияСерий,ТекущиеДанные);
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено Тогда
		ПараметрыФормыУказанияСерий.Вставить("Склад", Неопределено);
	ИначеЕсли ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено Тогда
		ПараметрыФормыУказанияСерий.Вставить("Склад", ТекущиеДанные[ПараметрыУказанияСерий.ИмяПоляСклад]);
	Иначе
		ПараметрыФормыУказанияСерий.Вставить("Склад", Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
	КонецЕсли;
	
	ПараметрыФормыУказанияСерий.Вставить("УпаковкаДляПодстановки",УпаковкаДляПодстановки);
	
	ПараметрыФормыУказанияСерий.Вставить("АдресВоВременномХранилище",АдресВоВременномХранилище);
	ПараметрыФормыУказанияСерий.Вставить("РегистрироватьСерии", РегистрироватьСерии);
	ПараметрыФормыУказанияСерий.Вставить("ТолькоПросмотр", ПараметрыУказанияСерий.ТолькоПросмотр);
	ПараметрыФормыУказанияСерий.Вставить("ТолькоРедактированиеКоличества", Ложь);
	ПараметрыФормыУказанияСерий.Вставить("Количество",КоличествоВДокументе);
	ПараметрыФормыУказанияСерий.Вставить("АдресВоВременномХранилище",АдресВоВременномХранилище);
	ПараметрыФормыУказанияСерий.Вставить("СерииВТЧТовары", ПараметрыУказанияСерий.ИмяТЧСерии = ПараметрыУказанияСерий.ИмяТЧТовары);
	Если Объект.Свойство("Ссылка") Тогда
		ПараметрыФормыУказанияСерий.Вставить("Регистратор",Объект.Ссылка);
	КонецЕсли;
	ПараметрыФормыУказанияСерий.Вставить("ПараметрыУказанияСерий",ПараметрыУказанияСерий);
	
	Если РегистрироватьСерии Тогда
		ИмяФормы = "Обработка.ПодборСерийВДокументы.Форма.РегистрацияИПодборСерийПоОднойСтрокеТоваров";
	Иначе
		ИмяФормы = "Обработка.ПодборСерийВДокументы.Форма.ПодборСерийПоОстаткамДляВсехСтрокТоваров";
	КонецЕсли;
	
	ПараметрыФормыУказанияСерий.Вставить("ИмяФормы", ИмяФормы);
	
	Если ЗначениеЗаполнено(ЗаголовокКолонкиКоличество) Тогда
		ПараметрыФормыУказанияСерий.Вставить("ЗаголовокКолонкиКоличество", ЗаголовокКолонкиКоличество);
	КонецЕсли;
	
	Возврат ПараметрыФормыУказанияСерий;
	
КонецФункции

Функция ДействияСРесурсамиИзСтроки(Ресурсы)
	
	ДействияСРесурсами = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ресурсы);
	
	РавноНулю           = Новый Массив();
	НеБольше            = Новый Массив();
	БольшеНуля          = Новый Массив();
	
	Для Каждого СтрокаРесурса Из ДействияСРесурсами Цикл                                             
		
		НачалоИмениПоля = СтрНайти(СтрокаРесурса, "[");
		
		Если НачалоИмениПоля > 0 Тогда
			СтрокаБезНачИмени = Лев(СтрокаРесурса, НачалоИмениПоля - 1);
		
			Если СтроковыеФункцииКлиентСервер.НайтиСимволСКонца(СтрокаБезНачИмени, "НЕ")>0 Тогда
				ИмяРесурса = СтрЗаменить(СтрокаРесурса, "НЕ", "");
				ИмяРесурса = СтрЗаменить(ИмяРесурса, "[", "");
				ИмяРесурса = СтрЗаменить(ИмяРесурса, "]", "");
				ИмяРесурса = СокрЛП(ИмяРесурса);
			
				РавноНулю.Добавить(ИмяРесурса);
			ИначеЕсли СтроковыеФункцииКлиентСервер.НайтиСимволСКонца(СтрокаБезНачИмени, "ПО")>0 Тогда
				ИмяРесурса = СтрЗаменить(СтрокаРесурса, "ПО", "");
				ИмяРесурса = СтрЗаменить(ИмяРесурса, "[", "");
				ИмяРесурса = СтрЗаменить(ИмяРесурса, "]", "");
				ИмяРесурса = СокрЛП(ИмяРесурса);
				
				НеБольше.Добавить(ИмяРесурса);
			Иначе
				ИмяРесурса = СтрЗаменить(СтрокаРесурса, "[", "");
				ИмяРесурса = СтрЗаменить(ИмяРесурса, "]", "");
				ИмяРесурса = СокрЛП(ИмяРесурса);
			
				БольшеНуля.Добавить(ИмяРесурса);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДействий = Новый Структура();
	СтруктураДействий.Вставить("РавноНулю",           РавноНулю);
	СтруктураДействий.Вставить("НеБольше",            НеБольше);
	СтруктураДействий.Вставить("БольшеНуля",          БольшеНуля);
	
	Возврат СтруктураДействий;
	
КонецФункции

// Переносит значение числового поля из таблицы источник в таблицу приемник. 
//
// Параметры:
//  ТаблицаИсточник			 - ТаблицаЗначений - 
//  ТаблицаПриемник			 - ТаблицаЗначений - 
//  ИмяПоляКоличество		 - Строка - Заполняемое поле. Если в таблице приемнике отсутствует - добавляется.
//  Измерения				 - Строка - Ключевые поля для поиска соответствий между строками таблиц
//  Ресурсы					 - Строка - Условия накладываемые на числовые поля строк таблицы приемника, вида:
//  									"НЕ [Количество], ПО [КоличествоВЗаказе], [КоличествоВЗаказе]"
//  									Где в квадратных скобках пишется имя числового поля в таблице приемнике, 
//  									и перед ним идентификатор действия:
//  									НЕ [ИмяПоля] - значение ИмяПоля должно быть равно нулю
//  									ПО [ИмяПоля] - в строке приемнике в поле *ИмяПоляКоличество* нельзя записать число большее,
//  									чем в ИмяПоля.
//  									[ИмяПоля] - значение ИмяПоля должно быть больше нуля
//  РаспределятьПолностью	 - Булево - Если в результате действия ограничения условия "ПО", в строке источнике остается нераспределенное
//  									количество - разрешаем списать это количество в последнюю сопоставленную строку приемника.
//
Процедура РаспределитьКоличество(ТаблицаИсточник, ТаблицаПриемник, ИмяПоляКоличество, Измерения, Ресурсы, РаспределятьПолностью = Ложь) Экспорт
	
	Если ТаблицаПриемник.Колонки.Найти(ИмяПоляКоличество) = Неопределено Тогда
		ТаблицаПриемник.Колонки.Добавить(ИмяПоляКоличество, Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ПараметрыОтбораСтрок = ДействияСРесурсамиИзСтроки(Ресурсы);
	
	Отбор = Новый Структура(Измерения);
	
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаИсточник);
		НайденныеСтроки = ТаблицаПриемник.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаПриемник Из НайденныеСтроки Цикл
			
			ПроверкаПройдена = СтрокаСоответствуетОтбору(СтрокаПриемник, ПараметрыОтбораСтрок);
			
			Если Не ПроверкаПройдена Тогда
				Продолжить;
			КонецЕсли;
			
			// Если массив НеБольше - пустой, тогда переносится как есть
			// иначе происходит доп. проверка на то, что результат заполнения поля не превысит значения проверяемых полей.
			Количество = СтрокаИсточник[ИмяПоляКоличество];
			
			КоличествоПолейПроверки = ПараметрыОтбораСтрок.НеБольше.Количество();
			Если КоличествоПолейПроверки > 0 Тогда
				МаксимальноеКоличество = СтрокаПриемник[ПараметрыОтбораСтрок.НеБольше[0]];
				Для н = 1 По КоличествоПолейПроверки - 1 Цикл
					ПолеПроверки = ПараметрыОтбораСтрок.НеБольше[КоличествоПолейПроверки - н - 1];
					МаксимальноеКоличество = Мин(СтрокаПриемник[ПолеПроверки], МаксимальноеКоличество);
				КонецЦикла;
				
				Превышение = СтрокаПриемник[ИмяПоляКоличество] + Количество - МаксимальноеКоличество;
				Если Превышение >= Количество Тогда
					Количество = 0;
				ИначеЕсли Превышение > 0 Тогда
					Количество = Количество - Превышение;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаПриемник[ИмяПоляКоличество] = СтрокаПриемник[ИмяПоляКоличество] + Количество;
			СтрокаИсточник[ИмяПоляКоличество] = СтрокаИсточник[ИмяПоляКоличество] - Количество;
			
		КонецЦикла;
		
		// Если было распределено не полное количество, записываем остаток в последнюю найденную строку.
		Если РаспределятьПолностью
			И СтрокаИсточник[ИмяПоляКоличество] > 0 
			И НайденныеСтроки.Количество() > 0 Тогда
			
			Количество = СтрокаИсточник[ИмяПоляКоличество];
			НайденнаяСтрока = НайденныеСтроки[НайденныеСтроки.Количество() - 1];
			
			Если Не СтрокаСоответствуетОтбору(НайденнаяСтрока, ПараметрыОтбораСтрок) Тогда
				Продолжить;
			КонецЕсли;
			
			НайденнаяСтрока[ИмяПоляКоличество] = НайденнаяСтрока[ИмяПоляКоличество] + Количество;
			СтрокаИсточник[ИмяПоляКоличество] = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Извлекает из временного хранилища серии, указанные в форме редактирования серий, помещает эти строки в ТЧ "Серии" документа,
//	перерасчитывает статусы указания серий строках товаров.
//
// Параметры:
//  Объект						 - ДанныеФормыСтуктура - основной реквизит формы документа,
//  ПараметрыУказанияСерий		 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа,
//  ПараметрыФормыУказанияСерий	 - Структура - см. ПараметрыФормыУказанияСерий().
//  Действия					 - Структура - описание действий, которые необходимо выполнить со строками после указания серий, см. ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ()
//  КешированныеЗначения		 - Структура - Сохраненные значения параметров, используемых при обработке строк.
//
Процедура ОбработатьУказаниеСерий(Объект, ПараметрыУказанияСерий,ПараметрыФормыУказанияСерий, Действия = Неопределено, КешированныеЗначения = Неопределено) Экспорт
	
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено;
	
	// Обработка подбора серий
	// Если обрабатывается результаты формы подбора серий,
	// то серии целиком загружаются из обработки.
	
	Если Не ПараметрыФормыУказанияСерий.РегистрироватьСерии Тогда 
		
		СтруктураВозврата = ПолучитьИзВременногоХранилища(ПараметрыФормыУказанияСерий.АдресВоВременномХранилище);
		
		Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
			
			ЗаполнитьСерииВТЧПоТаблицеСерий(Объект,
											ПараметрыУказанияСерий,
											СтруктураВозврата.ТаблицаСерий,
											Действия);
											
			Если ПараметрыУказанияСерий.ИспользуютсяТоварныеМеста
				И Не ПараметрыУказанияСерий.ИспользоватьАдресноеХранение Тогда
				НоменклатураСервер.РазбитьТоварыПоТоварнымМестам(Объект[ПараметрыУказанияСерий.ИмяТЧТовары]);									
			КонецЕсли;
			
		Иначе
			Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Загрузить(СтруктураВозврата.ТаблицаСерий);
			ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
		КонецЕсли;
		
		
		Возврат;
	КонецЕсли;
	// Конец области обработки подбора серий

//	Обработка регистрации серий
// Удалим прежние строки серий
	ТекстПоляСвязи = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас  ;
	КонецЦикла;
	СтруктураПоиска = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
	
	НомерВставляемойСтроки = 0;
	
	Упаковки = Новый Массив();
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ЕстьУпаковкиВТЧТовары = Ложь;
		Если Объект.Свойство("Упаковка") Тогда
			Упаковки.Добавить(Объект.Упаковка);
		КонецЕсли;
	Иначе
		КолонкиТаблицы = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(Новый Массив);
		ЕстьУпаковкиВТЧТовары = КолонкиТаблицы.Колонки.Найти("Упаковка") <> Неопределено;
	КонецЕсли;
	
	КолонкиТаблицы = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить(Новый Массив);
	ЕстьУпаковкиВТЧСерии = КолонкиТаблицы.Колонки.Найти("Упаковка") <> Неопределено;
	
	Если ЕстьУпаковкиВТЧТовары Тогда
		УпаковкиТЧТовары = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(, "Упаковка").ВыгрузитьКолонку("Упаковка");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Упаковки, УпаковкиТЧТовары);
	КонецЕсли;
	
	Если ЕстьУпаковкиВТЧСерии Тогда
		УпаковкиТЧСерии = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить(, "Упаковка").ВыгрузитьКолонку("Упаковка");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Упаковки, УпаковкиТЧСерии);
	КонецЕсли;
	
	Упаковки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Упаковки);
	ТипыУпаковок = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Упаковки, "ТипУпаковки");
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска,ПараметрыФормыУказанияСерий);
	НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоиска);
	
	КоличествоУпаковокДоУдаленияСерий = 0;
	УпаковкиТоварныхМест = Новый Массив();
	
	ТаблицаСерий = ПолучитьИзВременногоХранилища(ПараметрыФормыУказанияСерий.АдресВоВременномХранилище);
	
	Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
		
		Если ПараметрыУказанияСерий.ИмяТЧСерии = ПараметрыУказанияСерий.ИмяТЧТовары Тогда
			Если ЕстьУпаковкиВТЧТовары Тогда
				КоличествоУпаковокДоУдаленияСерий = КоличествоУпаковокДоУдаленияСерий + СтрМас.КоличествоУпаковок;
			Иначе
				КоличествоУпаковокДоУдаленияСерий = КоличествоУпаковокДоУдаленияСерий + СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество];
			КонецЕсли;
		КонецЕсли;
		
		Если ТаблицаСерий.НайтиСтроки(Новый Структура("Серия", СтрМас.Серия)).Количество() = 0 Тогда 
			Если НомерВставляемойСтроки = 0 Тогда
				НомерВставляемойСтроки = СтрМас.НомерСтроки;
			КонецЕсли;
			Если ПараметрыФормыУказанияСерий.ТолькоРедактированиеКоличества Тогда
				СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество] = 0;
			Иначе
				Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Удалить(СтрМас);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Добавим новые строки серий
	СтрокиСерийДляОбработки =  Новый Массив;
	
	КоличествоУпаковокПослеУдаленияСерий = 0;
	
	Для Каждого СтрТабл Из ТаблицаСерий Цикл
		СтруктураПоискаПоСериям = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПоиска);
		СтруктураПоискаПоСериям.Вставить("Серия", СтрТабл.Серия);
			
		СтарыеСтроки = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаПоСериям);
			
		Если СтарыеСтроки.Количество() = 0 Тогда 
			Если НомерВставляемойСтроки = 0 Тогда
				НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Добавить();
			Иначе
				НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Вставить(НомерВставляемойСтроки - 1);
			КонецЕсли;
		Иначе
			НоваяСтрока = СтарыеСтроки[0];
		КонецЕсли;
		
		НомерВставляемойСтроки = НоваяСтрока.НомерСтроки + 1;
			
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ПараметрыФормыУказанияСерий);
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТабл);
			
		Если ЕстьУпаковкиВТЧСерии И ПараметрыФормыУказанияСерий.Свойство("УпаковкаДляПодстановки") Тогда
				
			НоваяСтрока.Упаковка = ПараметрыФормыУказанияСерий.УпаковкаДляПодстановки;
				
		КонецЕсли;
			
		Если ПараметрыУказанияСерий.ИмяПоляКоличество <> "Количество" И ПараметрыУказанияСерий.ИмяПоляКоличество <> "КоличествоУпаковок" Тогда
			
			Если ЕстьУпаковки Тогда
				НоваяСтрока[ПараметрыУказанияСерий.ИмяПоляКоличество] = СтрТабл.КоличествоУпаковок;
			Иначе
				НоваяСтрока[ПараметрыУказанияСерий.ИмяПоляКоличество] = СтрТабл.Количество;
			КонецЕсли;
			
		КонецЕсли;
			
		СтрокиСерийДляОбработки.Добавить(НоваяСтрока);
			
		Если ПараметрыУказанияСерий.ИмяТЧСерии = ПараметрыУказанияСерий.ИмяТЧТовары Тогда
				
			Если Действия <> Неопределено Тогда
					
				ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОбработатьСтрокуТЧ(НоваяСтрока, Действия, КешированныеЗначения);
					
			КонецЕсли;
			
			Если ЕстьУпаковкиВТЧТовары Тогда
				КоличествоУпаковокПослеУдаленияСерий = КоличествоУпаковокПослеУдаленияСерий + НоваяСтрока.КоличествоУпаковок; 
			Иначе
				КоличествоУпаковокДоУдаленияСерий = КоличествоУпаковокДоУдаленияСерий + СтрМас[ПараметрыУказанияСерий.ИмяПоляКоличество];
			КонецЕсли;
				
		КонецЕсли;
			
	КонецЦикла;
	
	Если ПараметрыУказанияСерий.ИмяТЧСерии = ПараметрыУказанияСерий.ИмяТЧТовары Тогда
		
		Дельта = КоличествоУпаковокПослеУдаленияСерий - КоличествоУпаковокДоУдаленияСерий;
		
		Возврат;
	КонецЕсли;
	
	Если ЕстьУпаковки
		И ЗначениеЗаполнено(ПараметрыФормыУказанияСерий.Упаковка) Тогда //не заполнена для тары
		КоличествоСерий = ТаблицаСерий.Итог("КоличествоУпаковок");
		КоличествоТоваров = ПараметрыФормыУказанияСерий.Количество 
							/Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ПараметрыФормыУказанияСерий.Упаковка, ПараметрыФормыУказанияСерий.Номенклатура); 
	Иначе
		КоличествоСерий = ТаблицаСерий.Итог("Количество");
		КоличествоТоваров = ПараметрыФормыУказанияСерий.Количество;
	КонецЕсли;	
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		
		Если КоличествоСерий <> КоличествоТоваров 
			И Действия <> Неопределено
			И Действия.Свойство("ОбновлятьКоличествоТоваровПриРегистрацииСерий")
			И Действия.ОбновлятьКоличествоТоваровПриРегистрацииСерий Тогда
			
			Объект[ПараметрыУказанияСерий.ИмяПоляКоличество] = КоличествоСерий;
			КоличествоТоваров = КоличествоСерий;
			ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОбработатьСтрокуТЧ(Объект, Действия, КешированныеЗначения);
			
		КонецЕсли;
		
		СерииУказаныПолностью = (КоличествоСерий = КоличествоТоваров);
		
		НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
																			Объект.СтатусУказанияСерий,
																			СерииУказаныПолностью,
																			КоличествоСерий);
		
	Иначе
		
		НайденныеСтрокиТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоиска);
		
		Если КоличествоСерий <> КоличествоТоваров 
			И Действия <> Неопределено
			И Действия.Свойство("ОбновлятьКоличествоТоваровПриРегистрацииСерий")
			И Действия.ОбновлятьКоличествоТоваровПриРегистрацииСерий Тогда
			
			ЕстьПересчетКоличества = Ложь;
			КоличествоЕдиницСуффикс = "";
			
			Если Действия.Свойство("ПересчитатьКоличествоЕдиниц") Тогда
				Действия.Удалить("ПересчитатьКоличествоЕдиниц");
				ЕстьПересчетКоличества = Истина;
			ИначеЕсли Действия.Свойство("ПересчитатьКоличествоЕдиницСуффикс") Тогда
				КоличествоЕдиницСуффикс = Действия.ПересчитатьКоличествоЕдиницСуффикс;
				Действия.Удалить("ПересчитатьКоличествоЕдиницСуффикс");
				ЕстьПересчетКоличества = Истина;
			КонецЕсли;
			
			Если Действия.Свойство("ПересчитатьКоличествоУпаковок") Тогда
				Действия.Удалить("ПересчитатьКоличествоУпаковок");
				ЕстьПересчетКоличества = Истина;
			КонецЕсли;
			
			КоличествоКРаспределению = КоличествоСерий - КоличествоТоваров;
			
			Для Каждого СтрокаТоваров Из НайденныеСтрокиТоваров Цикл
				Дельта = КоличествоКРаспределению;
				
				Если -Дельта > СтрокаТоваров[ПараметрыУказанияСерий.ИмяПоляКоличество] Тогда
					Дельта = -СтрокаТоваров[ПараметрыУказанияСерий.ИмяПоляКоличество];
					СтрокаТоваров[ПараметрыУказанияСерий.ИмяПоляКоличество] = 0;
				Иначе
					СтрокаТоваров[ПараметрыУказанияСерий.ИмяПоляКоличество] = СтрокаТоваров[ПараметрыУказанияСерий.ИмяПоляКоличество] + Дельта;
				КонецЕсли;
				
				Если ЕстьПересчетКоличества Тогда
					Если ЕстьУпаковки Тогда
						Если КоличествоЕдиницСуффикс <> "" Тогда
							Действия.Вставить("ПересчитатьКоличествоЕдиницСуффикс", КоличествоЕдиницСуффикс);
						Иначе
							Действия.Вставить("ПересчитатьКоличествоЕдиниц");
						КонецЕсли; 
					Иначе
						Если КоличествоЕдиницСуффикс <> "" Тогда
							Действия.Вставить("ПересчитатьКоличествоУпаковокСуффикс", КоличествоЕдиницСуффикс);
						Иначе
							Действия.Вставить("ПересчитатьКоличествоУпаковок");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				КоличествоТоваров = КоличествоТоваров + Дельта;
				
				КоличествоКРаспределению = КоличествоКРаспределению - Дельта;
				
				ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОбработатьСтрокуТЧ(СтрокаТоваров, Действия, КешированныеЗначения);
				
				Если КоличествоКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		СерииУказаныПолностью = (КоличествоСерий = КоличествоТоваров
			И (КоличествоТоваров > 0
				Или ПараметрыФормыУказанияСерий.ТолькоРедактированиеКоличества));
		 // В режиме ТолькоРедактированиеКоличества допускается нулевое количество как в ТЧ Товары, так и в ТЧ Серии.
		Для Каждого СтрМас Из НайденныеСтрокиТоваров Цикл
				
			Если СтрМас.СтатусУказанияСерий = 0 Тогда
				ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий, НайденныеСтрокиТоваров, СтрокиСерийДляОбработки);
				Прервать;
			КонецЕсли;
		
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
																					СтрМас.СтатусУказанияСерий,
																					СерииУказаныПолностью,
																					КоличествоСерий,
																					СтрМас);
			Иначе
				
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
																						СтрМас[ИмяПоляСтатус],
																						СерииУказаныПолностью,
																						КоличествоСерий,
																						СтрМас);
				КонецЦикла;
				
			КонецЕсли;
				
		КонецЦикла;
	КонецЕсли;
// Конец области обработки регистрации серий
КонецПроцедуры

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект			 - ДокументОбъект или ДанныеФормыСтруктура - документ, для которого нужно сформировать параметры проверки,
//  МенеджерОбъекта	 - ДокументМенеджер - менеджер документа, для которого нужно сформировать параметры проверки.
// 
// Возвращаемое значение:
//  Структура - параметры, уточняющие особенности указания серий в каждом документе, состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект, МенеджерОбъекта) Экспорт
	
	ИменаРеквизитов = МенеджерОбъекта.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий();
	ЗначенияРеквизитов = ЗначенияРеквизитовДляЗаполненияПараметровУказанияСерий(Объект, ИменаРеквизитов);
	Возврат МенеджерОбъекта.ПараметрыУказанияСерий(ЗначенияРеквизитов);
	
КонецФункции

// Сохраняет режим сканирования серий. Используется для того, чтобы режим сканирования был един для всех форм.
//
// Параметры:
//		Форма - УправляемаяФорма - форма, в которой должен быть реквизит строкового типа РежимСканированияСерий.
//
Процедура СохранитьНастройкуРежимСканированияСерий(Форма) Экспорт
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить("РежимСканированияСерий","",Форма.РежимСканированияСерий);
	
КонецПроцедуры

// Загружает из настроек режим сканирования серий. Если настройка не была ранее сохранена, то используется режим ТоварВсеСерии.
//
// Параметры:
//		Форма - УправляемаяФорма - форма, в которой должен быть реквизит строкового типа РежимСканированияСерий.
//
Процедура ЗагрузитьНастройкуРежимСканированияСерий(Форма) Экспорт
	
	Форма.РежимСканированияСерий = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить("РежимСканированияСерий", "",
		"ТоварВсеСерии");
	
КонецПроцедуры

// Конец области процедур работы с сериями

// Процедуры и функции заполнения служебных реквизитов по номенклатуре

// Получает служебные реквизиты по номенклатуре в структуре
//
// Параметры:
// 		СтруктураДанных - Структура, СтрокаТаблицыЗначений - Структура данных, в которой необходимо заполнить поля
// 		СтруктураДействий - Структура - Структура с действиями по получению служебных реквизитов.
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтруктуре(СтруктураДанных, СтруктураДействий) Экспорт
	
	ОбработкаТабличнойЧастиСерверЕГАИСУТ.НормализоватьДействия(СтруктураДействий);
	
	СтруктураДопДанных = ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОписаниеДополнительнойИнформации(СтруктураДействий);
	
	ТаблицаВыгрузки = Новый ТаблицаЗначений;
	ТаблицаВыгрузки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	Для Каждого Источник Из СтруктураДопДанных.СтруктураИсточников Цикл
		ТаблицаВыгрузки.Колонки.Добавить(Источник.Ключ, ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОписаниеТиповПоТипу(ТипЗнч(СтруктураДанных[Источник.Ключ])));
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(ТаблицаВыгрузки.Добавить(), СтруктураДанных);
	
	Запрос = Новый Запрос(ОбработкаТабличнойЧастиСерверЕГАИСУТ.ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных));
	Запрос.УстановитьПараметр("КоллекцияДанных", ТаблицаВыгрузки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка, СтруктураДопДанных.РеквизитыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСлужебныеРеквизитыВСтруктуре()

// Заполняет служебные реквизиты по номенклатуре в коллекции
//
// Параметры:
//  КоллекцияДанных		 - ДанныеФормыКоллекция, ТаблицаЗначений - Таблица, в которой необходимо заполнить реквизиты
//  СтруктураДействий	 - Структура							 - Структура с действиями по получению служебных реквизитов
//  СтрокиЗаполнения	 - Массив, ДанныеФормыЭлементКоллекции	 - строки, для которых требуется заполнение.
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(КоллекцияДанных, СтруктураДействий, СтрокиЗаполнения = Неопределено) Экспорт
	
	ОбработкаТабличнойЧастиСерверЕГАИСУТ.НормализоватьДействия(СтруктураДействий);
	
	СтруктураДопДанных = ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОписаниеДополнительнойИнформации(СтруктураДействий);
	Колонки = "НомерСтроки" + СтруктураДопДанных.РеквизитыВыгрузки;
	
	Если СтрокиЗаполнения = Неопределено Тогда
		
		Если ТипЗнч(КоллекцияДанных) = Тип("ТаблицаЗначений") Тогда
			ПараметрКоллекция = КоллекцияДанных;
		Иначе
			ПараметрКоллекция = КоллекцияДанных.Выгрузить(, Колонки);
		КонецЕсли;
		
	ИначеЕсли СтрокиЗаполнения.Количество() > 0 Тогда
		
		Если ТипЗнч(КоллекцияДанных) = Тип("ТаблицаЗначений") Тогда
			ПараметрКоллекция = КоллекцияДанных.Скопировать(СтрокиЗаполнения, Колонки);
		Иначе
			ПараметрКоллекция = КоллекцияДанных.Выгрузить(СтрокиЗаполнения, Колонки);
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(ОбработкаТабличнойЧастиСерверЕГАИСУТ.ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных));
	Запрос.УстановитьПараметр("КоллекцияДанных", ПараметрКоллекция);
	
	Если СтрокиЗаполнения <> Неопределено Тогда
		
		ТЗРезультат = Запрос.Выполнить().Выгрузить();
		Для Каждого Стр Из ТЗРезультат Цикл
			ЗаполнитьЗначенияСвойств(КоллекцияДанных[Стр.НомерСтроки - 1], Стр, СтруктураДопДанных.РеквизитыЗаполнения);
		КонецЦикла;
		
	Иначе
		
		Если ПараметрКоллекция.Количество() > 0 Тогда
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Для Номер = 0 По КоллекцияДанных.Количество()-1 Цикл
				Выборка.Следующий(); // Количество строк в выборке по запросу всегда равно количеству строк в коллекции
				ЗаполнитьЗначенияСвойств(КоллекцияДанных[Номер], Выборка, СтруктураДопДанных.РеквизитыЗаполнения);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения колонок "ХарактеристикиИспользуются" в формах.
//
// Параметры:
//  ТаблицаФормы		 - ДанныеФормыКоллекция		 - табличная часть объекта, в котором нужно заполнить
//  		колонку "ХарактеристикиИспользуются";
//  ПараметрыЗаполнения	 - Структура, Неопределено	 - дополнительные параметры.
//
Процедура ЗаполнитьПризнакИспользованияХарактеристик(ТаблицаФормы,ПараметрыЗаполнения = Неопределено) Экспорт
	
	Перем СуффиксДопРеквизита;
	
	Если ТаблицаФормы.Количество() = 0
		Или Не ПолучитьФункциональнуюОпцию("ИспользованиеХарактеристикНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаполнения <> Неопределено Тогда
		
		ПараметрыЗаполнения.Свойство("СуффиксДопРеквизита",СуффиксДопРеквизита);	
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки,";
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "	
	|	ТаблицаТоваров.Номенклатура" + СуффиксДопРеквизита +"  КАК Номенклатура" + СуффиксДопРеквизита +","; 
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "	
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки,";
	Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
		ТекстЗапроса = ТекстЗапроса + "	
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура" + СуффиксДопРеквизита +" КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ХарактеристикиИспользуются" + СуффиксДопРеквизита +",";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ВестиУчетПоХарактеристикам
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	
	СтрокаДопРеквизитов = ?(Не ЗначениеЗаполнено(СуффиксДопРеквизита), "", ",Номенклатура"+СуффиксДопРеквизита);
	Запрос.УстановитьПараметр("ТаблицаТоваров",  ТаблицаФормы.Выгрузить(,"НомерСтроки,Номенклатура"+СтрокаДопРеквизитов));
	
	ТаблицаПризнаков = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрТабл Из ТаблицаФормы Цикл
		
		СтрокаХарактеристик = ТаблицаПризнаков[СтрТабл.НомерСтроки-1];
		
		СтрТабл.ХарактеристикиИспользуются = СтрокаХарактеристик.ХарактеристикиИспользуются;
		Если ЗначениеЗаполнено(СуффиксДопРеквизита) Тогда
			СтрТабл["ХарактеристикиИспользуются"+СуффиксДопРеквизита] = СтрокаХарактеристик["ХарактеристикиИспользуются"+СуффиксДопРеквизита];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Конец области процедур и функций заполнения служебных реквизитов по номенклатуре

// Условное оформление

// Устанавливаем условное оформление для назначений номенклатуры
//
// Параметры:
//		Форма - Форма - Содержит данную форму 
//		ИмяПоляВводаНазначения - Строка - Наименование элемента формы, содержащего характеристики номенклатуры,
//											   если оно отличается от "ТоварыХарактеристика"
//		ПутьКПолюОтбораТипНоменклатуры - Строка - Полный путь к реквизиту "Тип номенклатуры",
//									если он отличается от "Объект.Товары.ТипНоменклатуры"
//		ТолькоТовары - Булево, Истина - признак того, что условное оформление применяется только в отношении товаров.
// 
Процедура УстановитьУсловноеОформлениеНазначенияНоменклатуры(Форма,
	                                                            ИмяПоляВводаНазначения = "ТоварыНазначение",
																ПутьКПолюОтбораТипНоменклатуры = "Объект.Товары.ТипНоменклатуры",
																ТолькоТовары = Истина) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов") Тогда
		Возврат;
	КонецЕсли;
																
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаНазначения].Имя);
	
	ГруппаОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбораТипНоменклатуры);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Услуга;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбораТипНоменклатуры);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.ПустаяСсылка();
	
	Если ТолькоТовары Тогда
		Текст = НСтр("ru = '<для товаров>'");
	Иначе
		Текст = НСтр("ru = '<для товаров и работ>'");
	КонецЕсли;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", Текст);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
КонецПроцедуры

// Устанавливаем условное оформление для характеристик номенклатуры
//
// Параметры:
// 		Форма - Форма - Содержит данную форму 
// 		ИмяПоляВводаХарактеристики - Строка - Наименование элемента формы, содержащего характеристики номенклатуры,
//											   если оно отличается от "ТоварыХарактеристика"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "характеристики используются",
//									если он отличается от "Объект.Товары.ХарактеристикиИспользуются".
// 
Процедура УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма,
	                                                            ИмяПоляВводаХарактеристики = "ТоварыХарактеристика",
																ПутьКПолюОтбора = "Объект.Товары.ХарактеристикиИспользуются") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользованиеХарактеристикНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
																
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаХарактеристики].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

// Устанавливаем условное оформление для серий номенклатуры
//
// Параметры:
//		Форма - Форма - Содержит данную форму
//		ОсобыйВариантУказанияСерий - Булево, Строка - Ложь, если серии указываются в отдельной ТЧ,
//			"СерииВсегдаВТЧТовары" - если у объекта нет специальной ТЧ для указания серий,
//			"СерииПриПланированииОтгрузкиУказываютсяВТЧТовары" - если серии могут указываться в разных ТЧ,
//				при этом серии с политикой учета "При планировании отгрузки" указываются в ТЧ Товары,
//		ИмяПоляВводаСерии - Строка - Наименование элемента формы, содержащего серии номенклатуры,
//									   если оно отличается от "ТоварыСерия"
//		ПутьКПолюОтбораСтатусУказанияСерий - Строка - Полный путь к реквизиту "Статус указания серий",
//														если он отличается от "Объект.Товары.СтатусУказанияСерий"
//		ПутьКПолюОтбораТипНоменклатуры - Строка - Полный путь к реквизиту "Тип номенклатуры",
//														если он отличается от "Объект.Товары.ТипНоменклатуры".
//
Процедура УстановитьУсловноеОформлениеСерийНоменклатуры(Форма,
														ИмяПоляВводаСерии = "ТоварыСерия",
														ПутьКПолюОтбора = "Объект.Товары.Номенклатура.ВестиУчетПоСериям") Экспорт
														
	Если Не ПолучитьФункциональнуюОпцию("ИспользованиеСерийНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ИмяПоляВводаСерии) = Тип("Булево") Тогда
		Возврат;
	КонецЕсли;
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаСерии].Имя);
		
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
		
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<серии не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

// Устанавливаем условное оформление для единиц измерения номенклатуры
//
// Параметры:
// 		Форма - Форма - Содержит данную форму
// 		ИмяПоляВводаЕдиницИзмерения - Строка - Наименование элемента формы, содержащего ед. измерения номенклатуры,
//									   			если оно отличается от "ТоварыНоменклатураЕдиницаИзмерения"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "Упаковка",
//									если он отличается от "Объект.Товары.Упаковка".
//
Процедура УстановитьУсловноеОформлениеЕдиницИзмерения(Форма,
													  ИмяПоляВводаЕдиницИзмерения = "ТоварыНоменклатураЕдиницаИзмерения",
													  ПутьКПолюОтбора = "Объект.Товары.Упаковка") Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаЕдиницИзмерения].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);	
	
КонецПроцедуры

// Дополнительные параметры функции НоменклатураСервер.УстановитьУсловноеОформлениеСодержания.
// 
// Возвращаемое значение:
//   Структура - свойства:
// 		* ИмяПоляВводаСодержания - Строка - Наименование элемента формы "Содержание",
//									   		значение по умолчанию "ТоварыСодержание"
// 		* ИмяПоляВводаУпаковки - Строка - Наименование элемента формы "Упаковка",
//									   		значение по умолчанию "ТоварыУпаковка"
// 		* ПутьКПолюОтбораВариантаОформления - Строка - Полный путь к реквизиту "ВариантОформленияПродажи",
//														значение по умолчанию "Объект.Товары.ВариантОформленияПродажи"
// 		* ПутьКПолюОтбораСодержания - Строка - Полный путь к реквизиту "Содержание",
//												значение по умолчанию "Объект.Товары.Содержание".
//
Функция ДополнительныеПараметрыУстановитьУсловноеОформлениеСодержания() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяПоляВводаСодержания", "ТоварыСодержание");
	ДополнительныеПараметры.Вставить("ИмяПоляВводаУпаковки", "ТоварыУпаковка");
	ДополнительныеПараметры.Вставить("ПутьКПолюОтбораВариантаОформления", "Объект.Товары.ВариантОформленияПродажи");
	ДополнительныеПараметры.Вставить("ПутьКПолюОтбораСодержания", "Объект.Товары.Содержание");
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Устанавливаем условное оформление содержания в зависимости от варианта оформления продажи.
//
// Параметры:
//	Форма - УправляемаяФорма - форма, для элементов которой осуществляется настройка условного оформления;
//	ДополнительныеПараметры - Структура - см. НоменклатураСервер.ДополнительныеПараметрыУстановитьУсловноеОформлениеСодержания.
//
Процедура УстановитьУсловноеОформлениеСодержания(Форма, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыУстановитьУсловноеОформлениеСодержания();
	КонецЕсли;
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ДополнительныеПараметры.ИмяПоляВводаСодержания].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ДополнительныеПараметры.ПутьКПолюОтбораВариантаОформления);
	
	СписокТиповВариантыОформленияПродажи = Новый СписокЗначений;
	СписокТиповВариантыОформленияПродажи.Добавить(Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);
	СписокТиповВариантыОформленияПродажи.Добавить(Перечисления.ВариантыОформленияПродажи.АктНаПередачуПрав);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = СписокТиповВариантыОформленияПродажи;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<для работ, оформляемых актом>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", ПолучитьФункциональнуюОпцию("НеБазоваяВерсия"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ДополнительныеПараметры.ИмяПоляВводаСодержания].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ДополнительныеПараметры.ПутьКПолюОтбораВариантаОформления);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОформленияПродажи.АктВыполненныхРабот;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ДополнительныеПараметры.ПутьКПолюОтбораСодержания);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);	
	
	//

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ДополнительныеПараметры.ИмяПоляВводаУпаковки].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ДополнительныеПараметры.ПутьКПолюОтбораВариантаОформления);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОформленияПродажи.АктВыполненныхРабот;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
КонецПроцедуры

// Устанавливаем условное оформление номеров ГТД
//
// Параметры:
// 		Форма - Форма - Содержит данную форму
// 		ИмяПоляВводаНомераГТД - Строка - Наименование элемента формы, содержащего номер ГТД,
//									   		если оно отличается от "ТоварыНомерГТД"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "ВедетсяУчетПоГТД",
//										если он отличается от "Объект.Товары.ВедетсяУчетПоГТД".
//
Процедура УстановитьУсловноеОформлениеНомераГТД(Форма,
												ИмяПоляВводаНомераГТД = "ТоварыНомерГТД",
												ПутьКПолюОтбора = "Объект.Товары.ВедетсяУчетПоГТД") Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаНомераГТД].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<ГТД не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаНомераГТД].Имя);

	ГруппаОтбора1 = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗапретитьПоступлениеТоваровБезНомеровГТД");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Устанавливаем условное оформление для статусов указания серий
//
// Параметры:
//		Форма - Форма - Содержит данную форму
//		СерииВсегдаВТЧТовары - Булево - Истина, если у объекта нет специальной ТЧ для указания серий
//		ИмяПоляВводаСтатусаУказанияСерий - Строка - Наименование элемента формы, содержащего статус указания
//									   				серии номенклатуры,если оно отличается от "ТоварыСтатусУказанияСерий"
//		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "СтатусУказанияСерий",
//									если он отличается от "Объект.Товары.СтатусУказанияСерий".
//
Процедура УстановитьУсловноеОформлениеСтатусовУказанияСерий(Форма,
															СерииВсегдаВТЧТовары,	
													  		ИмяПоляВводаСтатусаУказанияСерий = "ТоварыСтатусУказанияСерий",
													 		ПутьКПолюОтбора = "Объект.Товары.СтатусУказанияСерий") Экспорт
	
	Если СерииВсегдаВТЧТовары Тогда
		Если Не ПолучитьФункциональнуюОпцию("ИспользованиеСерииНоменклатуры") Тогда
			Возврат;
		КонецЕсли;												
	КонецЕсли;
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаСтатусаУказанияСерий].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);	
	
КонецПроцедуры

// Конец области условного оформления

// Разбиение на товарные места сбор в набор

Процедура РазбитьТоварыПоТоварнымМестам(ТабличнаяЧастьТовары) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	УпаковкиЕдиницыИзмерения.Ссылка КАК ТоварноеМесто,
	|	УпаковкиЕдиницыИзмерения.КоличествоУпаковок КАК КоличествоТоварныхМест
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО (ВЫБОР
	|				КОГДА Номенклатура.НаборУпаковок = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
	|					ТОГДА УпаковкиЕдиницыИзмерения.Владелец = Номенклатура.Ссылка
	|				КОГДА Номенклатура.НаборУпаковок <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)
	|					ТОГДА УпаковкиЕдиницыИзмерения.Владелец = Номенклатура.НаборУпаковок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокНоменклатуры)
	|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
	|	И УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)";
	
	ТаблицаТоваров = ТабличнаяЧастьТовары.Выгрузить();
	Запрос.УстановитьПараметр("СписокНоменклатуры", ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"));
	НоменклатураИУпаковки = Запрос.Выполнить().Выгрузить();
	УпаковкиТоварныеМеста = НоменклатураИУпаковки.ВыгрузитьКолонку("ТоварноеМесто");
	
	Если НоменклатураИУпаковки.Количество() > 0 Тогда
		
		ТабличнаяЧастьТовары.Очистить();
		
		Для Каждого Строка Из ТаблицаТоваров Цикл
			Если УпаковкиТоварныеМеста.Найти(Строка.Упаковка) <> Неопределено Тогда
				ДобавитьИЗаполнитьСтроку(ТабличнаяЧастьТовары, Строка);
			Иначе
				УпаковкиНоменклатуры = НоменклатураИУпаковки.НайтиСтроки(Новый Структура("Номенклатура", Строка.Номенклатура));
				Если УпаковкиНоменклатуры.Количество() = 0 Тогда
					ДобавитьИЗаполнитьСтроку(ТабличнаяЧастьТовары, Строка);
				Иначе
					Для Каждого СтрокаУпаковка Из УпаковкиНоменклатуры Цикл
						НоваяСтрока = ДобавитьИЗаполнитьСтроку(ТабличнаяЧастьТовары, Строка);
						НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество * СтрокаУпаковка.КоличествоТоварныхМест;  
						НоваяСтрока.Упаковка = СтрокаУпаковка.ТоварноеМесто;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьКомплектностьТоварныхМест(ТабличнаяЧастьТоваров, Отказ, ПараметрыПроверки = Неопределено) Экспорт  
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ИнтеграцияЕГАИСПереопределяемый.ПараметрыПроверкиЗаполненияКоличества();
	КонецЕсли;
	
	ТаблицаТоваров = ТабличнаяЧастьТоваров.Выгрузить();
	
	ЕстьОшибки = Ложь;	
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Таблица.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Таблица.Серия КАК Справочник.СерииНоменклатуры) КАК Серия,
	|	&ПоляГруппировки,
	|	ВЫРАЗИТЬ(Таблица.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка,
	|	Таблица.Количество КАК Количество,
	|	Таблица.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	&УсловиеОтбораСтрокПроверкиКомплектности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	&ПоляГруппировки,
	|	Таблица.Упаковка КАК Упаковка,
	|	Таблица.Номенклатура.Наименование КАК НоменклатураПредставление,
	|	Таблица.Характеристика.Наименование КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Таблица.Серия) КАК СерияПредставление,
	|	СУММА(Таблица.Количество) КАК Количество,
	|	СУММА(Таблица.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаТоваров КАК Таблица
	|ГДЕ
	|	ЕСТЬNULL(Таблица.Упаковка.ТипУпаковки, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Серия,
	|	&ПоляГруппировки,
	|	Таблица.Упаковка,
	|	Таблица.Характеристика.Наименование,
	|	Таблица.Номенклатура.Наименование
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	&ПоляГруппировки,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	УпаковкиЕдиницыИзмерения.Ссылка КАК Упаковка,
	|	УпаковкиЕдиницыИзмерения.КоличествоУпаковок КАК КоличествоТоварныхМест
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО (УпаковкиЕдиницыИзмерения.Владелец = Номенклатура.Ссылка)
	|ГДЕ
	|	Номенклатура.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаТоваров.Номенклатура КАК Номенклатура
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров)
	|	И УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|	И УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхвеличин.Упаковка)
	|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка,
	|	Номенклатура.ЕдиницаИзмерения,
	|	УпаковкиЕдиницыИзмерения.Ссылка,
	|	УпаковкиЕдиницыИзмерения.КоличествоУпаковок
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО (УпаковкиЕдиницыИзмерения.Владелец = Номенклатура.НаборУпаковок)
	|ГДЕ
	|	Номенклатура.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаТоваров.Номенклатура КАК Номенклатура
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров)
	|	И УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|	И УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхвеличин.Упаковка)
	|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления";
	
	Если ПараметрыПроверки.УсловиеОтбораСтрокПроверкиКомплектности <> "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораСтрокПроверкиКомплектности", 
									СтрЗаменить(ПараметрыПроверки.УсловиеОтбораСтрокПроверкиКомплектности, ПараметрыПроверки.ИмяТЧ + ".", "Таблица."));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораСтрокПроверкиКомплектности", "ИСТИНА");
	КонецЕсли;
	
	ТекстПоляГруппировки = ПараметрыПроверки.ПоляГруппировкиПроверкиКомплектности;
	
	Если ТабличнаяЧастьТоваров.Количество() <> 0 Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТабличнаяЧастьТоваров[0], "Назначение") Тогда
			ПолеНазначение = ?(ЗначениеЗаполнено(ТекстПоляГруппировки), "Назначение,", "Назначение");
			ТекстПоляГруппировки = ПолеНазначение + ТекстПоляГруппировки;
		КонецЕсли;
	КонецЕсли;
	
	ТекстПоляГруппировки = СтрЗаменить(ТекстПоляГруппировки, " ", "");
	ТекстПоляГруппировки = СтрЗаменить(ТекстПоляГруппировки, ",", ",Таблица.");
	Если ЗначениеЗаполнено(ТекстПоляГруппировки) Тогда
		ТекстПоляГруппировки = "Таблица." + ТекстПоляГруппировки;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляГруппировки", ТекстПоляГруппировки);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляГруппировки,", "");
	КонецЕсли;
	
	МаксимальныйНомерПоля = СтрРазделить(ТекстПоляГруппировки, ",").Количество();
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Таблица", ТаблицаТоваров);
	РезультатЗапроса 		= Запрос.ВыполнитьПакет();
	ВыборкаПоНоменклатуре 	= РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТаблицаТоварныхМест 	= РезультатЗапроса[2].Выгрузить();
		
	Пока ВыборкаПоНоменклатуре.Следующий() Цикл
		ТоварныеМестаПоНоменклатуре = ТаблицаТоварныхМест.Скопировать(Новый Структура("Номенклатура", ВыборкаПоНоменклатуре.Номенклатура));
		Если ТоварныеМестаПоНоменклатуре.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ВыборкаПоХарактеристике = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
		Пока ВыборкаПоХарактеристике.Следующий() Цикл
			
			ПройтиРекурсивноПоПолямГруппировки(ВыборкаПоХарактеристике, ТоварныеМестаПоНоменклатуре, 1, МаксимальныйНомерПоля, ЕстьОшибки);
				
		КонецЦикла;
	КонецЦикла;
	
	Отказ = ?(ЕстьОшибки, Истина, Отказ);
				
КонецПроцедуры

Процедура ПройтиРекурсивноПоПолямГруппировки(Выборка, ТоварныеМестаПоНоменклатуре, НомерТекущегоПоля, МаксимальныйНомерПоля, ЕстьОшибки)
	
	Если НомерТекущегоПоля > МаксимальныйНомерПоля Тогда
		ПродолжитьОбходПоСериям(Выборка, ТоварныеМестаПоНоменклатуре, ЕстьОшибки);
	Иначе
		ВыборкаСледующегоУровня = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);			
		Пока ВыборкаСледующегоУровня.Следующий() Цикл
			ПройтиРекурсивноПоПолямГруппировки(ВыборкаСледующегоУровня, ТоварныеМестаПоНоменклатуре, НомерТекущегоПоля + 1, МаксимальныйНомерПоля, ЕстьОшибки); 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьОбходПоСериям(Выборка, ТоварныеМестаПоНоменклатуре, ЕстьОшибки)
	
	ЕстьПолеНазначение = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Выборка, "Назначение");
	
	ВыборкаПоСерии = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСерии.Следующий() Цикл 
		Выборка = ВыборкаПоСерии.Выбрать();
		
		КоличествоЦелыхНаборов = 999999999;
		МаксимальноеКоличествоНаборов = 0;
		
		Если Выборка.Количество() <> ТоварныеМестаПоНоменклатуре.Количество() Тогда
			КоличествоЦелыхНаборов = 0;
		КонецЕсли;
		
		КолонкаКоличествоВДокументе = ТоварныеМестаПоНоменклатуре.Колонки.Найти("КоличествоВДокументе");
		
		Если КолонкаКоличествоВДокументе = Неопределено Тогда
			ТоварныеМестаПоНоменклатуре.Колонки.Добавить("КоличествоВДокументе", Новый ОписаниеТипов("Число"));
		Иначе
			ТоварныеМестаПоНоменклатуре.ЗаполнитьЗначения(0, "КоличествоВДокументе");
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			НайденныеСтроки = ТоварныеМестаПоНоменклатуре.НайтиСтроки(Новый Структура("Упаковка", Выборка.Упаковка));
			Если НайденныеСтроки.Количество() > 0 Тогда 
				СтрокаТоварноеМесто = НайденныеСтроки[0];
			Иначе 
				Продолжить;
			КонецЕсли;
			
			КоличествоТоварныхМест = СтрокаТоварноеМесто.КоличествоТоварныхМест;
			КоличествоЦелыхНаборов = Мин(КоличествоЦелыхНаборов, Цел(Выборка.КоличествоУпаковок / КоличествоТоварныхМест));
			МаксимальноеКоличествоНаборовТекущее = ОкруглитьГрубо(Выборка.КоличествоУпаковок / КоличествоТоварныхМест);
			МаксимальноеКоличествоНаборов = Макс(МаксимальноеКоличествоНаборов, МаксимальноеКоличествоНаборовТекущее);
			
			СтрокаТоварноеМесто.КоличествоВДокументе = Выборка.КоличествоУпаковок;

		КонецЦикла;
		
		ОписанияНедостач = Новый Массив;
		ОписанияИзлишков = Новый Массив;
		
		Для Каждого ТоварноеМестоСтрока Из ТоварныеМестаПоНоменклатуре Цикл
			КоличествоИзлишек = ТоварноеМестоСтрока.КоличествоВДокументе - КоличествоЦелыхНаборов * ТоварноеМестоСтрока.КоличествоТоварныхМест;
			КоличествоНедостача = МаксимальноеКоличествоНаборов * ТоварноеМестоСтрока.КоличествоТоварныхМест - ТоварноеМестоСтрока.КоличествоВДокументе;
			Если КоличествоИзлишек > 0 Тогда
				ОписанияИзлишков.Добавить(Строка(КоличествоИзлишек) + Символы.НПП + ТоварноеМестоСтрока.Упаковка);					
			КонецЕсли;
			Если КоличествоНедостача > 0 Тогда
				ОписанияНедостач.Добавить(Строка(КоличествоНедостача) + Символы.НПП + ТоварноеМестоСтрока.Упаковка);
			КонецЕсли;
		КонецЦикла;
		
		Если ОписанияИзлишков.Количество() <> 0 Или ОписанияНедостач.Количество() <> 0 Тогда
			ЕстьОшибки = Истина;
			СообщениеПользователю = НСтр("ru = 'Из следущих товарных мест нельзя собрать целое количество %ЕдИзм% товара ""%Товар%"": %ОписаниеИзлишков%. Необходимо или удалить эти товарные места, или дополнить товарными местами: %ОписаниеНедостач%.'");
			ОписаниеИзлишков = СтрСоединить(ОписанияИзлишков, ", ");
			ОписаниеНедостач = СтрСоединить(ОписанияНедостач, ", ");
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%ОписаниеИзлишков%", 	ОписаниеИзлишков);
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%ОписаниеНедостач%", 	ОписаниеНедостач);
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%ЕдИзм%", 				ТоварныеМестаПоНоменклатуре[0].ЕдиницаИзмерения);
			Назначение = ?(ЕстьПолеНазначение, ВыборкаПоСерии.Назначение, "");
			ПредставлениеТовара = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
											ВыборкаПоСерии.НоменклатураПредставление,
											ВыборкаПоСерии.ХарактеристикаПредставление,
											, // Упаковка
											ВыборкаПоСерии.СерияПредставление,
											Назначение);
			СообщениеПользователю = СтрЗаменить(СообщениеПользователю, "%Товар%", ПредставлениеТовара);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет комплектность товарных мест в ордерах на отгрузку 
//
// Параметры:
//  Объект	 - 	ДокументОбъект.РасходныйОрдерНаТовары, ДокументОбъект.ОрдерНаПеремещениеТоваров - обрабатываемый документ.
//
Процедура ЗаполнитьДействиеПоКомплектностиТоварныхМест(Объект) Экспорт 
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОтгружаемыеТовары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ОтгружаемыеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение,
	|	ОтгружаемыеТовары.Серия,
	|	ВЫРАЗИТЬ(ОтгружаемыеТовары.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка,
	|	ОтгружаемыеТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ОтгружаемыеТовары.Действие
	|ПОМЕСТИТЬ ОтгружаемыеТовары
	|ИЗ
	|	&ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|ГДЕ
	|	&УсловиеПоУпаковочнымЛистам
	|	И ОтгружаемыеТовары.Действие <> ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгружаемыеТовары.Номенклатура,
	|	ОтгружаемыеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение,
	|	ОтгружаемыеТовары.Серия,
	|	ОтгружаемыеТовары.Упаковка,
	|	ОтгружаемыеТовары.Действие,
	|	СУММА(ОтгружаемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ОтгружаемыеТоварныеМеста
	|ИЗ
	|	ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|ГДЕ
	|	ЕСТЬNULL(ОтгружаемыеТовары.Упаковка.ТипУпаковки, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтгружаемыеТовары.Номенклатура,
	|	ОтгружаемыеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение,
	|	ОтгружаемыеТовары.Серия,
	|	ОтгружаемыеТовары.Упаковка,
	|	ОтгружаемыеТовары.Действие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Действие,
	|	УпаковкиЕдиницыИзмерения.Ссылка КАК ТоварноеМесто,
	|	УпаковкиЕдиницыИзмерения.КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварныеМестаНоменклатуры
	|ИЗ
	|	ОтгружаемыеТоварныеМеста КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО Товары.Номенклатура.НаборУпаковок = УпаковкиЕдиницыИзмерения.Владелец
	|ГДЕ
	|	НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
	|	И УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Действие,
	|	УпаковкиЕдиницыИзмерения.Ссылка,
	|	УпаковкиЕдиницыИзмерения.КоличествоУпаковок
	|ИЗ
	|	ОтгружаемыеТоварныеМеста КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО Товары.Номенклатура = УпаковкиЕдиницыИзмерения.Владелец
	|ГДЕ
	|	НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
	|	И УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварныеМестаНоменклатуры.Номенклатура,
	|	ТоварныеМестаНоменклатуры.Характеристика,
	|	ТоварныеМестаНоменклатуры.Назначение,
	|	ТоварныеМестаНоменклатуры.Серия,
	|	ТоварныеМестаНоменклатуры.Действие,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ОтгружаемыеТоварныеМеста.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок - (ВЫРАЗИТЬ(ЕСТЬNULL(ОтгружаемыеТоварныеМеста.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))) >= 0
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ОтгружаемыеТоварныеМеста.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))
	|			ИНАЧЕ (ВЫРАЗИТЬ(ЕСТЬNULL(ОтгружаемыеТоварныеМеста.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))) - 1
	|		КОНЕЦ) КАК КоличествоНаборов
	|ПОМЕСТИТЬ ТаблицаКоличествоНаборовОтгрузить
	|ИЗ
	|	ТоварныеМестаНоменклатуры КАК ТоварныеМестаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОтгружаемыеТоварныеМеста КАК ОтгружаемыеТоварныеМеста
	|		ПО ТоварныеМестаНоменклатуры.Номенклатура = ОтгружаемыеТоварныеМеста.Номенклатура
	|			И ТоварныеМестаНоменклатуры.Характеристика = ОтгружаемыеТоварныеМеста.Характеристика
	|			И ТоварныеМестаНоменклатуры.Назначение = ОтгружаемыеТоварныеМеста.Назначение
	|			И ТоварныеМестаНоменклатуры.Серия = ОтгружаемыеТоварныеМеста.Серия
	|			И ТоварныеМестаНоменклатуры.Действие = ОтгружаемыеТоварныеМеста.Действие
	|			И ТоварныеМестаНоменклатуры.ТоварноеМесто = ОтгружаемыеТоварныеМеста.Упаковка
	|ГДЕ
	|	ТоварныеМестаНоменклатуры.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварныеМестаНоменклатуры.Номенклатура,
	|	ТоварныеМестаНоменклатуры.Характеристика,
	|	ТоварныеМестаНоменклатуры.Назначение,
	|	ТоварныеМестаНоменклатуры.Серия,
	|	ТоварныеМестаНоменклатуры.Действие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнутреннийЗапрос.Номенклатура,
	|	ВнутреннийЗапрос.Характеристика,
	|	ВнутреннийЗапрос.Назначение,
	|	ВнутреннийЗапрос.Серия,
	|	ВнутреннийЗапрос.Действие,
	|	ВнутреннийЗапрос.Упаковка,
	|	СУММА(ВнутреннийЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТаблицаКоличествоУпаковокОтобрать
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтгружаемыеТоварныеМеста.Номенклатура КАК Номенклатура,
	|		ОтгружаемыеТоварныеМеста.Характеристика КАК Характеристика,
	|		ОтгружаемыеТоварныеМеста.Назначение КАК Назначение,
	|		ОтгружаемыеТоварныеМеста.Серия КАК Серия,
	|		ОтгружаемыеТоварныеМеста.Действие КАК Действие,
	|		ОтгружаемыеТоварныеМеста.Упаковка КАК Упаковка,
	|		ОтгружаемыеТоварныеМеста.КоличествоУпаковок - ЕСТЬNULL(ТаблицаКоличествоНаборовОтгрузить.КоличествоНаборов, 0) * ОтгружаемыеТоварныеМеста.Упаковка.КоличествоУпаковок КАК КоличествоУпаковок
	|	ИЗ
	|		ОтгружаемыеТоварныеМеста КАК ОтгружаемыеТоварныеМеста
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКоличествоНаборовОтгрузить КАК ТаблицаКоличествоНаборовОтгрузить
	|			ПО ОтгружаемыеТоварныеМеста.Номенклатура = ТаблицаКоличествоНаборовОтгрузить.Номенклатура
	|				И ОтгружаемыеТоварныеМеста.Характеристика = ТаблицаКоличествоНаборовОтгрузить.Характеристика
	|				И ОтгружаемыеТоварныеМеста.Назначение = ТаблицаКоличествоНаборовОтгрузить.Назначение
	|				И ОтгружаемыеТоварныеМеста.Серия = ТаблицаКоличествоНаборовОтгрузить.Серия
	|				И ОтгружаемыеТоварныеМеста.Действие = ТаблицаКоличествоНаборовОтгрузить.Действие
	|	ГДЕ
	|		ОтгружаемыеТоварныеМеста.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтгружаемыеТоварныеМеста.Номенклатура,
	|		ОтгружаемыеТоварныеМеста.Характеристика,
	|		ОтгружаемыеТоварныеМеста.Назначение,
	|		ОтгружаемыеТоварныеМеста.Серия,
	|		ОтгружаемыеТоварныеМеста.Действие,
	|		ОтгружаемыеТоварныеМеста.Упаковка,
	|		ОтгружаемыеТоварныеМеста.КоличествоУпаковок
	|	ИЗ
	|		ОтгружаемыеТоварныеМеста КАК ОтгружаемыеТоварныеМеста
	|	ГДЕ
	|		ОтгружаемыеТоварныеМеста.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать)) КАК ВнутреннийЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнутреннийЗапрос.Номенклатура,
	|	ВнутреннийЗапрос.Характеристика,
	|	ВнутреннийЗапрос.Назначение,
	|	ВнутреннийЗапрос.Серия,
	|	ВнутреннийЗапрос.Действие,
	|	ВнутреннийЗапрос.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварныеМестаНоменклатуры.Номенклатура,
	|	ТоварныеМестаНоменклатуры.Характеристика,
	|	ТоварныеМестаНоменклатуры.Назначение,
	|	ТоварныеМестаНоменклатуры.Серия,
	|	ТоварныеМестаНоменклатуры.Действие,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаКоличествоУпаковокОтобрать.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок - (ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаКоличествоУпаковокОтобрать.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))) >= 0
	|				ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаКоличествоУпаковокОтобрать.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))
	|			ИНАЧЕ (ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаКоличествоУпаковокОтобрать.КоличествоУпаковок, 0) / ТоварныеМестаНоменклатуры.КоличествоУпаковок КАК ЧИСЛО(12, 0))) - 1
	|		КОНЕЦ) КАК КоличествоНаборов
	|ПОМЕСТИТЬ ТаблицаКоличествоНаборовОтобрать
	|ИЗ
	|	ТоварныеМестаНоменклатуры КАК ТоварныеМестаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКоличествоУпаковокОтобрать КАК ТаблицаКоличествоУпаковокОтобрать
	|		ПО ТоварныеМестаНоменклатуры.Номенклатура = ТаблицаКоличествоУпаковокОтобрать.Номенклатура
	|			И ТоварныеМестаНоменклатуры.Характеристика = ТаблицаКоличествоУпаковокОтобрать.Характеристика
	|			И ТоварныеМестаНоменклатуры.Назначение = ТаблицаКоличествоУпаковокОтобрать.Назначение
	|			И ТоварныеМестаНоменклатуры.Серия = ТаблицаКоличествоУпаковокОтобрать.Серия
	|			И ТоварныеМестаНоменклатуры.Действие = ТаблицаКоличествоУпаковокОтобрать.Действие
	|			И ТоварныеМестаНоменклатуры.ТоварноеМесто = ТаблицаКоличествоУпаковокОтобрать.Упаковка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварныеМестаНоменклатуры.Номенклатура,
	|	ТоварныеМестаНоменклатуры.Характеристика,
	|	ТоварныеМестаНоменклатуры.Назначение,
	|	ТоварныеМестаНоменклатуры.Серия,
	|	ТоварныеМестаНоменклатуры.Действие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВнутренняяТаблица.Номенклатура,
	|	ВнутренняяТаблица.Характеристика,
	|	ВнутренняяТаблица.Назначение,
	|	ВнутренняяТаблица.Серия,
	|	ВнутренняяТаблица.Упаковка,
	|	ВнутренняяТаблица.КоличествоУпаковокВНаборе,
	|	ВнутренняяТаблица.Действие,
	|	СУММА(ВнутренняяТаблица.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтгружаемыеТоварныеМеста.Номенклатура КАК Номенклатура,
	|		ОтгружаемыеТоварныеМеста.Характеристика КАК Характеристика,
	|		ОтгружаемыеТоварныеМеста.Назначение КАК Назначение,
	|		ОтгружаемыеТоварныеМеста.Серия КАК Серия,
	|		ОтгружаемыеТоварныеМеста.Упаковка КАК Упаковка,
	|		ОтгружаемыеТоварныеМеста.Упаковка.КоличествоУпаковок КАК КоличествоУпаковокВНаборе,
	|		ОтгружаемыеТоварныеМеста.Действие КАК Действие,
	|		ЕСТЬNULL(ТаблицаКоличествоНаборовОтгрузить.КоличествоНаборов, 0) * ОтгружаемыеТоварныеМеста.Упаковка.КоличествоУпаковок КАК КоличествоУпаковок
	|	ИЗ
	|		ОтгружаемыеТоварныеМеста КАК ОтгружаемыеТоварныеМеста
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКоличествоНаборовОтгрузить КАК ТаблицаКоличествоНаборовОтгрузить
	|			ПО ОтгружаемыеТоварныеМеста.Номенклатура = ТаблицаКоличествоНаборовОтгрузить.Номенклатура
	|				И ОтгружаемыеТоварныеМеста.Характеристика = ТаблицаКоличествоНаборовОтгрузить.Характеристика
	|				И ОтгружаемыеТоварныеМеста.Назначение = ТаблицаКоличествоНаборовОтгрузить.Назначение
	|				И ОтгружаемыеТоварныеМеста.Серия = ТаблицаКоличествоНаборовОтгрузить.Серия
	|				И ОтгружаемыеТоварныеМеста.Действие = ТаблицаКоличествоНаборовОтгрузить.Действие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтгружаемыеТоварныеМеста.Номенклатура,
	|		ОтгружаемыеТоварныеМеста.Характеристика,
	|		ОтгружаемыеТоварныеМеста.Назначение,
	|		ОтгружаемыеТоварныеМеста.Серия,
	|		ОтгружаемыеТоварныеМеста.Упаковка,
	|		ОтгружаемыеТоварныеМеста.Упаковка.КоличествоУпаковок,
	|		ОтгружаемыеТоварныеМеста.Действие,
	|		ЕСТЬNULL(ТаблицаКоличествоНаборовОтобрать.КоличествоНаборов, 0) * ОтгружаемыеТоварныеМеста.Упаковка.КоличествоУпаковок
	|	ИЗ
	|		ОтгружаемыеТоварныеМеста КАК ОтгружаемыеТоварныеМеста
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКоличествоНаборовОтобрать КАК ТаблицаКоличествоНаборовОтобрать
	|			ПО ОтгружаемыеТоварныеМеста.Номенклатура = ТаблицаКоличествоНаборовОтобрать.Номенклатура
	|				И ОтгружаемыеТоварныеМеста.Характеристика = ТаблицаКоличествоНаборовОтобрать.Характеристика
	|				И ОтгружаемыеТоварныеМеста.Назначение = ТаблицаКоличествоНаборовОтобрать.Назначение
	|				И ОтгружаемыеТоварныеМеста.Серия = ТаблицаКоличествоНаборовОтобрать.Серия
	|				И ОтгружаемыеТоварныеМеста.Действие = ТаблицаКоличествоНаборовОтобрать.Действие) КАК ВнутренняяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнутренняяТаблица.Номенклатура,
	|	ВнутренняяТаблица.Характеристика,
	|	ВнутренняяТаблица.Назначение,
	|	ВнутренняяТаблица.Серия,
	|	ВнутренняяТаблица.Упаковка,
	|	ВнутренняяТаблица.КоличествоУпаковокВНаборе,
	|	ВнутренняяТаблица.Действие";
	
	УсловиеПоУпаковочнымЛистам = "НЕ ОтгружаемыеТовары.ЭтоУпаковочныйЛист";
	ЭтоОрдерНаПеремещение = ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров");
	Если ЭтоОрдерНаПеремещение Тогда
		УсловиеПоУпаковочнымЛистам = "ИСТИНА";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоУпаковочнымЛистам", УсловиеПоУпаковочнымЛистам);
	
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаТоваров = Объект.ОтгружаемыеТовары.Выгрузить();
	
	Запрос.УстановитьПараметр("ОтгружаемыеТовары", ТаблицаТоваров);
	ТаблицаКоличествоУпаковок = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваровОтобрать = ТаблицаТоваров.СкопироватьКолонки();
	ТаблицаТоваровНеОтгружать = ТаблицаТоваров.СкопироватьКолонки();
		
	СтрокиКУдалению = Новый Массив;
	Для Каждого Строка Из ТаблицаТоваров Цикл
		
		Если Строка.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать
			Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Упаковка, "ТипУпаковки") <> Перечисления.ТипыУпаковокНоменклатуры.ТоварноеМесто Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, Упаковка, Действие");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
		
		НайденныеСтроки = ТаблицаКоличествоУпаковок.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			КоличествоУпаковокРаспределить = Мин(НайденныеСтроки[0].КоличествоУпаковок, Строка.КоличествоУпаковок);
			Если КоличествоУпаковокРаспределить = 0 Тогда
				СтрокиКУдалению.Добавить(Строка);	
			КонецЕсли;
			НайденныеСтроки[0].КоличествоУпаковок = НайденныеСтроки[0].КоличествоУпаковок - КоличествоУпаковокРаспределить;
			
			НовоеДействие = Неопределено;
			
			Если Строка.КоличествоУпаковок > КоличествоУпаковокРаспределить Тогда // Оставшееся количество понижаем в действии
				
				Если Строка.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить Тогда
					НовоеДействие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
					ТаблицаРезерва = ТаблицаТоваровОтобрать;
				Иначе
					НовоеДействие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать;
					ТаблицаРезерва = ТаблицаТоваровНеОтгружать;
				КонецЕсли;
								
				НоваяСтрока = ТаблицаРезерва.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.КоличествоУпаковок = Строка.КоличествоУпаковок - КоличествоУпаковокРаспределить;
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок / НайденныеСтроки[0].КоличествоУпаковокВНаборе;
				НоваяСтрока.Действие = НовоеДействие;
				Строка.КоличествоУпаковок = КоличествоУпаковокРаспределить;
				Строка.Количество = КоличествоУпаковокРаспределить / НайденныеСтроки[0].КоличествоУпаковокВНаборе;
				
				СтруктураПоиска.Вставить("Действие", НовоеДействие);
				НайденныеСтрокиНовоеДействие = ТаблицаКоличествоУпаковок.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтрокиНовоеДействие.Количество() = 0 Тогда 
					НайденныеСтроки[0].Действие = НовоеДействие;
				КонецЕсли;
				
			КонецЕсли;
			
			Если НайденныеСтроки[0].КоличествоУпаковок = 0 И НайденныеСтроки[0].Действие <> НовоеДействие Тогда 
				ТаблицаКоличествоУпаковок.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаТоваров.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	СтрокиКУдалению.Очистить();
	Для Каждого Строка Из ТаблицаТоваровОтобрать Цикл
		
		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, Упаковка, Действие");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
		
		НайденныеСтроки = ТаблицаКоличествоУпаковок.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			КоличествоУпаковокРаспределить = Мин(НайденныеСтроки[0].КоличествоУпаковок, Строка.КоличествоУпаковок);
			Если КоличествоУпаковокРаспределить <> 0 Тогда
				НоваяСтрокаОбъекта = ТаблицаТоваров.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаОбъекта, Строка);
				НоваяСтрокаОбъекта.КоличествоУпаковок = КоличествоУпаковокРаспределить;
				НоваяСтрокаОбъекта.Количество = КоличествоУпаковокРаспределить / НайденныеСтроки[0].КоличествоУпаковокВНаборе;
			КонецЕсли;
			
			НайденныеСтроки[0].КоличествоУпаковок = НайденныеСтроки[0].КоличествоУпаковок - КоличествоУпаковокРаспределить;
			
			Если Строка.КоличествоУпаковок > КоличествоУпаковокРаспределить Тогда // Оставшееся количество понижаем в действии
				
				НоваяСтрока = ТаблицаТоваровНеОтгружать.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.КоличествоУпаковок = Строка.КоличествоУпаковок - КоличествоУпаковокРаспределить;
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок / НайденныеСтроки[0].КоличествоУпаковокВНаборе;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать;					
				
			КонецЕсли;
			
			Если НайденныеСтроки[0].КоличествоУпаковок = 0 Тогда 
				ТаблицаКоличествоУпаковок.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Для Каждого СтрокаНеотгружаемых Из ТаблицаТоваровНеОтгружать Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНеотгружаемых);
		Если Не ЭтоОрдерНаПеремещение Тогда
			НоваяСтрока.УпаковочныйЛистРодитель = Документы.УпаковочныйЛист.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЭтоОрдерНаПеремещение Тогда
		КолонкиГруппировок = "Номенклатура, Характеристика, Назначение, Серия, СтатусУказанияСерий, ЭтоУпаковочныйЛист, УпаковочныйЛистРодитель, УпаковочныйЛист, Упаковка, Действие";	
	ИначеЕсли ЭтоОрдерНаПеремещение Тогда 
		КолонкиГруппировок = "Номенклатура, Характеристика, Назначение, Серия, СтатусУказанияСерий, Упаковка, Действие";
	КонецЕсли;
	
	ТаблицаТоваров.Свернуть(КолонкиГруппировок, "Количество, КоличествоУпаковок");
	
	Объект.ОтгружаемыеТовары.Загрузить(ТаблицаТоваров);
			
КонецПроцедуры

// Конец области разбиения на товарные места сбор в набор

// Конец области программного интерфейса

// Служебные процедуры и функции

Процедура ВызватьИсключениеПоОшибкеШаблонаНаименования(ИмяОперанда, ИмяРеквизита = "")
	
	ШаблонСообщенияОбОшибке =
		НСтр("ru = 'Невозможно рассчитать наименование по шаблону.
				   |Ошибка в имени операнда ""%ИмяОперанда%"".'");
	ТекстСообщенияОбОшибке  =
		СтрЗаменить(ШаблонСообщенияОбОшибке, "%ИмяОперанда%", ИмяОперанда + ?(ИмяРеквизита = "", "", ":" + ИмяРеквизита));
	
	ВызватьИсключение ТекстСообщенияОбОшибке;
	
КонецПроцедуры

Функция ОкруглитьГрубо(Число)
	
	Если Цел(Число) <> Число Тогда
		Результат = Цел(Число) + 1;
	Иначе
		Результат = Число;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьИЗаполнитьСтроку(Таблица, СтрокаИсточник)
	НоваяСтрока = Таблица.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточник);
	Возврат НоваяСтрока;
КонецФункции

// Процедуры работы с сериями

Функция ВыборкаИзЗапросаРасчетаСтатусовУказанияСерий(ПараметрыУказанияСерий,
													ТаблицаТовары,
													ТаблицаСерии,
													Склад, //Если склады в ТЧ, то параметр игнорируется
													СтрокиТоваровДляОбработки = Неопределено,
													СтрокиСерийДляОбработки = Неопределено)
	
	МодульМенеджера = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
	
	ТекстЗапроса = МодульМенеджера.ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Если ТипЗнч(Склад) = Тип("Структура") Тогда
		Запрос.УстановитьПараметр("СкладОтправитель", Склад.Отправитель);
		Запрос.УстановитьПараметр("СкладПолучатель", Склад.Получатель);
	Иначе
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("СкладОтправитель", Склад);
		Запрос.УстановитьПараметр("СкладПолучатель", Склад);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаПоПараметрамУказанияСерий(Запрос,ПараметрыУказанияСерий);
	
	Если ТипЗнч(ТаблицаТовары) = Тип("ТаблицаЗначений") Тогда
		ТаблицаТоварыПараметрЗапроса = ТаблицаТовары;
	Иначе
		Если СтрокиТоваровДляОбработки <> Неопределено Тогда
			ТаблицаТоварыПараметрЗапроса = ТаблицаТовары.Выгрузить(СтрокиТоваровДляОбработки);
		Иначе
			ТаблицаТоварыПараметрЗапроса = ТаблицаТовары.Выгрузить();
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ТаблицаСерии) = Тип("ТаблицаЗначений") Тогда
		ТаблицаСерииПараметрЗапроса = ТаблицаСерии;
	Иначе
		Если СтрокиСерийДляОбработки <> Неопределено Тогда
			ТаблицаСерииПараметрЗапроса = ТаблицаСерии.Выгрузить(СтрокиСерийДляОбработки);
		Иначе
			ТаблицаСерииПараметрЗапроса = ТаблицаСерии.Выгрузить();
		КонецЕсли;
	КонецЕсли;
	
 	Запрос.УстановитьПараметр("Товары", ТаблицаТоварыПараметрЗапроса);
	Запрос.УстановитьПараметр("Серии", ТаблицаСерииПараметрЗапроса);
	
	УстановитьПривилегированныйРежим(Истина); // В перемещении товаров один из складов может быть недоступен пользователю
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ЗначенияРеквизитовДляЗаполненияПараметровУказанияСерий(Объект, ИменаРеквизитов)
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Объект)) Тогда
		Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, ИменаРеквизитов);
	Иначе
		Структура = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Структура, Объект);
	КонецЕсли;
	Если Структура.Свойство("Дата") И НЕ ЗначениеЗаполнено(Структура.Дата) Тогда
		Структура.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Структура.Свойство("ДатаОтгрузки") И НЕ ЗначениеЗаполнено(Структура.ДатаОтгрузки) Тогда
		Структура.ДатаОтгрузки = ТекущаяДатаСеанса();
	КонецЕсли;
	Возврат Структура;
	
КонецФункции

Процедура УстановитьПараметрыЗапросаПоПараметрамУказанияСерий(Запрос,ПараметрыУказанияСерий)
	
	Запрос.УстановитьПараметр("ОтгрузкаВРозницу", Ложь);
	Запрос.УстановитьПараметр("ОтгрузкаКлиенту", Ложь);
		
	Запрос.УстановитьПараметр("ОтгрузкаКомплектующихДляСборки", Ложь);
	
	Запрос.УстановитьПараметр("ОтгрузкаКомплектовДляРазборки", Ложь);
	
	Запрос.УстановитьПараметр("ОтгрузкаНаВнутренниеНужды", Ложь);

	Запрос.УстановитьПараметр("ОтгрузкаПоВозвратуПоставщику", Ложь);
	
	Запрос.УстановитьПараметр("ОтгрузкаПоПеремещению", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаОтПоставщика", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаПоВозвратуОтКлиента", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаПоПеремещению", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаПоПрочемуОприходованию", Ложь);            
	
	Запрос.УстановитьПараметр("ПриемкаСобранныхКомплектов", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаКомплектующихПослеРазборки", Ложь);
	
	Запрос.УстановитьПараметр("ОтражениеИзлишков", Ложь);
	
	Запрос.УстановитьПараметр("ОтражениеНедостач", Ложь);

	Запрос.УстановитьПараметр("ОтражениеПорчи", Ложь);
	
	Запрос.УстановитьПараметр("Пересчет", Ложь);
	
	Запрос.УстановитьПараметр("ОтражениеРезультатовПересчетов", Ложь);

	Запрос.УстановитьПараметр("ОтборИзЯчеек", Ложь);
	
	Запрос.УстановитьПараметр("КонтрольОтгрузки", Ложь);

	Запрос.УстановитьПараметр("РазмещениеВЯчейки", Ложь);

	Запрос.УстановитьПараметр("ПеремещениеМеждуПомещениями", Ложь);

	Запрос.УстановитьПараметр("ПриемкаПродукцииИзПроизводства", Ложь);
	
	Запрос.УстановитьПараметр("ПередачаВПроизводствоОтгрузка", Ложь);
	
	Запрос.УстановитьПараметр("ПередачаВПроизводствоПриемка", Ложь);
	
	Запрос.УстановитьПараметр("ОтгрузкаПоПеремещениюВПроизводстве", Ложь);
	
	Запрос.УстановитьПараметр("ПриемкаПоПеремещениюВПроизводстве", Ложь);
	
	Запрос.УстановитьПараметр("ВозвратМатериаловИзПроизводстваОтгрузка", Ложь);
	
	Запрос.УстановитьПараметр("СписаниеМатериаловНаЗатраты", Ложь);
	
	Запрос.УстановитьПараметр("ПотреблениеМатериаловПриПроизводстве", Ложь);
	
	Запрос.УстановитьПараметр("ВозвратМатериаловИзПроизводстваПриемка", Ложь);
	
	Для каждого ДопПараметр Из ПараметрыУказанияСерий.ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ДопПараметр.Ключ, ДопПараметр.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Дата", ПараметрыУказанияСерий.Дата);
	
	Запрос.УстановитьПараметр("Дата", ПараметрыУказанияСерий.Дата);
	Запрос.УстановитьПараметр("ТоварВШапке", ПараметрыУказанияСерий.ТоварВШапке);
	
	Запрос.УстановитьПараметр("ТолькоСерииСУчетомОстатков", ПараметрыУказанияСерий.ТолькоСерииСУчетомОстатков);
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеСерииСУчетомТовараВШапке(ДокументОбъект, ПараметрыУказанияСерий)
	
	ТЧТовары = Новый Массив;
	ТЧТовары.Добавить(ДокументОбъект);
	Если ПараметрыУказанияСерий.Свойство("Шапка") Тогда
		ОчиститьНеиспользуемыеСерииВТЧТовары(ДокументОбъект, ПараметрыУказанияСерий.Шапка, ТЧТовары);
	Иначе
		ОчиститьНеиспользуемыеСерииВТЧТовары(ДокументОбъект, ПараметрыУказанияСерий, ТЧТовары);
	КонецЕсли;	
	
	Если ПараметрыУказанияСерий.Свойство("ТЧ") Тогда
		ОчиститьНеиспользуемыеСерииВТЧТовары(ДокументОбъект, ПараметрыУказанияСерий.ТЧ, ДокументОбъект[ПараметрыУказанияСерий.ТЧ.ИмяТЧТовары]);	
	КонецЕсли;
		
	Если ПараметрыУказанияСерий.Свойство("ТЧ") Тогда
		ТаблицаТовары = ДокументОбъект[ПараметрыУказанияСерий.ТЧ.ИмяТЧТовары].Выгрузить();
	Иначе
		ТаблицаТовары = Новый ТаблицаЗначений;
		ТаблицаТовары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТовары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаТовары.Колонки.Добавить("СтатусУказанияСерий", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0, ДопустимыйЗнак.Неотрицательный)));
		
		Для каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(ДокументОбъект[СтрМас]));
			ТаблицаТовары.Колонки.Добавить(СтрМас, Новый ОписаниеТипов(МассивТипов));
		КонецЦикла;
	КонецЕсли;
		
	НоваяСтрока = ТаблицаТовары.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументОбъект);
	
	Если ПараметрыУказанияСерий.Свойство("ТЧ") Тогда
		ОчиститьНеиспользуемыеСерииВТЧСерии(ДокументОбъект, ПараметрыУказанияСерий.ТЧ, ТаблицаТовары);
	Иначе
		ОчиститьНеиспользуемыеСерииВТЧСерии(ДокументОбъект, ПараметрыУказанияСерий, ТаблицаТовары);
	КонецЕсли;	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеСерииВТЧТовары(ДокументОбъект, ПараметрыУказанияСерий, ТЧТовары)
	
	ЕстьСерииВТЧТовары = ТЧТовары.Количество() > 0
		И ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ТЧТовары[0], "Серия");
	
	Если ЕстьСерииВТЧТовары Тогда
		
		Для Каждого СтрТабл Из ТЧТовары Цикл
			
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				Если Не НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(СтрТабл.СтатусУказанияСерий, ПараметрыУказанияСерий)
					Или НоменклатураКлиентСервер.ВЭтомСтатусеСерииНеУказываются(СтрТабл.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
					
					СтрТабл.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
					
				КонецЕсли;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					Суффикс = НоменклатураКлиентСервер.СуффиксВИмениПоляСтатусУказанияСерий(ИмяПоляСтатус);
					
					Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(СтрТабл, "Серия" + Суффикс)
						И (НоменклатураКлиентСервер.ВЭтомСтатусеСерииНеУказываются(СтрТабл[ИмяПоляСтатус], ПараметрыУказанияСерий)
						Или Не НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(СтрТабл.СтатусУказанияСерий, ПараметрыУказанияСерий)) Тогда
						
						СтрТабл["Серия" + Суффикс] = Справочники.СерииНоменклатуры.ПустаяСсылка();
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеСерииВТЧСерии(ДокументОбъект, ПараметрыУказанияСерий, ТаблицаТовары)
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии
		Или ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	
	ТекстПоляСвязиТовары = "";
	ТекстПоляСвязиСерии = "";
	ТекстПоляСвязиСоединениеТоварыСерии = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязиТовары = ТекстПоляСвязиТовары + "
		|	ТаблицаТоваров." + СтрМас + ",";
		ТекстПоляСвязиСерии = ТекстПоляСвязиСерии + "
		|	ТаблицаСерий." + СтрМас + ",";
		ТекстПоляСвязиСоединениеТоварыСерии = ТекстПоляСвязиСоединениеТоварыСерии + "
		|			И ТаблицаТоваров."+СтрМас+" = ТаблицаСерий."+СтрМас;
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ 
	|	&ТекстПоляСвязиТовары,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТекстПоляСвязиСерии,
	|	ТаблицаСерий.НомерСтроки,
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика
	|ПОМЕСТИТЬ ТаблицаСерий
	|ИЗ
	|	&ТаблицаСерий КАК ТаблицаСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаСерий КАК ТаблицаСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваров
	|		ПО ТаблицаСерий.Номенклатура = ТаблицаТоваров.Номенклатура
	|			И ТаблицаСерий.Характеристика = ТаблицаТоваров.Характеристика
	|			И &ТекстПоляСвязиСоединениеТоварыСерии
	|ГДЕ
	|	НЕ ЕСТЬNULL(ТаблицаТоваров.СтатусУказанияСерий, 0) В (&СтатусыСерийУказываемыхВТЧСерии)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки УБЫВ";
	
	СтатусыСерийУказываемыхВТЧСерии = Новый Массив;
	СтатусыСерийУказываемыхВТЧСерии.Добавить(1);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(2);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(3);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(4);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(5);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(6);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(7);
	СтатусыСерийУказываемыхВТЧСерии.Добавить(8);
	Если ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии Тогда
		СтатусыСерийУказываемыхВТЧСерии.Добавить(9);
		СтатусыСерийУказываемыхВТЧСерии.Добавить(10);
	КонецЕсли;
	
	ЕстьСерииВТЧТовары = ТаблицаТовары.Количество() > 0
		И ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ТаблицаТовары[0], "Серия");
		
	Если Не ЕстьСерииВТЧТовары Тогда
		СтатусыСерийУказываемыхВТЧСерии.Добавить(13);
		СтатусыСерийУказываемыхВТЧСерии.Добавить(14);
	КонецЕсли;
	
	
	Запрос.УстановитьПараметр("ТаблицаТовары",ТаблицаТовары);
	Запрос.УстановитьПараметр("ТаблицаСерий",
			ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить());
	Запрос.УстановитьПараметр("СтатусыСерийУказываемыхВТЧСерии", СтатусыСерийУказываемыхВТЧСерии);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляСвязиТовары,", ТекстПоляСвязиТовары);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляСвязиСерии,", ТекстПоляСвязиСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстПоляСвязиСоединениеТоварыСерии", ТекстПоляСвязиСоединениеТоварыСерии);
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].Удалить(ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии][Выборка.НомерСтроки-1]);
		
	КонецЦикла;

КонецПроцедуры

// Настраивает отбор в параметрах по другим параметрам. Вызывается из обработки получения данных выбора
// и при создании формы выбора политик учета серий.
//
// Параметры:
//  Параметры - Структура - или параметры формы выбора политик учета серий, или параметр обработчика
//                          ПриПолученииДанныхВыбора модуля
//							менеджера политик учета серий.
//
Процедура НастроитьОтборВПараметрахПолитикУчетаСерий(Параметры) Экспорт
	
	ИспользоватьСерии = Ложь;
	
	Если Не Параметры.Свойство("ИспользоватьСерии", ИспользоватьСерии) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИспользоватьСерии Тогда
		Возврат;
	КонецЕсли;
	
	МассивДопустимыхТиповПолитик = ДопустимыеТипыПолитикУчетаСерий(Параметры);

	Параметры.Отбор.Вставить("ТипПолитики", Новый ФиксированныйМассив(МассивДопустимыхТиповПолитик));
	
	Склад = Неопределено;
	
	Параметры.Свойство("Склад", Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Если ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
			
			Параметры.Отбор.Вставить("ДляСклада", Истина);
			Параметры.Отбор.Вставить("УказыватьПриОтраженииИзлишков", Ложь);
			Параметры.Отбор.Вставить("УказыватьПриОтраженииНедостач", Ложь);
		КонецЕсли;
		
	Иначе
		Если Параметры.Свойство("ДляСклада") Тогда
			Параметры.Отбор.Вставить("ДляСклада", Истина);
			Параметры.Отбор.Вставить("УказыватьПриОтраженииИзлишков", Ложь);
			Параметры.Отбор.Вставить("УказыватьПриОтраженииНедостач", Ложь);
			
		ИначеЕсли Параметры.Свойство("ДляПроизводства")	Тогда
			Параметры.Отбор.Вставить("ДляПроизводства", Истина);
		Иначе
			
			Параметры.Отбор.Вставить("УказыватьПриОтраженииИзлишков", Ложь);
			Параметры.Отбор.Вставить("УказыватьПриОтраженииНедостач", Ложь);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ДопустимыеТипыПолитикУчетаСерий(Параметры)
	
	МассивДопустимыхТиповПолитик = Новый Массив;
	
	Если Не Параметры.ИспользоватьСерии Тогда
		Возврат МассивДопустимыхТиповПолитик;
	КонецЕсли;
	
	// Для отображения предопределенной политики "Серии не используются"
	МассивДопустимыхТиповПолитик.Добавить(Перечисления.ТипыПолитикУказанияСерий.ПустаяСсылка());
	
	Если Параметры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		
		Если Параметры.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара Тогда
			
			МассивДопустимыхТиповПолитик.Добавить(Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий);
			
		Иначе	
			
			МассивДопустимыхТиповПолитик.Добавить(Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий);
			МассивДопустимыхТиповПолитик.Добавить(Перечисления.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий);
			МассивДопустимыхТиповПолитик.Добавить(Перечисления.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям);
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивДопустимыхТиповПолитик;
	
КонецФункции

Процедура ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, Таблица, Знач Метаданные = Неопределено, ЭтоТаблицаСерий = Ложь) Экспорт
	
	Если Метаданные = Неопределено Тогда
		Метаданные = ИнтеграцияЕГАИСПереопределяемый.МетаданныеОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		МетаданныеРеквизитовТЧТовары = Метаданные.Реквизиты;
	Иначе
		МетаданныеРеквизитовТЧТовары = Метаданные.ТабличныеЧасти[ПараметрыУказанияСерий.ИмяТЧТовары].Реквизиты;
	КонецЕсли;	
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		Если Таблица.Колонки.Найти(СтрМас) = Неопределено Тогда
			Таблица.Колонки.Добавить(СтрМас,МетаданныеРеквизитовТЧТовары[СтрМас].Тип);
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоТаблицаСерий Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ИменаПолейДополнительные Цикл
		Если Таблица.Колонки.Найти(СтрМас) = Неопределено Тогда
			Таблица.Колонки.Добавить(СтрМас,МетаданныеРеквизитовТЧТовары[СтрМас].Тип);
		КонецЕсли;
	КонецЦикла;

	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		Если Таблица.Колонки.Найти("СтатусУказанияСерий") = Неопределено Тогда
			Таблица.Колонки.Добавить("СтатусУказанияСерий",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
		КонецЕсли;
	Иначе
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			Если Таблица.Колонки.Найти(СтрМас) = Неопределено Тогда
				Таблица.Колонки.Добавить(СтрМас,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСерииВТЧПоТаблицеСерий(Объект, ПараметрыУказанияСерий, Серии, Действия) Экспорт
	ТекстПоляСвязи = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас  ;
	КонецЦикла;
	СтруктураПоиска = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
	
	СвернутаяТаблицаТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить(,"Номенклатура,Характеристика"+ТекстПоляСвязи);
	СвернутаяТаблицаТоваров.Свернуть("Номенклатура,Характеристика"+ТекстПоляСвязи);
	
	Для Каждого СтрТабл Из СвернутаяТаблицаТоваров Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрТабл);
		
		НайденныеСтрокиСерий = Серии.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтрокиСерий.Количество() > 0 Тогда
			
			НайденныеСтрокиТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоиска);
			НомерПервойУдаляемойСтроки = 0;
			Для Каждого СтрМас Из НайденныеСтрокиТоваров Цикл
				
				Если НомерПервойУдаляемойСтроки = 0 Тогда
					НомерПервойУдаляемойСтроки = СтрМас.НомерСтроки;
				КонецЕсли;
				
				Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Удалить(СтрМас);	
				
			КонецЦикла;
			
			Индекс = 0;
			
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				Если НомерПервойУдаляемойСтроки = 0 Тогда
					НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Добавить();
				Иначе
					НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Вставить(НомерПервойУдаляемойСтроки - 1 + Индекс);
					Индекс = Индекс + 1;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрМас);
				НоваяСтрока.КоличествоУпаковок = СтрМас.Количество;
				ОбработкаТабличнойЧастиСерверЕГАИСУТ.ОбработатьСтрокуТЧ(НоваяСтрока, Действия, Неопределено);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
	
КонецПроцедуры

// Возвращает значение константы
//
// Параметры:
//  ТипИзмеряемойВеличиныСтрока	 - Строка - тип измеряемой величины строкой.
// 
// Возвращаемое значение:
//   СправочникСсылка.УпаковкиЕдиницыИзмерения 
//
Функция ЕдиницаИзмеренияПоУмолчанию(ТипИзмеряемойВеличиныСтрока) Экспорт
	
	Если ТипИзмеряемойВеличиныСтрока = "Вес" Тогда
		Значение = Константы.ЕдиницаИзмеренияВеса.Получить();
	ИначеЕсли ТипИзмеряемойВеличиныСтрока = "Объем" Тогда
		Значение = Константы.ЕдиницаИзмеренияОбъема.Получить();
	ИначеЕсли ТипИзмеряемойВеличиныСтрока = "Площадь" Тогда
		Значение = Константы.ЕдиницаИзмеренияПлощади.Получить();
	ИначеЕсли ТипИзмеряемойВеличиныСтрока = "Длина" Тогда 
		Значение = Константы.ЕдиницаИзмеренияДлины.Получить();
	ИначеЕсли ТипИзмеряемойВеличиныСтрока = "Штука" Тогда 
		Значение = Константы.ЕдиницаИзмеренияКоличестваШтук.Получить();
	ИначеЕсли ТипИзмеряемойВеличиныСтрока = "Розлив" Тогда 
		Значение = Константы.ЕдиницаИзмеренияРазливнойПродукции.Получить();
	Иначе
		Значение = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Значение;
КонецФункции

// Функция возращает строковое представление склада
//
// Параметры:
//  СкладПредставление		 - Строка - строковое представление склада
// 
// Возвращаемое значение:
//  Строка - строковое представление склада.
//
Функция ПолучитьПредставлениеСклада(СкладПредставление) Экспорт

	СтрПредставление = СокрЛП(СкладПредставление);

	Возврат СтрПредставление;

КонецФункции

Процедура ЗаполнитьСерииПоFEFOВТЧСерии(Объект, ПараметрыУказанияСерий)
	
    СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено
		Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено;
    
	ТекстЗапроса = ТекстЗапросаДляЗаполненияСерийПОFEFO(ПараметрыУказанияСерий);
	
	КлючевыеПоля = "Номенклатура,Характеристика";
    ПоляСвязиВТекстЗапроса = "";
    Для Каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
        КлючевыеПоля = КлючевыеПоля + "," + ПолеСвязи;
        ПоляСвязиВТекстЗапроса = ПоляСвязиВТекстЗапроса + "
        |ДанныеДокумента." + ПолеСвязи + ",";
    КонецЦикла;
    
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.ПоляСвязи,", ПоляСвязиВТекстЗапроса);
    
    ИмяПоляСклад = ПараметрыУказанияСерий.ИмяПоляСклад;
    
    Запрос = Новый Запрос;
    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
   	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    
    Если Не СкладыВТЧ Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Склад", "&Склад");
        Запрос.УстановитьПараметр("Склад",Объект[ИмяПоляСклад]);
    КонецЕсли;
    
    Запрос.Текст = ТекстЗапроса;
	ТаблицаСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить();
	Запрос.УстановитьПараметр("ТаблицаСерий", ТаблицаСерий);
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ТаблицаТоваровПодмена = ТаблицаСерий.СкопироватьКолонки();
		ТаблицаТоваровПодмена.Очистить();
		ТаблицаТоваровПодмена.Колонки.Добавить("СтатусУказанияСерий",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
		ЗаполнитьЗначенияСвойств(ТаблицаТоваровПодмена.Добавить(),Объект);
		Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваровПодмена);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	КонецЕсли;
    Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
    
    РезультатЗапроса = Запрос.ВыполнитьПакет();
    
    Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Загрузить(РезультатЗапроса[5].Выгрузить()); //Серии, которые не надо менять
    
    Выборка = РезультатЗапроса[4].Выбрать();
    СтруктураКоличества = Новый Структура("Количество,КоличествоУпаковок");
	ОстаткиПоСтрокамТоваров = Новый Соответствие;
	
	Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("Серия") Цикл
					ОстатокПоСерии = Выборка.СвободныйОстаток;
					КэшКлючевыхПолей = Новый Структура(КлючевыеПоля);
					Пока Выборка.Следующий() Цикл // Обход строк товаров
						Если ПараметрыУказанияСерий.ТоварВШапке Тогда
							ТекСтрока = Объект;
						Иначе
							ТекСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары][Выборка.НомерСтроки - 1];
						КонецЕсли;
						Если ОстаткиПоСтрокамТоваров[Выборка.НомерСтроки] = Неопределено Тогда
							ОстатокПоТовару = ТекСтрока.Количество;
						Иначе
							ОстатокПоТовару = ОстаткиПоСтрокамТоваров[Выборка.НомерСтроки];
						КонецЕсли;
						
						Если ОстатокПоТовару = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Если Не НоменклатураСервер.СтруктурыРавны(ТекСтрока,КэшКлючевыхПолей,КлючевыеПоля) Тогда
							ЗаполнитьЗначенияСвойств(КэшКлючевыхПолей,ТекСтрока);
							// Выборка упорядочена по ключевым полям,
							// поэтому изменение ключевых полей говорит нужно добавить новую строку серий.
							НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Добавить();
						КонецЕсли;
						
						ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока,,"Количество");
						НоваяСтрока.Серия = Выборка.Серия;
						ДобавляемоеКоличество = Мин(ОстатокПоТовару,ОстатокПоСерии);
						СтруктураКоличества.Количество         = НоваяСтрока.Количество + ДобавляемоеКоличество;
						СтруктураКоличества.КоличествоУпаковок = СтруктураКоличества.Количество;
						ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураКоличества); 
						
						ОстатокПоТовару = ОстатокПоТовару - ДобавляемоеКоличество;
						ОстаткиПоСтрокамТоваров.Вставить(Выборка.НомерСтроки,ОстатокПоТовару);
						
						Если ОстатокПоТовару = 0 Тогда
							ТекСтрока.СтатусУказанияСерий = 6;
							
							Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
								НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
										ТекСтрока[ИмяПоляСтатус],
										Истина,
										ДобавляемоеКоличество);
							КонецЦикла;

						КонецЕсли;
						ОстатокПоСерии = ОстатокПоСерии - ДобавляемоеКоличество;
						Если ОстатокПоСерии = 0 Тогда
							Прервать;
						КонецЕсли;
						ОстаткиПоСтрокамТоваров.Вставить(Выборка.НомерСтроки, ОстатокПоТовару)
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ЗаполнитьЗначенияСвойств(ТаблицаТоваровПодмена[0],Объект);
		ТЧТовары = ТаблицаТоваровПодмена;
	Иначе
		ТЧТовары = Объект[ПараметрыУказанияСерий.ИмяТЧТовары];
	КонецЕсли;
	НайденныеТовары = ТЧТовары.НайтиСтроки(Новый Структура("СтатусУказанияСерий",5));
	Если НайденныеТовары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоляСклад = ПараметрыУказанияСерий.ИмяПоляСклад;
	СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти(ИмяПоляСклад) <> Неопределено
		Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ИмяПоляСклад) <> Неопределено;
    
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура";
	
	ЕдиницыИзмерения = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из НайденныеТовары Цикл
		
		Если СкладыВТЧ Тогда
			ТекущийСклад = Стр.Склад;
		Иначе
			ТекущийСклад = Объект[ИмяПоляСклад];
		КонецЕсли;
		ЭтоПодразделение = Ложь;
		
		КоличествоНеРаспределено = 0;
		Если Стр.Количество = 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены. В табличной части ""Товары"" не указано количество.""'");
			
		ИначеЕсли ОстаткиПоСтрокамТоваров[Стр.НомерСтроки] = Неопределено Тогда 
			
			Если ЭтоПодразделение Тогда
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены.
					|В подразделении ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены.
					|На складе ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""'");
			КонецЕсли; 

			КоличествоНеРаспределено = Стр.Количество;
			
		Иначе
			
			Если ЭтоПодразделение Тогда
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии заполнены не полностью.
					|В подразделении ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""'");
			Иначе	
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии заполнены не полностью.
					|На складе ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""'");
			КонецЕсли; 
			КоличествоНеРаспределено = ОстаткиПоСтрокамТоваров[Стр.НомерСтроки];
			
		КонецЕсли;	
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеТовара%", НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Стр.Номенклатура, Стр.Характеристика));
		
		Если СкладыВТЧ Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеСклада%", ПолучитьПредставлениеСклада(ТекущийСклад));
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеСклада%", ПолучитьПредставлениеСклада(Объект[ИмяПоляСклад]));
		КонецЕсли;
			
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Количество%", КоличествоНеРаспределено);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ЕдиницаИзмерения%",ЕдиницыИзмерения.Найти(Стр.Номенклатура,"Номенклатура").ЕдиницаИзмерения);
		Если ПараметрыУказанияСерий.ТоварВШапке Тогда
			Поле = "Номенклатура";
		Иначе
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Стр.НомерСтроки, "Номенклатура");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект");
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСерииПоFEFOВТЧТовары(Объект, ПараметрыУказанияСерий)
    
	ТекстИсключения = НСтр("ru = 'Ошибка заполнения серий по FEFO. Процедура заполнения не поддерживает данный документ.'");
	ВызватьИсключение ТекстИсключения;
	
КонецПроцедуры

Функция ТекстЗапросаДляЗаполненияСерийПОFEFO(ПараметрыУказанияСерий)

	ТекстЗапросаТаблицаОстатковПоСериям =
	    "ВЫБРАТЬ
	    |	СерииТоваров.Номенклатура КАК Номенклатура,
	    |	СерииТоваров.Характеристика КАК Характеристика,
	    |	СерииТоваров.Склад,
	    |	СерииТоваров.Серия,
	    |	СУММА(СерииТоваров.СвободныйОстаток) КАК СвободныйОстаток
	    |ПОМЕСТИТЬ ТаблицаОстатковПоСериям
	    |ИЗ
	    |	(ВЫБРАТЬ
	    |		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	    |		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	    |		ТоварыНаСкладахОстатки.Серия КАК Серия,
	    |		ТоварыНаСкладахОстатки.Склад КАК Склад,
	    |		ТоварыНаСкладахОстатки.КоличествоОстаток КАК СвободныйОстаток
	    |	ИЗ
	    |		РегистрНакопления.ТоварыНаСкладах.Остатки(
	    |				,
	    |				(Номенклатура, Характеристика, Склад) В
	    |					(ВЫБРАТЬ
	    |						ТаблицаТоваровДляЗапроса.Номенклатура,
	    |						ТаблицаТоваровДляЗапроса.Характеристика,
	    |						ТаблицаТоваровДляЗапроса.Склад
	    |					ИЗ
	    |						ТаблицаТоваровДляЗапроса)) КАК ТоварыНаСкладахОстатки
	    |	
	    |	ОБЪЕДИНИТЬ ВСЕ
	    |	
	    |	ВЫБРАТЬ
	    |		ТоварыНаСкладах.Номенклатура,
	    |		ТоварыНаСкладах.Характеристика,
	    |		ТоварыНаСкладах.Серия,
	    |		ТоварыНаСкладах.Склад,
	    |		ВЫБОР
	    |			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	    |				ТОГДА -ТоварыНаСкладах.Количество
	    |			ИНАЧЕ ТоварыНаСкладах.Количество
	    |		КОНЕЦ
	    |	ИЗ
	    |		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	    |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровДляЗапроса КАК ТаблицаТоваровДляЗапроса
	    |			ПО ТоварыНаСкладах.Номенклатура = ТаблицаТоваровДляЗапроса.Номенклатура
	    |				И ТоварыНаСкладах.Характеристика = ТаблицаТоваровДляЗапроса.Характеристика
	    |				И ТоварыНаСкладах.Склад = ТаблицаТоваровДляЗапроса.Склад
	    |				И (ТоварыНаСкладах.Регистратор = &Ссылка)
		|	) КАК СерииТоваров
	    |ГДЕ
	    |	СерииТоваров.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	СерииТоваров.Номенклатура,
	    |	СерииТоваров.Характеристика,
	    |	СерииТоваров.Склад,
	    |	СерииТоваров.Серия
	    |
	    |ИМЕЮЩИЕ
	    |	СУММА(СерииТоваров.СвободныйОстаток) > 0";

	ТекстЗапроса =
	    "ВЫБРАТЬ
	    |	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	    |	ДанныеДокумента.Склад КАК Склад,
	    |	ДанныеДокумента.Номенклатура КАК Номенклатура,
	    |	ДанныеДокумента.Характеристика КАК Характеристика,
	    |	ДанныеДокумента.ПоляСвязи,
	    |	ДанныеДокумента.Количество КАК Количество
	    |ПОМЕСТИТЬ ТаблицаТоваров
	    |ИЗ
	    |	&ТаблицаТоваров КАК ДанныеДокумента
	    |ГДЕ
	    |	ДанныеДокумента.СтатусУказанияСерий В (5, 6)
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	ДанныеДокумента.Номенклатура КАК Номенклатура,
	    |	ДанныеДокумента.Характеристика КАК Характеристика,
	    |	ДанныеДокумента.Серия КАК Серия,
	    |	ДанныеДокумента.Склад КАК Склад,
	    |	ДанныеДокумента.ПоляСвязи,
	    |	ДанныеДокумента.Количество КАК Количество
	    |ПОМЕСТИТЬ ТаблицаСерий
	    |ИЗ
	    |	&ТаблицаСерий КАК ДанныеДокумента
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	ТаблицаТоваровДляЗапроса.Склад КАК Склад,
	    |	ТаблицаТоваровДляЗапроса.Номенклатура КАК Номенклатура,
	    |	ТаблицаТоваровДляЗапроса.Характеристика КАК Характеристика,
	    |	СУММА(ТаблицаТоваровДляЗапроса.Количество) КАК Количество
	    |ПОМЕСТИТЬ ТаблицаТоваровДляЗапроса
	    |ИЗ
	    |	ТаблицаТоваров КАК ТаблицаТоваровДляЗапроса
	    |ГДЕ
	    |	ТаблицаТоваровДляЗапроса.Количество > 0
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	ТаблицаТоваровДляЗапроса.Склад,
	    |	ТаблицаТоваровДляЗапроса.Номенклатура,
	    |	ТаблицаТоваровДляЗапроса.Характеристика
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |" + ТекстЗапросаТаблицаОстатковПоСериям + "
		|;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	ДанныеДокумента.Номенклатура,
		|	ДанныеДокумента.Характеристика,
	    |	ДанныеДокумента.Склад,
	    |	ДанныеДокумента.НомерСтроки,
	    |	ТаблицаОстатковПоСериям.Серия КАК Серия,
	    |	ЕСТЬNULL(ТаблицаОстатковПоСериям.СвободныйОстаток, 0) КАК СвободныйОстаток
	    |ИЗ
	    |	ТаблицаОстатковПоСериям КАК ТаблицаОстатковПоСериям
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ДанныеДокумента
	    |		ПО (ДанныеДокумента.Номенклатура = ТаблицаОстатковПоСериям.Номенклатура)
	    |			И (ДанныеДокумента.Характеристика = ТаблицаОстатковПоСериям.Характеристика)
	    |			И (ДанныеДокумента.Склад = ТаблицаОстатковПоСериям.Склад)
	    |
	    |УПОРЯДОЧИТЬ ПО
	    |	ДанныеДокумента.Номенклатура,
		|	ДанныеДокумента.Характеристика,
	    |	ДанныеДокумента.Склад,
	    |	ТаблицаОстатковПоСериям.Серия,
	    |	ДанныеДокумента.ПоляСвязи,
	    |	ДанныеДокумента.НомерСтроки
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	ДанныеДокумента.ПоляСвязи,
	    |	ДанныеДокумента.Номенклатура,
	    |	ДанныеДокумента.Характеристика,
	    |	ДанныеДокумента.Серия,
	    |	ДанныеДокумента.Количество
	    |ИЗ
	    |	ТаблицаСерий КАК ДанныеДокумента
	    |ГДЕ
	    |	НЕ (ДанныеДокумента.Номенклатура, ДанныеДокумента.Склад) В
	    |				(ВЫБРАТЬ
	    |					ТаблицаТоваров.Номенклатура,
	    |					ТаблицаТоваров.Склад
	    |				ИЗ
	    |					ТаблицаТоваров)";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СтрокаСоответствуетОтбору(СтрокаПриемник, ПараметрыОтбора)
	
	Для Каждого ПолеПроверки Из ПараметрыОтбора.РавноНулю Цикл
		// Значение поля проверки должно быть равно нулю
		Если СтрокаПриемник[ПолеПроверки] <> 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПолеПроверки Из ПараметрыОтбора.БольшеНуля Цикл
		// Значение поля проверки должно быть больше нуля
		Если СтрокаПриемник[ПолеПроверки] <= 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Вариант процедуры РаспределитьКоличество
// Переносит значение числового поля из таблицы источник в таблицу приемник.
// Заполняет произвольное поле ЗаполняемоеПоле, и если одной строке приемника соответствует несколько строк источника,
// отличающиеся лишь значением поля ЗаполняемоеПоле - дробит соответствующую строку приемника.
//
// Параметры:
//  ТаблицаИсточник			 - ТаблицаЗначений	 - 
//  ТаблицаПриемник			 - ТаблицаЗначений	 - 
//  ИмяПоляКоличество		 - Строка	 - Заполняемое поле. Если в таблице приемнике отсутствует - добавляется.
//  Измерения				 - Строка	 - Ключевые поля для поиска соответствий между строками таблиц
//  Ресурсы					 - Строка	 - Условия накладываемые на числовые поля строк таблицы приемника, вида:
//  									"НЕ [Количество], ПО [КоличествоВЗаказе], [КоличествоВЗаказе]"
//  									Где в квадратных скобках пишется имя числового поля в таблице приемнике,
//  									и перед ним идентификатор действия:
//  									НЕ [ИмяПоля] - значение ИмяПоля должно быть равно нулю
//  									ПО [ИмяПоля] - в строке приемнике в поле *ИмяПоляКоличество* нельзя записать число большее,
//  									чем в ИмяПоля.
//  									[ИмяПоля] - значение ИмяПоля должно быть больше нуля
//  РаспределятьПолностью	 - Булево	 - Если в результате действия ограничения условия "ПО", в строке источнике остается нераспределенное
//  			количество - разрешаем списать это количество в последнюю сопоставленную строку приемника
//  ЗаполняемыеПоля			 - Строка	 - Произвольные поля, значение которых нужно скопировать из строки источника в строку приемника.
//
Процедура РаспределитьКоличествоИЗаполнить(ТаблицаИсточник, ТаблицаПриемник, ИмяПоляКоличество, Измерения, Ресурсы, 
									РаспределятьПолностью = Ложь, ЗаполняемыеПоля = "") Экспорт
	
	Если ТаблицаПриемник.Колонки.Найти(ИмяПоляКоличество) = Неопределено Тогда
		ТаблицаПриемник.Колонки.Добавить(ИмяПоляКоличество, Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ПараметрыОтбораСтрок = ДействияСРесурсамиИзСтроки(Ресурсы);
	
	Отбор = Новый Структура(Измерения);
	
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаИсточник);
		НайденныеСтроки = ТаблицаПриемник.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаПриемник Из НайденныеСтроки Цикл
			
			ПроверкаПройдена = СтрокаСоответствуетОтбору(СтрокаПриемник, ПараметрыОтбораСтрок);
			
			Если Не ПроверкаПройдена Тогда
				Продолжить;
			КонецЕсли;
			
			// Если массив НеБольше - пустой, тогда переносится как есть
			// иначе происходит доп. проверка на то, что результат заполнения поля не превысит значения проверяемых полей.
			Количество = СтрокаИсточник[ИмяПоляКоличество];
			
			КоличествоПолейПроверки = ПараметрыОтбораСтрок.НеБольше.Количество();
			Если КоличествоПолейПроверки > 0 Тогда
				МаксимальноеКоличество = СтрокаПриемник[ПараметрыОтбораСтрок.НеБольше[0]];
				Для н = 1 По КоличествоПолейПроверки - 1 Цикл
					ПолеПроверки = ПараметрыОтбораСтрок.НеБольше[КоличествоПолейПроверки - н - 1];
					МаксимальноеКоличество = Мин(СтрокаПриемник[ПолеПроверки], МаксимальноеКоличество);
				КонецЦикла;
				
				Превышение = СтрокаПриемник[ИмяПоляКоличество] + Количество - МаксимальноеКоличество;
				Если Превышение >= Количество Тогда
					Количество = 0;
				ИначеЕсли Превышение > 0 Тогда
					Количество = Количество - Превышение;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			МассивЗаполняемыхПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ЗаполняемыеПоля, ",");
			
			ЗаполняемыеПоляРавны = Истина;
			Для Каждого ЗаполняемоеПоле Из МассивЗаполняемыхПолей Цикл
				Если СтрокаПриемник[ЗаполняемоеПоле] <> СтрокаИсточник[ЗаполняемоеПоле] Тогда
					ЗаполняемыеПоляРавны = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ЗаполняемыеПоля)
				И Не ЗаполняемыеПоляРавны
				И СтрокаПриемник[ИмяПоляКоличество] = 0 Тогда
				// Можно заполнять текущую строку Приемник
				ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник, ЗаполняемыеПоля);
				
				СтрокаПриемник[ИмяПоляКоличество] = СтрокаПриемник[ИмяПоляКоличество] + Количество;
				СтрокаИсточник[ИмяПоляКоличество] = СтрокаИсточник[ИмяПоляКоличество] - Количество;
				
			ИначеЕсли ЗначениеЗаполнено(ЗаполняемыеПоля)
				И Не ЗаполняемыеПоляРавны Тогда
				// В текущую строку Приемник уже записывались данные, нужно выделить новую строку
				НоваяСтрокаПриемник = ТаблицаПриемник.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаПриемник, СтрокаПриемник);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаПриемник, СтрокаИсточник, ЗаполняемыеПоля);
				
				Если КоличествоПолейПроверки > 0 Тогда
					ПолеКонтроляКоличества = ПараметрыОтбораСтрок.НеБольше[0];
					НоваяСтрокаПриемник[ПолеКонтроляКоличества] = НоваяСтрокаПриемник[ПолеКонтроляКоличества] - НоваяСтрокаПриемник[ИмяПоляКоличество];
					СтрокаПриемник[ПолеКонтроляКоличества] = СтрокаПриемник[ИмяПоляКоличество];
				КонецЕсли;
				
				НоваяСтрокаПриемник[ИмяПоляКоличество] = Количество;
				СтрокаИсточник[ИмяПоляКоличество] = СтрокаИсточник[ИмяПоляКоличество] - Количество;
				
			Иначе
				СтрокаПриемник[ИмяПоляКоличество] = СтрокаПриемник[ИмяПоляКоличество] + Количество;
				СтрокаИсточник[ИмяПоляКоличество] = СтрокаИсточник[ИмяПоляКоличество] - Количество;
			КонецЕсли;
			
		КонецЦикла;
		
		// Если было распределено не полное количество, записываем остаток в последнюю найденную строку.
		Если РаспределятьПолностью
			И СтрокаИсточник[ИмяПоляКоличество] > 0 
			И НайденныеСтроки.Количество() > 0 Тогда
			
			Количество = СтрокаИсточник[ИмяПоляКоличество];
			НайденнаяСтрока = НайденныеСтроки[НайденныеСтроки.Количество() - 1];
			
			Если Не СтрокаСоответствуетОтбору(НайденнаяСтрока, ПараметрыОтбораСтрок) Тогда
				Продолжить;
			КонецЕсли;
			
			НайденнаяСтрока[ИмяПоляКоличество] = НайденнаяСтрока[ИмяПоляКоличество] + Количество;
			СтрокаИсточник[ИмяПоляКоличество] = 0;
			
			Если ЗначениеЗаполнено(ЗаполняемыеПоля) Тогда
				ЗаполнитьЗначенияСвойств(НайденнаяСтрока, СтрокаИсточник, ЗаполняемыеПоля);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Конец области процедур работы с сериями

// Конец области служебных процедур и функции
