
//#Область ПрограммныйИнтерфейс

Процедура ОбработатьВводШтрихкода(Форма, ДанныеШтрихкода, КэшированныеЗначения) Экспорт
	
	Если ДанныеШтрихкода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатОбработкиШтрихкода = Форма.Подключаемый_ОбработатьВводШтрихкода(ДанныеШтрихкода, КэшированныеЗначения);
	
	ЗавершитьОбработкуВводаШтрихкода(Форма, РезультатОбработкиШтрихкода, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ЗавершитьОбработкуВводаШтрихкода(Форма, РезультатОбработкиШтрихкода, КэшированныеЗначения, ОповещениеПриЗавершении = Неопределено)
	
	Если ЗначениеЗаполнено(РезультатОбработкиШтрихкода.ТекстОшибки) Тогда
		
		ДанныеШтрихкода = Новый Структура;
		ДанныеШтрихкода.Вставить("АлкогольнаяПродукция", РезультатОбработкиШтрихкода.АлкогольнаяПродукция);
		ДанныеШтрихкода.Вставить("Штрихкод",             РезультатОбработкиШтрихкода.Штрихкод);
		ДанныеШтрихкода.Вставить("ТекстОшибки",          РезультатОбработкиШтрихкода.ТекстОшибки);
		ДанныеШтрихкода.Вставить("ТипШтрихкода",         РезультатОбработкиШтрихкода.ТипШтрихкода);
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ДанныеШтрихкода", ДанныеШтрихкода);
		
		ОткрытьФормуЕГАИС(
			"Обработка.ПроверкаИПодборАлкогольнойПродукцииЕГАИС.Форма.ИнформацияОНевозможностиДобавленияОтсканированного",
			ПараметрыОткрытияФормы,
			Форма,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли РезультатОбработкиШтрихкода.ЕстьОшибкиВДеревеУпаковок Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("АдресХранилищаДереваУпаковки", РезультатОбработкиШтрихкода.АдресДереваУпаковок);
		
		ОткрытьФормуЕГАИС(
			"Обработка.ПроверкаИПодборАлкогольнойПродукцииЕГАИС.Форма.ИнформацияОНевозможностиДобавленияОтсканированного",
			ПараметрыОткрытияФормы,
			Форма,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма",                       Форма);
		ДополнительныеПараметры.Вставить("РезультатОбработкиШтрихкода", РезультатОбработкиШтрихкода);
		ДополнительныеПараметры.Вставить("КэшированныеЗначения",        КэшированныеЗначения);
		ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении",     ОповещениеПриЗавершении);
		
		ОткрытьФормуЕГАИС(
			"Обработка.РаботаСАкцизнымиМаркамиЕГАИС.Форма.ФормаВводаАкцизнойМаркиПоискНоменклатуры",
			РезультатОбработкиШтрихкода.ПараметрыВыбораНоменклатуры,
			Форма,,,,
			ОписаниеОповещенияЕГАИС("ВыборНоменклатурыЗавершение", АкцизныеМаркиЕГАИСКлиент, ДополнительныеПараметры),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли РезультатОбработкиШтрихкода.ТребуетсяВыборСправки2 Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", РезультатОбработкиШтрихкода.Справки2);
		
		ПараметрыВыбораСправки2 = Новый Структура;
		ПараметрыВыбораСправки2.Вставить("РежимВыбора",        Истина);
		ПараметрыВыбораСправки2.Вставить("ЗакрыватьПриВыборе", Истина);
		ПараметрыВыбораСправки2.Вставить("Отбор",              Отбор);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма",                       Форма);
		ДополнительныеПараметры.Вставить("РезультатОбработкиШтрихкода", РезультатОбработкиШтрихкода);
		ДополнительныеПараметры.Вставить("КэшированныеЗначения",        КэшированныеЗначения);
		ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении",     ОповещениеПриЗавершении);
		
		ОткрытьФормуЕГАИС(
			"Справочник.Справки2ЕГАИС.ФормаВыбора",
			ПараметрыВыбораСправки2,
			Форма,,,,
			ОписаниеОповещенияЕГАИС("ВыборСправки2Завершение", АкцизныеМаркиЕГАИСКлиент, ДополнительныеПараметры),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли РезультатОбработкиШтрихкода.ТребуетсяОбработкаШтрихкода Тогда
		
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПослеОбработкиШтрихкодов", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

Процедура ВыборНоменклатурыЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатВыбора.Номенклатура) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не указана номенклатура'"));
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении = Неопределено Тогда
		
		РезультатОбработкиШтрихкода = Форма.Подключаемый_ОбработатьВыборНоменклатуры(
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода,
			ДополнительныеПараметры.КэшированныеЗначения);
		
		ЗавершитьОбработкуВводаШтрихкода(Форма, РезультатОбработкиШтрихкода, ДополнительныеПараметры.КэшированныеЗначения);
		
	Иначе
		
		ДанныеШтрихкода = АкцизныеМаркиВызовСервера.ОбработатьДанныеШтрихкодаПослеВыбораНоменклатуры(
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода);
		
		ВыполнитьОбработкуОповещенияЕГАИС(ДополнительныеПараметры.ОповещениеПриЗавершении, ДанныеШтрихкода);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыборСправки2Завершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении = Неопределено Тогда
		РезультатОбработкиШтрихкода = Форма.Подключаемый_ОбработатьВыборСправки2(
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода,
			ДополнительныеПараметры.КэшированныеЗначения);
		
		ЗавершитьОбработкуВводаШтрихкода(Форма, РезультатОбработкиШтрихкода, ДополнительныеПараметры.КэшированныеЗначения);
	Иначе
		
		ДанныеШтрихкода = АкцизныеМаркиВызовСервера.ОбработатьДанныеШтрихкодаПослеВыбораСправки2(
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода);
		
		ВыполнитьОбработкуОповещенияЕГАИС(ДополнительныеПараметры.ОповещениеПриЗавершении, ДанныеШтрихкода);
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает считанный ШК в зависимости от его формата.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - процедура, вызываемая при завершении обработки,
//  Форма - УправляемаяФорма - форма, в которой отсканирован штрихкод,
//  СтруктураШтрихкода - Структура - структура с ключами:
//   * Штрихкод - Строка - считанный штрихкод,
//   * Количество - Число - количество упаковок.
//  ПараметрыСканированияАкцизныхМарок - Структура - параметры обработки акцизной марки (см. АкцизныеМаркиКлиентСервер.ПараметрыСканированияАкцизныхМарок()).
//
Процедура ОбработатьДанныеШтрихкода(ОповещениеПриЗавершении, Форма, СтруктураШтрихкода, ПараметрыСканированияАкцизныхМарок = Неопределено) Экспорт
	
	Если ПараметрыСканированияАкцизныхМарок = Неопределено Тогда
		ПараметрыСканированияАкцизныхМарок = АкцизныеМаркиКлиентСервер.ПараметрыСканированияАкцизныхМарок(Форма);
	КонецЕсли;
	
	СписокШтрихкодов = Новый Массив;
	
	Для Сч = 1 По СтруктураШтрихкода.Количество Цикл
		СписокШтрихкодов.Добавить(СтруктураШтрихкода.Штрихкод);
	КонецЦикла;
	
	ДанныеШтрихкодов = АкцизныеМаркиВызовСервера.ПолучитьДанныеПоШтрихкодам(
		СписокШтрихкодов, ПараметрыСканированияАкцизныхМарок,
		Неопределено, Форма.УникальныйИдентификатор);
	
	РезультатОбработки = ДанныеШтрихкодов.РезультатыОбработкиШтрихкодов[СтруктураШтрихкода.Штрихкод];
	
	Если РезультатОбработки <> Неопределено Тогда
		
		Если РезультатОбработки.ТребуетсяВыборНоменклатуры
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма,"Номенклатура")
			И ЗначениеЗаполнено(Форма.Номенклатура) Тогда
			ДанныеШтрихкода = ПолучитьИзВременногоХранилища(РезультатОбработки.АдресДанныхШтрихкода);
			ЗаполнитьЗначенияСвойств(ДанныеШтрихкода,Форма,"Номенклатура,Характеристика");
			ПоместитьВоВременноеХранилище(ДанныеШтрихкода,РезультатОбработки.АдресДанныхШтрихкода);
			РезультатОбработки.ТребуетсяВыборНоменклатуры = Ложь;
		КонецЕсли;
		
		Если РезультатОбработки.ЕстьОшибкиВДеревеУпаковок
			Или Не ПустаяСтрока(РезультатОбработки.ТекстОшибки)
			Или РезультатОбработки.ТребуетсяВыборНоменклатуры
			Или РезультатОбработки.ТребуетсяВыборСправки2 Тогда
			ЗавершитьОбработкуВводаШтрихкода(Форма, РезультатОбработки, Неопределено, ОповещениеПриЗавершении);
		Иначе
			ДанныеШтрихкода = ПолучитьИзВременногоХранилища(РезультатОбработки.АдресДанныхШтрихкода);
			ВыполнитьОбработкуОповещенияЕГАИС(ОповещениеПриЗавершении, ДанныеШтрихкода);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Открыть форму считывания акцизной марки
//
// Параметры:
//  Результат - Булево - Не используется
//  ДополнительныеПараметры - Структура - Параметры открытия формы считывания марки: (Форма, ИдентификаторСтроки, Редактирование, АдресВоВременномХранилище)
//
Процедура ОткрытьФормуСчитыванияАкцизнойМарки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = Источник.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	
	Если Не ТекущиеДанные.МаркируемаяАлкогольнаяПродукция Тогда
		ПоказатьПредупреждениеЕГАИС(
			Неопределено,
			НСтр("ru = 'Для данной строки не указываются акцизные марки'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Номенклатура"  , ТекущиеДанные.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	
	ОткрытьФормуЕГАИС(
		"Обработка.РаботаСАкцизнымиМаркамиЕГАИС.Форма.ФормаВводаАкцизнойМарки",
		ПараметрыОткрытияФормы,
		ДополнительныеПараметры.Форма,,,,
		ОписаниеОповещенияЕГАИС("ПоискПоШтрихкодуЗавершение", ДополнительныеПараметры.Форма, ДополнительныеПараметры))
	
КонецПроцедуры

//#КонецОбласти